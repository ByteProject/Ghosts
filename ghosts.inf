!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $MAX_ABBREVS=96
!% $TRANSCRIPT_FORMAT=1
!% $ZCODE_LESS_DICT_DATA=1
!% -r
!% -e
! ----------------------------------------------------------------------------
!  THE GHOSTS OF BLACKWOOD MANOR
!  An interactive Horror by Stefan Vogt
!  Copyright (C) 2023 Fusion Retro Books / ZZAP!64 
!  http://8bitgames.itch.io
! ----------------------------------------------------------------------------

! IFID (http://babel.ifarchive.org) -- generate with https://uuidgenerator.net
Array UUID_ARRAY string "UUID://xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx//";
#Ifdef UUID_ARRAY; #Endif;

Include ">abbrvs_generic.h";

Constant Story "The Ghosts of Blackwood Manor";
Constant Headline "^An interactive Horror by Stefan Vogt^\
                    (C) 2023 Fusion Retro Books / ZZAP!64^";
Release 1;

! game-specific defines
Constant DEBUG; ! deactivate for release
Constant CUSTOM_ABBREVIATIONS;
Constant MAX_CARRIED = 20;
Constant OPTIONAL_SCORED;
Constant OPTIONAL_FULL_SCORE;
Constant OBJECT_SCORE = 3;
Constant ROOM_SCORE = 3;
Constant MAX_SCORE 10;
Constant OPTIONAL_FULL_DIRECTIONS;
!Constant OPTIONAL_MANUAL_REACTIVE;
!Constant OPTIONAL_MANUAL_SCOPE;
Constant INITIAL_LOCATION_VALUE = GreatHall;
#ifV5;
Constant OPTIONAL_PROVIDE_UNDO; ! undo opcode is only supported in Z-machine v5 spec
#endif;

! 0 production build, 1 notification on errors, 2 full error messages
Constant RUNTIME_ERRORS = 0;

! routine overrides
Replace PronounNotice;
#ifV5;
Replace DrawStatusLine; ! replace Puny's routine for Infocom interpreter compatibility
#endif;

Include "globals.h";

! Constants for puny extensions
Constant FLAG_COUNT 25;

! Puny extensions
Include "ext_quote_box.h";
Include "ext_flags.h";
Include "ext_talk_menu.h";

! recurring messages and conversations (self-defined)
Constant MESS_TURN_SELF "Now you feel dizzy. You've had better ideas than that.";
Constant MESS_SCENERY "You needn't worry about that.";
Constant MESS_DOORS "It's the kind of old wooden door you often see in or around Blackwood Manor.";

! messages (lib-overrides)
Constant MSG_PROMPT 1000;
Constant MSG_TAKE_ANIMATE 1001;
Constant MSG_EAT_ANIMATE 1002;
Constant MSG_PARSER_NO_INPUT 1003;
Constant MSG_PARSER_UNKNOWN_VERB 1004;
Constant MSG_PARSER_CANT_SEE_SUCH_THING 1005;
Constant MSG_PARSER_DONT_UNDERSTAND_WORD 1006;
Constant MSG_LOOK_BEFORE_ROOMNAME 1007;
Constant MSG_FILL_NO_WATER 1008;
Constant MSG_INSERT_NOT_CONTAINER 1009;
Constant MSG_EMPTY_CANT_CONTAIN 1010;
Constant MSG_OPEN_YOU_CANT 1011;
Constant MSG_PARSER_NO_NEED_REFER_TO 1012;
[LibraryMessages p_msg p_arg_1 p_arg_2;
 switch(p_msg) {
    MSG_PROMPT:
      print ">";
      p_arg_1 = p_arg_2;
    MSG_TAKE_ANIMATE, MSG_EAT_ANIMATE:
      "Do you think ", (the) noun, " would like that?";
    MSG_PARSER_NO_INPUT:
      "You won't get very far without input. You know that, right?^";
    MSG_PARSER_UNKNOWN_VERB:
      "You've used an unknown verb. Could you try something else?";
    MSG_PARSER_CANT_SEE_SUCH_THING:
      "You don't see that here.";
    MSG_PARSER_DONT_UNDERSTAND_WORD:
      print "This game understands many words but ";
			print "~";
			_PrintUnknownWord();
			print_ret "~ is unfortunately not one of them.";
    MSG_LOOK_BEFORE_ROOMNAME:
      @new_line; ! mimics Inform style location texts, disable for Infocom style
    MSG_FILL_NO_WATER:
      "That doesn't seem to work. Are you referring to an object that can be filled with liquid? Please refer to the usage of the fill command by typing [fill object].";
    MSG_INSERT_NOT_CONTAINER,
    MSG_EMPTY_CANT_CONTAIN:
      print_ret "Either this object can't contain any things or doing it won't help you progress in this game.";
    MSG_OPEN_YOU_CANT:
      "That doesn't seem to be something you can ", (verbname) p_arg_1, ".";
    MSG_PARSER_NO_NEED_REFER_TO:
      print_ret (string)MESS_SCENERY;
   }
 rfalse;
];

[PrintRank; ! this uses Puny's PrintRank feature
  print ", earning you the rank: ";
  if (score >= 10) "The Ten Commandments.";
  if (score >= 5) "High Five.";
  if (score >= 1) "The chosen One.";
  "Absolute Zero.";
];

Array quote_1 --> 8 33
"Towards new and different shores,"
"forever driven onward. Through"
"endless darkness, always borne" 
"away. Upon the sea of time can"
"we not lie at anchor, for but a"
"single day?"
"                               "
"       -- Alphonse de Lamartine";

! flags (in the form of globals, which is very memory efficient)
Global PROGRESS_LEVEL = 0; 
! level 1 = snowstorm is over

! will be triggered when the room is described
! [ LookRoutine;
! ];

! will be triggered when you visit a new room
! [ NewRoom;
! ];

Include "puny.h";

! set this for non-animate objects that may be referred to with "him"
Attribute male;

! limbo for objects that we don't need anymore, beware of the grues!
Object Limbo;

! classes
Class Room;      ! inside locations (walls in scope)
Class Outside;   ! outside locations

! walls will be an object we put in scope on demand
Object walls "walls"
  with name 'wall' 'walls//p',
      description "You check the walls without gaining any new insights.",
      before [;
          Attack:
            "There is surely a better way to deal with frustration.";
          Turn:
            "The walls? Are you serious?";
          Push, Pull:
            "The walls won't move.";
      ],
      found_in [;
            if (location ofclass Room) rtrue;
            if (location ofclass Outside) rfalse;
      ],
  has pluralname scenery;

! talk_menu array
Array talk_array -->
TM_NPC Rosie
0 300 "Herself" "Tell me more about yourself!" "I'm just an average girl."
30 "Weather" "How do you like the weather?" "It's too hot for me." 1 300
0 "Heat" "Say, don't you like hot weather?" "No, I prefer it cold."
TM_NPC 0;

Room GreatHall "Great Hall" ! -- locations start here
  with name 'great' 'hall' 'manor' 'blackwood' 'living' 'room',
      description [;
        print "This part of the manor is said to date back to the 8th century. Blackwood was built on the remains of an ancient monastery. Today, the Great Hall is used as a sitting room with a cosy Victorian interior. Flames dance in the fireplace and wood crackles as it burns slowly, giving off a pleasant warmth. Flickering light wanders across the walls, engulfing the shadows and illuminating the room in a melancholy twilight. In one corner, just below the gallery of ancestors, stands a beautifully decorated Christmas tree. Through the large windows you can see ";
        if (PROGRESS_LEVEL == 0) print "a snowstorm raging, the wind is howling as waves of snowflakes streak by. The only exit is to the north, leading to the vestibule.";
        else print "the manor's feral garden and the surrounding forest blanketed in a white layer.";
        new_line; ],
      n_to Vestibule,
  has light;
   
Object -> vicinterior,
  with name 'victorian' 'style' 'victorian-style' 'interior' 'furniture',
        description "Just as one would imagine an old country house to be. The lovingly handcrafted pieces of furniture do not seem to be replicas, although you first assumed so because they are in impeccable condition. Witnesses of a bygone era with an inestimable value.",
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Object -> fireplace,
  with name 'fireplace' 'fire' 'wood' 'flames' 'warmth' 'twilight' 'shadows' 'light',
        description "For a second you lose yourself in the flames, immersed in the eternal struggle between darkness and light. How many generations must have warmed themselves here over the centuries? What were their worries, their hardships, as they lingered within these venerable walls? For a brief moment we shine brighter than the morning star, but when the Grim Reaper knocks on our door, he leads us back to the infinite gloom from which we once emerged.",
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Object -> xmastree,
  with name 'corner' 'christmas' 'xmas' 'tree' 'decorated' 'scottish' 'fir' 'decorations',
        description "The tree, a Scottish fir, looks wonderful in its red and white Christmas decorations. Cora has really outdone herself this year.",
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Object -> ancestgallery,
  with name 'ancestors' 'gallery' 'portraits' 'lords' 'ladies' 'pictures',
        description "Fascinated, you look at the many portraits of the former Lords and Ladies of Blackwood. There must be some clues in the manor's library to find out who they depict. Judging by their clothes, however, they are all long gone. The way of all flesh.",
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Constant MESS_STORM "The window panes rattle from the force of the storm. Heavy snowflakes swirl in the air and blanket the manor's feral garden and the surrounding forest in a white layer.";

Object -> windowshall,
  with name 'large' 'windows' 'window' 'pane' 'panes',
        description [;
          if (PROGRESS_LEVEL == 0) print_ret (string)MESS_STORM;
          "You stand at the window, peering out into the winter landscape beyond. The world outside is covered in a thick blanket of snow.";
        ],
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Object -> snowstorm,
  with name 'snowstorm' 'wind' 'snowflakes' 'streaks',
        description [;
          print_ret (string)MESS_STORM;
        ],
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

  Object -> gardenfromwindow,
  with name 'feral' 'garden',
        description "You definitely have to take care of the garden next spring. It has apparently been neglected a bit in the last few years, but it is not completely overgrown.",
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

  Object -> forestfromwindow,
  with name 'surrounding' 'forest',
        description "The adjacent forest surely holds many secrets, some of which may one day be rediscovered, others will remain hidden forever, buried and forgotten witnesses to a dark past.",
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

  Object -> exitgreathall,
  with name 'exit' 'door',
      description [;
          print_ret (string)MESS_DOORS; 
      ],
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Room TheChapel "The Chapel"
  with name 'chapel',
        description "The silence is almost palpable, adding to the sense of reverence that pervades this sacred place. Your footsteps echo softly off the cold, rough stone walls. The occasional creak of old wood can be heard. Rows of pews stretch out before you. The large altar is the centrepiece here, drawing your eyes to it with its grandeur and attention to detail. Above it is a large wooden crucifix, its once gilded surface now dulled with age. Behind the altar are niches with stained glass windows depicting scenes from the Bible. The colours of the glass are muted and subdued, casting a soft, warm light on the chapel floor. One wonders how many prayers and moments of reflection the chapel has witnessed over the centuries. Doors to the south and southwest lead to the Oratory and Sacristy, while the exit to the north leads back to the vestibule.",
        n_to Vestibule,
        s_to Sacristy,
        sw_to Oratory,
        d_to Undercroft,
  has light;

Room Oratory "Oratory"
  with name 'oratory',
        description "The oratory was used by the Lord and his family for private prayer and contemplation. A wooden cross hangs from the wall, with a single pew in front of it. An alcove to the left catches the eye with a beautifully carved statue of a saint, surrounded by a small collection of candles and dried flowers. The saint is depicted with a serene expression, holding a book and a cross, the eyes gazing towards the heavens. A small plaque beside the statue reads ~St. Margaret of Scotland.~ An exit to the north leads back to the chapel.",
        n_to TheChapel,
  has light;

Room Vestibule "Vestibule"
  with name 'vestibule',
        description "As you enter the vestibule, you can't help but feel a sense of awe. The high ceiling is supported by grand columns and arches, and the walls are decorated with intricate carvings and tapestries. The room is dimly lit by bulbs in wall sconces, casting long shadows across the stone floor. To the west, you can see an enormous wooden door leading to the outside, its surface polished by centuries of use. To the left of the door, a stone staircase spirals upwards to the upper levels of the manor.",
        n_to Library,
        s_to GreatHall,
        e_to Kitchen,
        w_to [;
          if (PROGRESS_LEVEL == 0) "A snowstorm is raging outside. You decide that it's not a good time to leave the manor now.";
          if (PROGRESS_LEVEL == 1) "You feel it's not a good idea to leave the manor in the middle of the night, so you decide to stay inside.";
          return Forecourt;
        ],
        sw_to TheChapel,
        u_to Corridor,
  has light;

Object -> Rosie "Rosie",
  with name 'Rosie' 'housekeeper',
    initial "Rosie is here.",
    description "Rosie has been Blackwood's housekeeper for many years, she already served the former owner of the manor. Despite her age, her eyes are sharp and she seems to notice every detail. Her hair is pulled back in a tight bun and she always has a smile on her face.",
    talk_start 0,
  has proper animate female;

Room Sacristy "Sacristy"
  with name 'sacristy',
        description "You are in a small, dimly lit room. The walls are lined with wooden shelves filled with old, leather-bound books. The smell of incense hangs heavily in the air. In the centre of the sacristy is a large wooden table covered with a green velvet cloth. On the table you can see a chalice, a censer and a paten, all of which appear to be made of gold. You notice a chest with iron fittings containing a set of richly embroidered silk vestments and a small cabinet with an elaborate painting on the front (Death Scene of Golgotha). It contains the oil and balm used to anoint the sick and dying. A single exit to the north leads back to the chapel.",
        n_to TheChapel,
  has light;

Room Kitchen "Kitchen"
  with name 'kitchen',
        description "As you enter the kitchen, you are immediately transported back to a bygone era. Although modernised, the kitchen retains its old world charm and historic character that reflects its storied past. The vast space is filled with sturdy oak cupboards, shelves and drawers. The fireplace, once the main source of heat and cooking, has been replaced by a modern cooker, but the original mantelpiece and hearth remain as a reminder of the roaring fire that once burned here. The heavy flagstone floors show the wear and tear of years of use. The rough-hewn stone worktops add to the old-world charm of the kitchen, while the electric lights brighten the room. It is easy to imagine the hustle and bustle of the staff as they worked to create the elaborate dishes that would have been served to the lords and ladies of the manor. There are exits to the north, south, east and west.",
        n_to Storeroom,
        s_to Buttery,
        e_to Pantry,
        w_to Vestibule,
  has light;

Room Pantry "Pantry"
  with name 'pantry',
        description "The purpose of the pantry in medieval times was to store food supplies and ensure that the manor had enough provisions to get through the winter months. Pantries are usually located on the east side of a manor house, where they are least exposed to the sun. What you see here is the juxtaposition of modern appliances and antique stone walls. Tall shelves run from the floor to the ceiling, lined with jars, baskets and trays of food. On the left-hand side of the room, a commercial fridge stands tall, its stainless steel finish smooth and its humming unobtrusive. You open the door to find it stocked with a variety of fresh vegetables, meat and dairy products. To the right is a long wooden table covered with an array of breads. There are traditional Scottish oatcakes, crusty baguettes and loaves of sourdough bread that Rosie, the housekeeper, made before leaving for her Christmas holiday. A door to the east leads back to the kitchen.",
        w_to Kitchen,
  has light;

Room Buttery "Buttery"
  with name 'buttery',
        description "The first thing you notice is the smell of alcohol. Wooden shelves are lined with bottles of various sizes, shapes and colours, each containing a different type of Scotch whisky. The shelves are dimly lit by candlelight, casting a warm glow over the amber liquid inside. In one corner, there's a cask of ale that you've been dying to try over the holidays. In the centre of the room, a small wooden table is littered with empty glasses. You love being here, soaking up the atmosphere. It's cosy, warm and inviting, but there's also a sense of history and tradition that's as intoxicating as the booze itself. Go north to get back to the kitchen.",
        n_to Kitchen,
  has light;

Room Storeroom "Storeroom"
  with name 'storeroom',
        description "You're struck by the musty smell and the dim lighting. The storeroom is cluttered with various items, some of which appear to be decades old. You see various boxes and containers, a wooden crate marked ~fragile~, and a dusty old typewriter. In front of you is a large chest of drawers with a mirror on top. The drawers are closed, but you can see that they're labelled with various items such as ~linen~, ~candles~ and ~cutlery~. On the floor you can spot an old-fashioned sewing machine and a pile of fabric scraps. On the right, there's a metal shelf with various cleaning supplies, a vacuum cleaner, a bucket with a mop, a broom and a selection of cleaning products. Above the shelf is a row of coat hangers with some old jackets and hats. You can leave by heading south.",
        s_to Kitchen,
  has light;

Room Library "Library"
  with name 'library',
        description "You are impressed by the grandeur of Blackwood's Library. The walls are lined with bookshelves that reach to the ceiling, filled with books of all shapes and sizes. Many appear to be very old, their bindings faded with time. The shelves themselves are made of dark, polished wood and have a warm, inviting feel to them. The air is thick with the scent of leather and old paper, a smell that immediately transports you to another time and place. A large wooden table is covered with old manuscripts and parchments, surrounded by a row of high-backed leather chairs, some of which look as if they've been there for centuries. One wonders what secrets hide here about the history of the manor and the surrounding area. A door to the south leads back to the vestibule.",
        s_to Vestibule,
  has light;

Outside Forecourt "Forecourt"
  with name 'forcourt',
        description "The forecourt is surrounded by a low stone wall and in its centre is a large stone fountain decorated with carvings of Celtic knots and ancient Gaelic symbols. The walls of Blackwood are of rough-hewn stone, covered with moss and ivy. The entrance to the east is a massive wooden door with iron hinges, above which hangs a coat of arms bearing the image of a stag, a symbol of Scotland's ancient kings. Near the manor house is a weathered stone bench overlooking a small pond with a stone bridge leading to a feral garden to the northeast. The air is cool and damp and you can hear the sound of a nearby stream as it meanders through the snowy, lush landscape of the surrounding forest.",
        e_to Vestibule,
        ne_to FeralGarden,
  has light;

Outside FeralGarden "Feral Garden"
  with name 'feral' 'garden',
        description "You are standing admidst barren, snow-covered thornbushes and frost-covered grass. Vines and thorny brambles creep up the walls of the manor house. To the west is an old dovecote, its structure weathered but still standing. A trail to the north leads deeper into the forest, disappearing into the shadows of the trees. You can hear the crunch of snow under your feet and the distant wail of the wind. The scent of the winter air fills your senses with the crisp, refreshing aroma of the season. A cobbled path leads back southwest to the forecourt.",
        n_to ForestTrail,
        w_to Dovecote,
        sw_to Forecourt,
  has light;

Outside ForestTrail "Forest Trail"
  with name 'forest' 'trail',
        description "Snow-covered trees tower above you, their branches creaking and swaying in the frigid wind. The trail ahead of you is barely visible, winding its way further northwest through the thick undergrowth and disappearing into the dense woods. The trail is narrow, flanked on either side by a wall of gnarled trees and prickly bushes, their leafless branches stretching out like skeletal fingers. It's very quiet except for the occasional rustle of a small animal darting through the underbrush. Further south, the path leads back to the feral garden of Blackwood Manor.",
        s_to FeralGarden,
        nw_to Ruins,
  has light;

Room Dovecote "Dovecote"
  with name 'dovecote',
        description "As you look around the dovecote, you notice that it has been repurposed as a garden shed. The inside is cluttered with garden tools, bags of soil and pots of various sizes. You can see that the nesting holes have been blocked off, but the faint scent of bird droppings still lingers in the air. Dovecotes were a common feature of Scottish manor and castle estates and served a number of purposes. One was to provide a source of fresh meat and eggs for the lord and his family, but it was also a symbol of status and wealth. A door to the east leads back into the garden.",
        e_to FeralGarden,
  has light;

Outside Ruins "Ruins"
  with name 'ruins' 'village',
        description "You see the ruins of a small village, with only the overgrown stone remains of what were once two or three houses. The ruins are ancient and weathered, covered in a thick layer of snow and ice. The walls are crumbling, the arches distorted and twisted by years of decay. The village must have been abandoned centuries ago. The only sounds you can hear are the soft crunching of the snow beneath your feet and the distant howling of the wind. The ruins seem to be in a deep slumber, frozen in time. Strange shapes and shadows lurk in the corners of your vision, making you wonder if you're really alone in this place. The trail heads north and south from here.",
        n_to OverGrownCemetery,
        se_to ForestTrail,
        d_to Cellar,
  has light;

Room Cellar "Cellar"
  with name 'cellar',
        description "The musty air is thick with the smell of decay, and the dim light of your flashlight reveals cobwebbed corners and forgotten debris. You can hear the scurrying of rats and the faint dripping of water in the distance. As you explore further, you notice strange symbols etched into the walls. Old wooden barrels and rusted tools are scattered about the cellar floor. Some barrels appear to be intact, while others have long since rotted, spilling their contents onto the floor. You can hear whispers, but you can't make out what they're saying. It could be a language you've never heard before. In a corner is a dark alcove where a mysterious ancient tome glimmers in the light. Narrow, creaky stairs lead up to the ruins of the abandoned village.",
        u_to Ruins,
   has  ~light;

Outside OverGrownCemetery "Overgrown Graveyard"
  with name 'overgrown' 'graveyard',
        description "The winter sun casts a pale light over the graveyard. The entrance gate is rusted and half-broken, with overgrown ivy vines creeping over its once-sturdy frame. The gravestones, some standing upright and others toppled over, are covered in thick moss and lichen. They are so old and weathered that it is difficult to read the inscriptions, but you can make out some of the names and dates carved into the stone. It seems like nobody's been buried here after 1597, which gives you an idea about how long the village to the south is abandoned. It's unusually quiet, as if the whole forest prevents this forgotten ground. The silence is so complete that you can hear your own breathing, and every footstep you take seems to echo through the stillness.",
        s_to Ruins,
  has light;

Room Undercroft "Undercroft"
  with name 'undercroft',
        description "Your flashlight illuminates ancient stone walls that seem to stretch into infinity. The architecture is imposing and grand, with soaring arches and vaulted ceilings. In the dim light, you can make out rusted weapons and tools hanging on the walls - longswords and halberds that once saw battle, pikes and spears that once defended the manor, and maces and morning stars that could crush a man's skull with a single blow. You also notice old tapestries and flags hanging from the walls, faded and tattered with time. They depict battles and heroic deeds, and you wonder how many of the people depicted in them might have once walked the halls of the manor. In one corner you see an ancient suit of armour, its once shining metal now covered in rust and cobwebs. You can almost hear the ghostly clanking of its joints as it marches off to war. Stone steps lead up to the chapel. A door leads to the northeast.",
        e_to SecretPassage,
        ne_to Crypt,
        u_to TheChapel,
   has  ~light;

Room SecretPassage "Secret Passage"
  with name 'secret' 'passage',
        description "As you make your way through the narrow passage, you notice that the walls are rough and uneven, carved out of solid stone. There is a heavy smell of damp earth and moss in the air. You can hear the sound of water dripping from the walls and ceiling, forming small puddles on the uneven floor. The tunnel twists and turns, curving around sharp corners, occasionally sloping down or up, stretching further east and west from here.",
        e_to Dungeon,
        w_to Undercroft,
   has  ~light;

Room Crypt "Crypt"
  with name 'crypt',
        description "The smell of decay fills the air. You can barely make out the outlines of several stone coffins, neatly arranged in rows around the perimeter of the room, elaborately decorated with wonderful carvings and engravings. One coffin has been opened, revealing the remains of an ancient shroud and the faint outline of bones. To the west is a large door leading back to the undercroft. The ceiling is vaulted and made up of large stone slabs, some of which have cracked over time. You can also see rusted iron sconces on the walls, some of which still bear the remnants of long extinguished candles.",
        s_to Undercroft,
  has light;

Room Dungeon "Dungeon"
  with name 'dungeon',
        description "As you peer into the dungeon, you feel a chill run down your spine. The air is thick with the stench of decay. The room is small and claustrophobic, with low ceilings and walls that seem to press in on you from all sides. As your eyes adjust to the gloom, you see a set of chains dangling from the ceiling in one corner. They creak softly in the silence, as if beckoning you closer. Moving towards them, you catch sight of a wooden table covered in a macabre array of torture tools. A pair of pincers lies beside a rack, while a branding iron glows ominously in the flashlight. You can't help but notice the stains which you suspect to be dry blood that mar the surface of the table, hinting at the unspeakable horrors that have taken place here. The very air itself seems to vibrate with a dark energy, as if the room is alive with malevolence. A cell door with rusty but solid metal bars to the east leads back to the passage from which you came. Someone spilled a line of white crystalline powder in front of it.",
        w_to SecretPassage,
   has  ~light;

Room Corridor "Grand Hallway"
  with name 'corridor',
        description "You are standing in Blackwood's grand hallway, with fine tapestries and beautiful oil paintings on the walls, depicting landscapes and scenes from a bygone era. To the north, an old wooden door leads to the lounge. To the south, you get to the Solar. The bathroom is to the northwest. As you walk along the patterned carpet runner, you can feel the soft, plush fibres under your feet and notice that the design is in the repeating pattern of a Scottish tartan. The borders of the carpet are decorated with delicate flowers and vines. A spiral stone staircase leads down to the vestibule.",
        n_to Lounge,
        s_to TheSolar,
        nw_to Bathroom,
        d_to Vestibule,
  has light;

Room TheSolar "The Solar"
  with name 'solar',
        description "You find yourself in the solar, a grand chamber that was once the private quarters of the lord and lady of the manor. The room is filled with the trappings of luxury and refinement, from the rich tapestries that adorn the walls to the antique furniture. In one corner of the room is a large four-poster bed with heavy velvet curtains drawn around it. Next to the bed is a small bedside table with a flickering oil lamp. There are also a few modern items scattered around the room. A rotary telephone sits on a small table beside the bed, and a portable television with rabbit-ear antennas stands on a low shelf in the corner. The room is bathed in a warm, golden light from the fireplace at the far end of the chamber. Doors lead east to the wardrobe and north back to the corridor.",
        n_to Corridor,
        e_to Wardrobe,
  has light;

Room Wardrobe "Dressing Room"
  with name 'dressing' 'room',
        description "As you enter the dressing room, you are greeted by a warm, rustic atmosphere. The area is lit by a few small windows with thick wooden frames that allow just enough light to filter in. Along one wall is a row of old-fashioned wardrobes made of dark, polished wood, their doors adorned with floral motifs and brass handles. Some modern elements stand out among the traditional decor. A small clock radio sits on a shelf near the armchair and a computer workstation is tucked away in a corner of the room. You brought the computer, an Apple Macintosh Plus, with you from New York. A printer and some floppy discs are neatly arranged on the desk next to it. To the west, a door leads back to the solar.",
        w_to TheSolar,
  has light;

Room Bathroom "Bathroom"
  with name 'bathroom',
        description "The bathroom is illuminated by a crystal chandelier, casting a warm and inviting glow on the marble floors and walls. In the centre of the room is a luxurious claw-foot bath with shiny silver taps and a pristine black iron drain. Against the wall is a polished wooden cabinet with gleaming brass handles. To your left, you can see a silver basin with a crystal-clear mirror above it. In the corner of the room is a sleek, modern toilet with a streamlined design. A stunning Celtic knot is carved into the marble wall above the bath. A door to the south leads back to the grand corridor.",
        s_to Corridor,
  has light;

Room Lounge "Lounge"
  with name 'lounge',
        description "The decor of the lounge is traditional, with dark wooden paneling and plush furniture, such as the comfortable armchairs, perfect for cozying up with a good book or a glass of whiskey. In one corner of the room, you notice a stereo system with a turntable and several vinyl records scattered nearby. A set of large, floor-standing speakers flank the turntable, ready to blast out some tunes. Nearby, you spot a well-stocked bar, with an array of liquors and mixers on offer. The bar is made of polished wood and has several high-backed stools pulled up to it. On the opposite side of the room, you see a large TV with a VHS system and several video tapes stacked neatly nearby. A grandfather clock breaks the silence with its ticks. An exit to the south leads back to the grand corridor.",
        s_to Corridor,
  has light;

[EndingSequence;
  print "^Just as quickly as it began, it ended. What a shame.";
  deadflag = 2; ! you have won!
];

[ Initialise; ! init vars and start the game
  score = 0; ! your score is at 0 when you start the game, which kinda makes sense
  notify_mode = false; ! deactivate score notifications if set false
  lookmode = 2; ! activate verbose look mode
  InitTalk(); ! initialize the talk menu extension

  thedark.description = "It is pitch black. You are likely to be eaten by a grue."; ! I hail you, Infocom!

  player.before = PlayerBefore; ! player.before override, see routine implementation
  player.parse_name = PlayerParseName; ! own routine that parses the player's name
  player.description = "There you are, a ghost in a shell, surrounded by a sea of shells.";

  QuoteBox(quote_1);
  print "Writer's block. What a terrible condition. And the pressure builds as everyone waits for the next big thing. Interesting how everything changes with age. All your books were written in New York, but over the years the city seemed to stifle you, to cast a dark veil over your inspiration. Your publisher suggested a change of scenery, perhaps somewhere rural. When your wife Cora told you about a historic country manor for sale near her hometown in the Scottish Highlands, you didn't hesitate for a second. It's only been a few days since you moved in and you've had little time to explore the property. Christmas is just around the corner and the moving boxes are still piling up. The solitude out here is already balm for the soul. You might even manage to write a few lines during the contemplative Christmas days. How about a ghost story?^^";
];

#ifdef DEBUG; 
  [ CheatmodeSub; ! hic sunt dracones... trigger with 'aenima' magic verb in debug mode
    PROGRESS_LEVEL = 1;
    move snowstorm to Limbo;
    PlayerTo(GreatHall);
    MoveFloatingObjects();
  ];
#endif;

! adding the ability of making non-animate objects referred to with "him" or "her"
[ PronounNotice p_object;
	if (p_object == 0 or player or Directions) return;
	if (p_object has pluralname) {
    themobj = p_object;
	}
  else if (p_object has male or female or neuter) {
    SetPronoun(p_object);
  }
  else if (p_object has animate) {
    SetPronoun(p_object);
	}
  else itobj = p_object;
];

[ SetPronoun p_object;
  if (p_object has female) herobj = p_object;
	else if (p_object has neuter) itobj = p_object;
	else himobj = p_object;
];

#ifV5;
! we use our own routine as Puny's might cause Infocom z5 terps to misbehave
[ DrawStatusLine width posa s1 s2; !
   _StatusLineHeight(statusline_height); @set_window 1; @set_cursor 1 1; style reverse;
   width = 0->33;
   FastSpaces (width);
   @set_cursor 1 2; _PrintObjName(location);
   if (width > 76)
   {
	   s1 = NumWidth(status_field_1);
	   s2 = NumWidth(status_field_2);
	   posa = width-26;
	   @set_cursor 1 posa;
	   print " Score: ", status_field_1;
	   FastSpaces(6-s1);
       print "Moves: ", status_field_2;
	   FastSpaces(6-s2);
   }
   else if (width > 39)
   {
	   s1 = NumWidth(status_field_1);
	   s2 = NumWidth(status_field_2);
	   posa = width - 9 - s1 - s2;
	   @set_cursor 1 posa;
	   print " Score: ", status_field_1, "/", status_field_2;
	   @print_char ' ';
   }
   @set_cursor 1 1; style roman; @set_window 0;
];

[NumWidth num width;
	width = 1;
	if(num < 0) {
		width++;
		num = -num;
	}
	if(num >= 10)
	{
	   width++;
	   if(num >= 100)
	   {
		   width++;
		   if(num >= 1000)
		   {
			   width++;
		   }
	   }
	}
	return width;
];
#endif;

[ AboutSub;
  print_ret (string)Story, " is copyright (c) 2023 by Stefan Vogt.^^You may freely distribute the game, but you have to link to <8bitgames.itch.io>. This work may not be sold or included in any for-profit collection without written permission from the author.^^Please send bug reports to ", "<stefan@@64","8-bit.info>.^^For acknowledgements and credits, please type ~credits~.";
];

[ CreditsSub;
  print_ret (string)Story, " is dedicated to literally no one.^^~Try to be like the turtle - at ease in your own shell.~ -- Bill Copeland^^Beta testers: Mom, Dad, Aunt Gertrud, Will from the butcher shop.^^This game was created using the Inform language by Graham Nelson and it utilizes PunyInform, a custom library by Fredrik Ramsberg and Johan Berntsson.";
];

[ UseSub;
  "The verb [use] is not something you need to refer to in the course of this game. Be more specfic. Here are a few common examples:^^[operate computer],^you can [talk to someone] or [ask someone],^and [insert floppy in drive].";
];

[ HelpSub;
  print_ret (string)Story, " package contains a PlayIF card that explains the basic gameplay and many synonyms will enhance what you read on it. There are a few extra verbs necessary but these will be introduced to you in advance. You never have to guess the verb.^^Conversations are not as complex as in Infocom titles. It is sufficient to type [talk to NPC] or [ask NPC].^^Use the [save] command to store your progress and [restore] to load it again.";
];

[ XyzzySub;
  "You perceive a mysterious whisper. Coming from everywhere and nowhere, yet present, it says ~Nice try.~";
];

[ LookUnderSub;
  "You don't have to look under any objects in this game. The only two verbs you need in this context are [examine] and [search].";
];

#ifV3;
[ UndoSub;
  "This version of the game does not support the UNDO command.";
];
#endif;

[ UnlockWithoutKeySub;
  if (noun has lockable) print_ret "Try being more specific. You probably want to unlock ", (the) noun, " with a certain object?";
  "You cannot unlock this object.";
];

[ TypeSub;
  "That's not an object on which you can type something.";
];

[ TypeErrSub;
  print "You'll need to be more specific. The type command wants you to provide a number or a string and an object to type it on.^^[type 1234 or ABCD on/into object]^";
  noun = false;
];

! if not overridden by a before routine, TURN NOUN LEFT/RIGHT will invoke a regular TURN NOUN
[ TurnLeftSub;
  <<Turn noun>>;
];

[ TurnRightSub;
  <<Turn noun>>;
];

[ TurnAroundSub;
  print_ret (string)MESS_TURN_SELF;
];

[ PlayerBefore;
    Turn:
      print_ret (string)MESS_TURN_SELF;
    Attack:
      "As hopeless as the situation may be, this is never a way out.";
    default:
      rfalse;
];

[ PlayerParseName n;
		n=0;
		while (NextWord()=='me' or 'myself' or 'self' or 'yourself') {n++;}
		return n;
];

[ FillErrorSub;
  "The fill command wants you to be more specific.^^[fill object with liquid]";
];

#ifdef DEBUG;
Verb 'aenima' * -> Cheatmode;
#endif;

Verb 'about' * -> About;
Verb 'credits' * -> Credits;
Verb 'help' * -> Help;

Verb 'use' * -> Use
           * noun -> Use
           * noun 'with'/'on'/'in'/'at' noun -> Use;

Verb 'xyzzy' * -> Xyzzy;

#ifV3;
Verb 'undo' * -> Undo;
#endif;

Verb 'type'  * -> TypeErr
             * special -> TypeErr
             * special 'on'/'into'/'in' -> TypeErr
             * special 'on'/'into'/'in' noun -> Type;

Extend 'turn' * noun 'left' -> TurnLeft
              * noun 'right' -> TurnRight;
Extend 'turn' * 'around' -> TurnAround;
Extend 'turn' * 'over' noun -> Turn;

Extend 'examine' * 'back' 'of' noun -> Turn;

Extend 'look' * 'under' noun -> LookUnder;
Extend 'look' * 'through'/'out' noun -> Examine;
Extend 'look' * 'out' 'of' noun -> Examine;

Extend 'unlock' first * noun -> UnlockWithoutKey;

Extend 'attack' * held 'at'/'on' noun -> Attack;

Extend 'fill' replace
  * noun -> FillError
  * held 'with' noun -> Fill
  * noun 'in'/'into' held -> Fill;

Extend 'speak' replace
  * noun -> Talk
  * 'to'/'with' noun -> Talk;

Extend 'ask' replace
  * noun -> Talk
  * noun 'for' noun -> AskFor;

Verb meta 'continue' = 'again';
Verb 'grab' = 'take';
Verb 'kick' = 'attack';
Verb 'flip' = 'turn';
Verb 'consult' = 'open';
Verb 'shoot' = 'attack';
Verb 'stab' = 'cut';
Verb 'pour' = 'fill';

end;
