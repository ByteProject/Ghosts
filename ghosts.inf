!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $MAX_ABBREVS=96
!% $TRANSCRIPT_FORMAT=1
!% $ZCODE_LESS_DICT_DATA=1
!% -r
!% -e
! ----------------------------------------------------------------------------
!  THE GHOSTS OF BLACKWOOD MANOR
!  An interactive Horror by Stefan Vogt
!  Copyright (c) 2023 Moonmist Entertainment
!  and Fusion Retro Books / ZZAP!64 
!  http://8bitgames.itch.io
! ----------------------------------------------------------------------------

! IFID (http://babel.ifarchive.org) -- generate with https://uuidgenerator.net
Array UUID_ARRAY string "UUID://1ff435df-9747-41ec-ac66-311c3cb4ee40//";
#Ifdef UUID_ARRAY; #Endif;

Include ">abbrvs.h";

Constant Story    "The Ghosts of Blackwood Manor";
Constant Headline "^An interactive Horror by Stefan Vogt^\
                   (c) 2023 Moonmist Entertainment^\
                   and Fusion Retro Books / ZZAP!64^";
Release 1;

! game-specific defines
Constant DEBUG; ! deactivate for release
Constant CUSTOM_ABBREVIATIONS;
Constant MAX_CARRIED = 20;
Constant OPTIONAL_SCORED;
Constant OPTIONAL_FULL_SCORE;
Constant OBJECT_SCORE = 3;
Constant ROOM_SCORE = 3;
Constant MAX_SCORE 500;
Constant OPTIONAL_FULL_DIRECTIONS;
!Constant OPTIONAL_MANUAL_REACTIVE;
!Constant OPTIONAL_MANUAL_SCOPE;
Constant INITIAL_LOCATION_VALUE = GreatHall;
#ifV5;
Constant OPTIONAL_PROVIDE_UNDO; ! undo opcode is only supported in Z-machine v5 spec
#endif;

! 0 production build, 1 notification on errors, 2 full error messages
Constant RUNTIME_ERRORS = 0;

! Puny built-in routine overrides
Replace DigSub;
Replace FillSub;
Replace ConsultSub;
Replace PronounNotice;
#ifV5;
Replace DrawStatusLine; ! replace Puny's routine for Infocom interpreter compatibility
#endif;

Include "globals.h";

! declare recurring properties to optimize story file size for Z-machine v5 spec
Property cheap_scenery;      ! used by Puny's cheap scenery extension
Property cheap_scenery_pt2;  ! additional cheap scenery to work around the 32 entry limit of properties
Property cheap_scenery_pt3;  ! additional cheap scenery to work around the 32 entry limit of properties
Property player_progress;    ! common property to be used when the player makes progress with an object
Property talk_start;         ! used by Puny's talk menu extension

! Constants for puny extensions
Constant FLAG_COUNT 50;
Constant TM_MSG_TOPICS_DEPLETED "You've ran out of things to talk about and end the conversation.";
Constant TM_MSG_EXIT "You decide to catch up later and end the conversation.";

! cheap scenery SceneReply must be defined before Puny extension import
[ SceneryReply;
    default:
        rfalse;
];

! import Puny extensions
Include "ext_quote_box.h";
Include "ext_flags.h";
Include "ext_talk_menu.h";
Include "ext_cheap_scenery.h";

! recurring messages and conversations
Constant MESS_TURN_SELF "Now you feel dizzy. You've had better ideas than that.";
Constant MESS_SCENERY "You needn't worry about that.";

! messages (lib-overrides)
Constant MSG_PROMPT 1000;
Constant MSG_TAKE_ANIMATE 1001;
Constant MSG_EAT_ANIMATE 1002;
Constant MSG_PARSER_NO_INPUT 1003;
Constant MSG_PARSER_UNKNOWN_VERB 1004;
Constant MSG_PARSER_CANT_SEE_SUCH_THING 1005;
Constant MSG_PARSER_DONT_UNDERSTAND_WORD 1006;
Constant MSG_LOOK_BEFORE_ROOMNAME 1007;
Constant MSG_INSERT_NOT_CONTAINER 1008;
Constant MSG_EMPTY_CANT_CONTAIN 1009;
Constant MSG_OPEN_YOU_CANT 1010;
Constant MSG_PARSER_NO_NEED_REFER_TO 1011;
Constant MSG_PUSH_STATIC 1012;
Constant MSG_PULL_STATIC 1013;
Constant MSG_TURN_STATIC 1014;
Constant MSG_TAKE_STATIC 1015;
Constant MSG_FILL_NO_WATER 1016;

[LibraryMessages p_msg p_arg_1 p_arg_2;
 switch(p_msg) {
    MSG_PROMPT:
      print ">";
      p_arg_1 = p_arg_2;
    MSG_TAKE_ANIMATE, MSG_EAT_ANIMATE:
      "Do you think ", (the) noun, " would like that?";
    MSG_PARSER_NO_INPUT:
      "You won't get very far without input. You understand that, right?^";
    MSG_PARSER_UNKNOWN_VERB:
      "That's an unknown verb. Could you try something else?";
    MSG_PARSER_CANT_SEE_SUCH_THING:
      "You don't see that here.";
    MSG_PARSER_DONT_UNDERSTAND_WORD:
      print "This game understands many words but ";
			print "~";
			_PrintUnknownWord();
			print_ret "~ is unfortunately not one of them.";
    MSG_LOOK_BEFORE_ROOMNAME:
      @new_line; ! mimics Inform style location texts, disable for Infocom style
    MSG_INSERT_NOT_CONTAINER,
    MSG_EMPTY_CANT_CONTAIN:
      print_ret "Either this object can't contain any things or doing it won't help you progress in this game.";
    MSG_OPEN_YOU_CANT:
      "That doesn't seem to be something you can ", (verbname) p_arg_1, ".";
    MSG_PARSER_NO_NEED_REFER_TO:
      print_ret (string)MESS_SCENERY;
    MSG_PUSH_STATIC, MSG_PULL_STATIC, MSG_TURN_STATIC, MSG_TAKE_STATIC:
		  print_ret (CTheyreorThats) noun, " immovable.";
   }
 rfalse;
];

[PrintRank; ! this uses Puny's PrintRank feature
  print ", earning you the rank: ";
  if (score >= 10) "The Ten Commandments.";
  if (score >= 5) "High Five.";
  if (score >= 1) "The chosen One.";
  "Absolute Zero.";
];

Array quote_1 --> 7 30
"How you have fallen from"
"heaven, morning star, son of"
"the dawn! You have been cast" 
"down to the earth, you who"
"once laid low the nations!"
"                            "
"             -- Isaiah 14:12";

! will be triggered when the room is described
! [ LookRoutine;
! ];

! will be triggered when you visit a new room
! [ NewRoom;
! ];

Include "puny.h";

! set this for non-animate objects that may be referred to with "him"
Attribute male;

! 0 = snowstorm raging / game begins
! 1 = waking up after falling asleep in lounge
! 2 = consult Rosie for the man with the scar
Global PROGRESS_LEVEL = 0; ! tracks the game's progress level 

! note that flags used in talk_array must have id 32-299
Constant F_ROSIE_TALKED_CORA 32;                      !1
Constant F_ROSIE_TALKED_HOLIDAYS 33;                  !2
Constant F_ROSIE_TALKED_SNOWSTORM 34;                 !3

! talk_menu array -- STATUS [ID 300-600] TOPIC PLAYERSAYS NPCSAYS [FLAGREF|UNLOCKREF|ROUTINE]*
Array talk_array -->
TM_NPC Rosie
30 "Preparations" "Hey Rosie, how are the preparations going?" "I am finished now, Sir Thomas. The manor is cleaned, I've made bread and biscuits for you and Lady Cora and the fridge is stocked with fresh vegetables and meat. It will be a lovely Christmas." 1 2 3
0 "Cora" "Is Cora back?" "The Lady has not yet returned from her visit to town. She wanted to buy a few things and come back in the afternoon, so she should be home soon." F_ROSIE_TALKED_CORA RosieFinalTopicVestibule
0 "Holidays" "I hope you enjoy your time off." "We are going to take it easy, just like every year. That's what I love about Christmas. The world slows down a bit and you reflect on the essentials. I'll be back for you and the Lady in the new year." F_ROSIE_TALKED_HOLIDAYS RosieFinalTopicVestibule
0 "Snowstorm" "It's quite a heavy snowstorm out there. Can you make it home?" "No worries, Sir Thomas. As you know, our house belongs to the manor. It's only a ten minute walk. It'll be fine." F_ROSIE_TALKED_SNOWSTORM RosieFinalTopicVestibule
0 300 "Blackwood" "Thank you, Rosie. A merry Christmas for you and your loved ones. Since Blackwood is new to us, may I call you in case we have a question?" "Of course, Sir Thomas! I left you my number in the kitchen." RosieLeavesManor
TM_NPC Cora
30 "Shopping" "Good morning, darling! I hope you slept well. How was the shopping?" "Hi, honey! I don't think I've ever slept better. This bed is so incredibly comfortable. I probably bought too much stuff though. And Inverness hasn't changed at all." 1
0 "Inverness" "Did you enjoy being home again?" "You know I haven't been there since Mum died in 1975. It felt so strange to be walking these streets after more than eleven years. I'll probably get used to it again." 1
0 "Breakfast" "Would you like some breakfast?" "Thank you, sweetie. I am not hungry. I had a coffee and now all I need to be happy is this bed and this book." 1 2
0 "Book" "What are you reading?" "Well, I found this book about the Scottish witch hunts lying around. Pretty morbid, isn't it?" IncrementScore
0 "Dream" "I had a strange dream tonight. There was a building with holes in the walls where birds were nesting." "Sounds like an old dovecote. You know we have one on the manor grounds, don't you?" IncrementScore
TM_NPC 0;

[ IncrementScore; ! routine to increment score during conversations
  score = score + 5;
];

[ RosieFinalTopicVestibule;
  if (FlagIsSet(F_ROSIE_TALKED_CORA, F_ROSIE_TALKED_HOLIDAYS, F_ROSIE_TALKED_SNOWSTORM)) {
    ActivateTopic(Rosie, 300);
  }
];

[ RosieLeavesManor;
	talk_menu_talking = false; 
  move Rosie to Limbo;
  IncrementScore();
	"^Rosie waves goodbye and leaves the manor.^^It will probably take a while for Cora to get back from the city. You wonder how you are going to pass the time until she returns, which reminds you that you wanted to check out the lounge on the first floor.";
];

! walls will be an object we put in scope on demand
Object walls "walls"
  with name 'wall' 'walls',
      description "You check the walls without gaining any new insights.",
      before [;
          Attack:
            "There is surely a better way to deal with frustration.";
          Turn:
            "The walls? Are you serious?";
          Push, Pull:
            "The walls won't move.";
      ],
      found_in [;
            if (location ofclass Room) rtrue;
            if (location ofclass Outside) rfalse;
      ],
  has scenery;

! limbo for objects that we don't need anymore, beware of the grues!
Object Limbo;

! classes
Class Room;      ! inside locations (walls in scope)
Class Outside;   ! outside locations

! NPCs 
Object Rosie "Rosie",
  with name 'Rosie' 'housekeeper',
    initial "Rosie is here. It seems as if she has been waiting for you.",
    description "Rosie has been Blackwood's housekeeper for many years. She already served the former owner of the manor. Despite her age, her eyes are sharp and she seems to notice every detail. Her hair is pulled back in a tight bun and she always has a smile on her face.",
    talk_start 0,
    before [;
      Touch, Attack: 
        "That wouldn't be very gentlemanly.";
    ]
  has proper animate female;

Object Cora "Cora",
  with name 'Cora',
    initial "Cora has snuggled up in bed and is reading a book.",
    description "Your wife Cora is a strikingly beautiful woman with fiery red hair and emerald green eyes. You love her lilting Scottish accent, and her words are always filled with warmth.",
    before [;
        Take, Touch, Push, Pull, Turn:
          "This is not that kind of game.";
        Kiss:
          "You kiss Cora. ~What was that for?~ She looks a little surprised, but definitely charmed.";
        Attack:
          "What's wrong with you? Just no.";
    ],
    talk_start 0,
  has proper animate female transparent;

Object -> book "witch hunt chronology", ! when the game starts, Cora is reading this book
  with name 'witch' 'hunt' 'chronology' 'book',
        description "Chronology of the Great Scottish Witch Hunts from 1590 to 1662. The book looks quite old and very likely belongs to the manor's library. A rather sombre read.",
    before [;
      Take:
        if (self in Cora) "Cora is currently reading it, you don't want to take it away from her.";
    ],
  has scored;

Room GreatHall "Great Hall" ! -- locations start here
  with name 'great' 'hall' 'manor' 'blackwood' 'living' 'room' 'exit' 'white' 'layer',
      description [;
        print "This part of the manor is said to date back to the 8th century. Blackwood was built on the remains of an ancient monastery. Today, the Great Hall is used as a sitting room with a cosy Victorian interior. Flames dance in the fireplace and wood crackles as it burns slowly, giving off a pleasant warmth. Flickering light wanders across the walls, engulfing the shadows and illuminating the room in a melancholy twilight. In one corner, just below the gallery of ancestors, stands a beautifully decorated Christmas tree. Through the large windows you can see ";
        if (PROGRESS_LEVEL == 0) print "a snowstorm raging, the wind is howling as waves of snowflakes streak by.";
        else print "the manor's feral garden and the surrounding forest blanketed in a white layer.";
        print " The only exit is to the north, leading to the vestibule.";
        if (self hasnt visited) print "^^[Welcome to Ghosts of Blackwood Manor. To learn more about how to play the game, type HELP. First time players should also type ABOUT.]";
        new_line; ],
      n_to Vestibule,
      cheap_scenery
          22 'cosy' 'victorian' 'interior' 'furniture' "Just as one would imagine an old country house to be. The lovingly handcrafted pieces of furniture do not seem to be replicas, although you first assumed so because they are in impeccable condition. Witnesses of a bygone era with an inestimable value."
          8 'fireplace' 'fire' 'wood' 'flames' 'warmth' 'twilight' 'shadows' 'light' "For a second you lose yourself in the flames, immersed in the eternal struggle between darkness and light. How many generations must have warmed themselves here over the centuries? What were their worries, their hardships, as they lingered within these venerable walls? For a brief moment we shine brighter than the morning star, but when the Grim Reaper knocks on our door, he leads us back to the infinite gloom from which we once emerged."
          54 'beautifully' 'christmas' 'xmas' 'decorated' 'scottish' 'fir' 'decorations' 'corner' 'tree' "The tree, a Scottish fir, looks wonderful in its red and white Christmas decorations. Cora has really outdone herself this year."
          CS_ADD_LIST GreatHall (cheap_scenery_pt2),
        cheap_scenery_pt2
          14 'large' 'windows' 'window' 'pane' 'panes' 
          [;
            Examine:
              if (PROGRESS_LEVEL == 0) print_ret (string)MESS_STORM;
              "You stand at the window, peering out into the winter landscape beyond. The world outside is covered in a thick blanket of snow.";
          ]
          11 'feral' 'garden' "You definitely have to take care of the garden next spring. It has apparently been neglected a bit in the last few years, but it is not completely overgrown."
          11 'surrounding' 'forest' "The adjacent forest surely holds many secrets, some of which may one day be rediscovered, others will remain hidden forever, buried and forgotten witnesses to a dark past.",
  has light;

Constant MESS_STORM "The window panes rattle from the force of the storm. Heavy snowflakes swirl in the air and blanket the manor's feral garden and the surrounding forest in a white layer.";

Object -> gallery "gallery of ancestors",
  with name 'gallery' 'of' 'ancestors' 'portraits' 'lords' 'ladies' 'pictures' 'painting',
    description [;
      print "Fascinated, you look at the many portraits of the former Lords and Ladies of Blackwood. There must be some clues in the manor's library to find out who they depict. Judging by their clothes, however, they are all long gone. The way of all flesh.";
      if (PROGRESS_LEVEL == 3) {
        new_line; new_line;
        if (bainportrait in self) <<Search self>>;
      }
      new_line; rtrue;
    ],
    before [;
      Search:
        if (bainportrait in self && PROGRESS_LEVEL == 3) {
          move bainportrait to location;
          PROGRESS_LEVEL = 4;
          IncrementScore();
          "On closer inspection, you can see the portrait Rosie was talking about. Lord Bain's cold gaze is hard to resist. An icy shiver runs down your spine. Bain is holding a hand to his chest, and on the back of his hand you recognise the same distinctive scar you saw in your dream. How can this be?";
        }
        else rfalse;
    ],
  has scenery;

Object -> -> bainportrait "portrait of Lord Bain",
  with name 'portrait' 'of' 'lord' 'bain' 'archibald' 'picture' 'scar',
    initial "One of the paintings in the gallery stands out. The portrait of the man with the scar on his hand, Lord Archibald Bain.",
    description "You wonder if what they say about him is true. Did he really wipe out an entire village because its inhabitants had allegedly made a pact with the devil? So much bloodshed, all in the name of God?",
    before [;
        Touch:
          "Your hand touches the portrait. Under your fingers you feel the brushstrokes of the painter who immortalised this moment in time many centuries ago.";
        Push:
          "You push it to the wall but that's the wrong direction if you want to closer inspect it.";
        Pull, Turn, Open, Search: 
          if (PROGRESS_LEVEL == 4 or 5) {
            give self transparent;
            if (PROGRESS_LEVEL == 4) IncrementScore();
            PROGRESS_LEVEL = 5;
            "You turn the portrait and on the back you notice a small elevation under the canvas as if there is something hidden underneath. You cannot open the canvas with your bare hands though. You need something like a small and very sharp knife or a cutter to not destroy the picture.";
          }
          else "No need to bother with it again.";
        Cut, Attack:
          if (self has transparent) <<Cut canvas>>;
          else "Is there any particular reason why you'd want to do that?";
    ],
  has static;

Object -> -> -> canvas "canvas",
  with name 'canvas' 'elevation',
    description "You wonder what is hidden underneath.",
    before [;
      Pull, Turn, Open, Search:
        <<Open bainportrait>>;
      Cut, Attack:
        if (~~ boxcutter in player or GreatHall) "You don't carry a suitable object.";
        if (~~ parchment in self) "No need to cut it again, you already found what was hidden inside the portrait.";
        IncrementScore();
        PROGRESS_LEVEL = 6;
        move parchment to GreatHall;
        "You carefully cut the canvas next to the elevation without destroying the picture. A parchment falls from the portrait.";
    ]
  has scenery;

Object -> -> -> -> parchment "parchment",
  with name 'parchment' 'writing',
    initial "A parchment has fallen from the portrait and is now lying on the floor.",
    description "The writing is ornate and looks old-fashioned. There is a kind of riddle on the parchment: Behind the endless knot I was laid to rest. Four, four, two, three, find me and you will see.",
  has scored;

Object -> snowstorm,
  with name 'snowstorm' 'wind' 'snowflakes' 'streaks',
        description [;
          print_ret (string)MESS_STORM;
        ],
    before [;
      Examine:
        rfalse;
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Room TheChapel "The Chapel"
  with name 'chapel' 'religious' 'symbols',
        description "The silence is almost palpable, adding to the sense of reverence that pervades this sacred place. Your footsteps echo softly off the cold, rough stone walls. The occasional creak of old wood can be heard. Rows of pews stretch out before you. The ancient altar is the centrepiece here, drawing your eyes to it with its grandeur and attention to detail. Above it is a large wooden crucifix, its once gilded surface now dulled with age. Behind the altar are niches with stained glass windows depicting scenes from the Bible. The colours of the glass are muted and subdued, casting a soft, warm light on the chapel floor. One wonders how many prayers and moments of reflection the chapel has witnessed over the centuries. Doors to the south and southwest lead to the Oratory and Sacristy, while the exit to the north leads back to the vestibule.",
        n_to Vestibule,
        s_to Sacristy,
        sw_to Oratory,
        d_to [;
          if (opening in self) {
            print "Slowly you descend until you are completely engulfed in darkness.^";
            return Undercroft;
          }
          "You cannot go that way.";
        ],
  has light scored;

Object -> altar "altar",
  with name 'large' 'ancient' 'altar',
    description "You approach the ancient altar in awe. The stone structure is weathered and worn, its edges smoothed by countless hands over the centuries. As you get closer, you notice that the altar is decorated with elaborate carvings of biblical scenes and symbols. There is a depiction of the last supper, with Jesus and his disciples gathered around a table, and an image of the crucifixion, with Jesus on the cross and Virgin Mary weeping at his feet. On the back of the altar you notice a bronze panel.",
    before [;
        Receive:
          if (receive_action ~= ##Insert) rfalse;
          <<Insert noun panel>>;
        Touch:
          "You feel the cold stone and wonder how many hands have touched it over the centuries.";
        Climb:
          "Hell no, you're too old for that.";
        Push, Pull:
          "No matter how hard you try, it won't move.";
    ],
  has scenery transparent;

Object -> -> panel "bronze panel",
  with name 'bronze' 'panel' 'spot' 'indention' 'silhouette' 'humanoid',
    description [;
        print "The panel depicts the chaotic events of the end of the world. The Four Horsemen ride through the sky, their steeds galloping at breakneck speed. This scene is explicitly described in the Book of Revelation. The first horseman rides a white horse, symbolising conquest, while the second rides a red horse, symbolising war. The third horseman rides a black horse, representing famine, and the last horseman rides a pale horse, representing death. A closer look at the panel reveals that the sky behind the horsemen is ablaze with fire and brimstone, and the ground is littered with the bodies of the damned. The scene is filled with vivid detail, from the writhing souls of the damned to the triumphant expressions on the faces of the saved. ";
        if (statuette in self) "The panel is now complete. The bronze statuette of the Archangel Gabriel blowing his trumpet to announce the Day of Judgement fitted perfectly into the indentation.";
        "There is a spot with an indentation in the panel where something seems to be missing. The indentation has the shape of a humanoid silhouette.";
    ],
    before [;
        Receive:
          if (receive_action ~= ##Insert) rfalse;
          if (statuette in self) "The statue is the right item for the panel, and you've already placed it where it belongs, so there's no need to worry about it.";
          if (~~ noun == statuette) "That's definitely the wrong object. You are looking for something that fits the spot in the bronze panel.";
          move statuette to self;
          move opening to TheChapel;
          give self transparent;
          IncrementScore();
          "You put the statuette in the bronze panel and it fits perfectly. The next moment, a hidden mechanism activates and the altar slides backwards, revealing an opening below that leads into the blackest darkness.";
        Push, Pull:
          <<Push altar>>;
        Touch:
          "It feels like cold metal. Other than that, there is nothing special about it.";
    ],
  has scenery;

Object -> -> -> opening "opening",
  with name 'opening',
    article "an",
    initial "An opening that was hidden below the altar leads down to a very dark place.",
    description "Fascinated you stare at the opening and remember a quote from Edgar Allan Poe: ~Deep into that darkness peering, long I stood there, wondering, fearing, doubting, dreaming dreams no mortal ever dared to dream before.~",
    before [;
        Enter:
          <<Go FAKE_D_OBJ>>;
    ],
  has static;

Room Oratory "Oratory"
  with name 'oratory',
        description "The oratory was used by the Lord and his family for private prayer and contemplation. A wooden cross hangs from the wall, with a single pew in front of it. An alcove to the left catches the eye with a beautifully carved statue of a saint, surrounded by a small collection of candles and dried flowers. The saint is depicted with a serene expression, holding a book and a cross, the eyes gazing towards the heavens. A small plaque beside the statue reads ~St. Margaret of Scotland.~ An exit to the north leads back to the chapel.",
        n_to TheChapel,
  has light scored;

Room Vestibule "Vestibule"
  with name 'vestibule',
        description "As you enter the vestibule, you can't help but feel a sense of awe. The high ceiling is supported by grand columns and arches, and the walls are decorated with intricate carvings and tapestries. The room is dimly lit by bulbs in wall sconces, casting long shadows across the stone floor. To the west, you can see an enormous wooden door leading to the outside, its surface polished by centuries of use. The libary lies to the north, the great hall to the south and the kitchen to the east. To the southwest, an olden hinged door leads to the chapel. A stone staircase spirals upwards to the upper levels of the manor. ",
        n_to Library,
        s_to GreatHall,
        e_to Kitchen,
        w_to [;
          if (PROGRESS_LEVEL == 0) "A snowstorm is raging outside. You decide that it's not a good time to leave the manor now.";
          return Forecourt;
        ],
        sw_to TheChapel,
        u_to Corridor,
  has light scored;

Room Sacristy "Sacristy"
  with name 'sacristy',
        description "You are in a small, dimly lit room. The walls are lined with wooden shelves filled with old, leather-bound books. The smell of incense hangs heavily in the air. In the centre of the sacristy is a large wooden table covered with a green velvet cloth. On the table you can see a chalice, a censer and a paten, all of which appear to be made of gold. You notice a chest with iron fittings containing a set of richly embroidered silk vestments and a small cabinet with an elaborate painting on the front (Death Scene of Golgotha). It contains the oil and balm used to anoint the sick and dying. A single exit to the north leads back to the chapel.",
        n_to TheChapel,
  has light scored;

Room Kitchen "Kitchen"
  with name 'kitchen',
        description "As you enter the kitchen, you are immediately transported back to a bygone era. Although modernised, the kitchen retains its old world charm and historic character that reflects its storied past. The vast space is filled with sturdy oak cupboards, shelves and drawers. The fireplace, once the main source of heat and cooking, has been replaced by a modern cooker, but the original mantelpiece and hearth remain as a reminder of the roaring fire that once burned here. The heavy flagstone floors show the wear and tear of years of use. The rough-hewn stone worktops add to the old-world charm of the kitchen, while the electric lights brighten the room. It is easy to imagine the hustle and bustle of the staff as they worked to create the elaborate dishes that would have been served to the lords and ladies of the manor. There are exits to the north, south, east and west.",
        n_to Storeroom,
        s_to Buttery,
        e_to Pantry,
        w_to Vestibule,
  has light scored;

Object -> cupboards "cupoards, shelves and drawers",
  with name 'sturdy' 'oak' 'cupboards' 'drawers' 'shelves' 'wood',
    description "The centuries of use have literally polished the wood. It has also darkened beautifully over time.",
    before [;
      Open, Search:
        if (PROGRESS_LEVEL == 5) "You look more closely, but all the knives you can find have blades that are too thick. You are afraid of damaging the portrait if you use them to gently cut the canvas on the side. You need a finer tool such as a box cutter.";
        "You don't find anything of use.";
    ],
  has scenery;

Object -> worktops "worktops",
  with name 'rough' 'rough-hewn' 'stone' 'worktops' 'worktop',
    description [;
      print "One can only admire this craftsmanship. You wonder how old these worktops are, probably many centuries.";
      if (note in self) {
        new_line;
        <<Search self>>;
      }
      new_line; rtrue;
    ],
    before [;
      Touch:
        "You feel cold stone.";
      Push, Pull, Turn:
        "No chance to move them, which is no surprise. They must weigh a ton.";
      Search:
        if (note in self) {
          move note to location;
          "Rosie seems to have left a note for you on one of the worktops.";
        }  
        else rfalse;
    ],
  has scenery;

Object -> -> note "note",
  with name 'note',
    initial "A note is lying on one of the worktops.",
    description "It has Rosie's phone number on it.",
  has scored;

Room Pantry "Pantry"
  with name 'pantry',
        description "The purpose of the pantry in medieval times was to store food supplies and ensure that the manor had enough provisions to get through the winter months. Pantries are usually located on the east side of a manor house, where they are least exposed to the sun. What you see here is the juxtaposition of modern appliances and antique stone walls. Tall shelves run from the floor to the ceiling, lined with jars, baskets and trays of food. On the left-hand side of the room, a commercial fridge stands tall, its stainless steel finish smooth and its humming unobtrusive, stocked with a variety of fresh vegetables, meat and dairy products. To the right is a long wooden table covered with an array of breads. There are traditional Scottish oatcakes, crusty baguettes and loaves of sourdough bread that Rosie, the housekeeper, made before leaving for her Christmas holiday. A door to the east leads back to the kitchen.",
        w_to Kitchen,
  has light scored;

Room Buttery "Buttery"
  with name 'buttery',
        description "The first thing you notice is the smell of alcohol. Wooden shelves are lined with bottles of various sizes, shapes and colours, each containing a different type of Scotch whisky. The shelves are dimly lit by candlelight, casting a warm glow over the amber liquid inside. In one corner, there's a cask of ale that you've been dying to try over the holidays. In the centre of the room, a small wooden table is littered with empty glasses. You love being here, soaking up the atmosphere. It's cosy, warm and inviting, but there's also a sense of history and tradition that's as intoxicating as the booze itself. Go north to get back to the kitchen.",
        n_to Kitchen,
  has light scored;

Room Storeroom "Storeroom"
  with name 'storeroom',
        description "You're struck by the musty smell and the dim lighting. The storeroom is cluttered with various items, some of which appear to be decades old. You see various boxes and containers, a wooden crate marked ~fragile~, and a dusty typewriter. In front of you is a large chest of drawers with a mirror on top. The drawers are closed, but you can see that they're labelled with various items such as ~linen~, ~candles~ and ~cutlery~. On the floor you can spot an old-fashioned sewing machine and a pile of fabric scraps. On the right, there's a metal shelf with various cleaning supplies, a vacuum cleaner, a bucket with a mop, a broom and a selection of cleaning products. Above the shelf is a row of coat hangers with some old jackets and hats. You can leave by heading south.",
        s_to Kitchen,
  has light scored;

Object -> oldjackets "old jackets",
  with name 'old' 'jackets',
    description "The old jackets are quite moth-eaten and definitely no longer suitable for wearing.",
    before [;
        Take, Wear:
          "Um, no. What's wrong with you?";
        Smell:
          "Wow, they smell pretty musty.";
        Touch:
          "Wonderful. Apparently they are also sticky.";
        Search:
          move electrictorch to Storeroom;
          "Oh, there's an electric torch in one of the pockets that might come in handy.";
    ],
  has scenery;

Object -> -> electrictorch "electric torch",
  with name 'electric' 'torch' 'flashlight' 'Ever Ready',
    describe [;
      if (self hasnt moved) {
        print "^You notice an electric torch ";
        if (self has on) print "(providing light) ";
        "sticking out of the pocket of an old jacket.";
      }
    ],
    article "an",
    description [;
        print "It's an Ever Ready electric torch, probably from the 1970s, ";
        if (self hasnt on) "currently off.";
        "shining brighter than the Morning Star.";
    ],
    before [;
        Drop:
          if (location == thedark) "It's not a good idea to drop your only source of light in the dark.";
        SwitchOff:
          if (self in Cellar or Undercroft or Crypt or Dungeon or SecretPassage) "It won't be a good idea to switch off your only source of light here while not carrying it. You won't be able to find it again and this area would become inaccessible.";
    ],
    after [;
        SwitchOn: 
          give self light;
          scope_modified = true;
        SwitchOff: 
          give self ~light;
          scope_modified = true;
    ],
has switchable scored;

Object -> drawers "drawers",
  with name 'linen' 'candles' 'cutlery' 'drawers' 'drawer' 'chest',
    description "Properly labelled. Commendable.",
    before [;
        Open, Search:
          if (~~ PROGRESS_LEVEL == 5 && boxcutter in self) "You look inside the drawers and find absolutely nothing that could be of any use to you right now. In addition, the drawer labelled with ~candles~ is completely empty. That's quite a disappointment.";
          move boxcutter to location;
          IncrementScore();
          "Your search reveals a box cutter, which you place on the ground.";
    ],
  has scenery;

Object -> -> boxcutter "box cutter",
  with name 'box' 'cutter' 'Stanley',
    initial "A box cutter lies on the ground.",
    description "That's an old Stanley box cutter. They were built to last forever unlike the cheap plastic you get these days.",
  has scored;

Room Library "Library"
  with name 'library',
        description "You are impressed by the grandeur of Blackwood's Library. The walls are lined with bookshelves that reach to the ceiling, filled with books of all shapes and sizes. Many appear to be very old, their bindings faded with time. The shelves themselves are made of dark, polished wood and have a warm, inviting feel to them. The air is thick with the scent of leather and old paper, a smell that immediately transports you to another time and place. A large wooden table is covered with old manuscripts and parchments, surrounded by a row of high-backed leather chairs, some of which look as if they've been there for centuries. One wonders what secrets hide here about the history of the manor and the surrounding area. A door to the south leads back to the vestibule.",
        s_to Vestibule,
  has light scored;

Outside Forecourt "Forecourt"
  with name 'forcourt',
        description "The forecourt is surrounded by a low stone wall and in its centre is a large stone fountain decorated with carvings of Celtic knots and ancient Gaelic symbols. The walls of Blackwood are of rough-hewn stone, covered with moss and ivy. The entrance to the east is a massive wooden door with iron hinges, above which hangs a coat of arms bearing the image of a stag, a symbol of Scotland's ancient kings. Near the manor house is a weathered stone bench overlooking a small pond with a stone bridge leading to a feral garden to the northeast. The air is cool and damp and you can hear the sound of a nearby stream as it meanders through the snowy, lush landscape of the surrounding forest.",
        e_to Vestibule,
        ne_to FeralGarden,
  has light scored;

Object -> car "car",
  with name 'car' 'Vauxhall' 'Astra' 'trunk',
    initial "Cora's car, a 1980 Vauxhall Astra, is parked in the forecourt.",
    description "It's actually a nice car. You just bought it a few days ago when you moved from the US to Scotland.",
    before [;
        Enter, SwitchOn:
          "It makes no sense to use the car. There has been a lot of snow during the night and the only road that leads from the manor house back to civilisation is impassable. Great. As cliched as the plot of a horror B movie.";
        Push, Pull, Turn: 
          "Doesn't move a bit. Here's the proof that you don't have superpowers.";
        Open, Search:
          if (foldingspade in self) {
            move foldingspade to Forecourt;
            "You search the car and notice your folding spade is in the trunk. You already wondered where it might be. Seems like Cora borrowed it when she bought the Christmas tree.";
          }
    ],
  has static;

Object -> -> foldingspade "folding spade",
  with name 'folding' 'spade',
    initial "You see your folding spade lying in the trunk of the car.",
    description "A US Army folding spade, pretty indestructible.",
  has scored;

Outside FeralGarden "Feral Garden"
  with name 'feral' 'garden',
        description "You are standing admidst barren, snow-covered thornbushes and frost-covered grass. Vines and thorny brambles creep up the walls of the manor house. To the west is an old dovecote, its structure weathered but still standing. A trail to the north leads deeper into the forest, disappearing into the shadows of the trees. You can hear the crunch of snow under your feet and the distant wail of the wind. The scent of the winter air fills your senses with the crisp, refreshing aroma of the season. A cobbled path leads back southwest to the forecourt.",
        n_to ForestTrail,
        w_to Dovecote,
        sw_to Forecourt,
  has light scored;

Outside ForestTrail "Forest Trail"
  with name 'forest' 'trail',
        description "Snow-covered trees tower above you, their branches creaking and swaying in the frigid wind. The trail ahead of you is barely visible, winding its way further northwest through the thick undergrowth and disappearing into the dense woods. The trail is narrow, flanked on either side by a wall of gnarled trees and prickly bushes, their leafless branches stretching out like skeletal fingers. It's very quiet except for the occasional rustle of a small animal darting through the underbrush. Further south, the path leads back to the feral garden of Blackwood Manor.",
        s_to FeralGarden,
        nw_to Ruins,
  has light scored;

Room Dovecote "Dovecote"
  with name 'dovecote',
        description "As you look around the dovecote, you notice that it has been repurposed as a garden shed. The inside is cluttered with garden tools, bags of soil and pots of various sizes. You can see that the nesting holes have been blocked off, but the faint scent of bird droppings still lingers in the air. Dovecotes were a common feature of Scottish manor and castle estates and served a number of purposes. One was to provide a source of fresh meat and eggs for the lord and his family, but it was also a symbol of status and wealth. A door to the east leads back into the garden.",
        e_to FeralGarden,
        cheap_scenery
             11 'garden' 'tools' "A million tools but neither a shovel nor a spade. This is hilarious.",
  has light scored;

Object -> spot "spot",
  with name 'spot' 'hole',
    initial "You have no idea how this can be but you recognise the dovecote from your dream, even though it looked different. You notice the spot where something has been buried.",
    description [;
        if (leatherbag in self) "If something was buried here, it was probably a long time ago. The spot looks absolutely unsuspicious.";
        else "There's now a hole in the ground where you dug.";
    ],
  has static;

Object -> -> leatherbag "leather pouch",
  with name 'leather' 'pouch',
    initial "Your digging at the spot revealed an old leather pouch.",
    description "The leather pouch looks very old, almost medieval. The many years in the ground have not done it any harm.",
    before [;
        Open:
          if (leatherbag.player_progress == true) rfalse;
          self.player_progress = true;
          PROGRESS_LEVEL = 2; ! ready to consult someone about the man with the scar
          "You open the leather pouch, revealing a mysterious golden key. A thought occurs to you. Something was indeed buried in the dovecote. That means the man with the scar must be real. You should consult someone who knows more about the manor.";
    ],
    player_progress false,
  has container openable scored;

Object -> -> -> goldenkey "golden key",
  with name 'golden' 'key' 'Christian' 'symbolism',
    description "A golden key, richly decorated with Christian symbolism. Definitely medieval.",
  has scored;

Outside Ruins "Ruins"
  with name 'ruins' 'village',
        description "You see the ruins of a small village, with only the overgrown stone remains of what were once two or three houses. The ruins are ancient and weathered, covered in a thick layer of snow and ice. The walls are crumbling, the arches distorted and twisted by years of decay. The village must have been abandoned centuries ago. The only sounds you can hear are the soft crunching of the snow beneath your feet and the distant howling of the wind. The ruins seem to be in a deep slumber, frozen in time. Strange shapes and shadows lurk in the corners of your vision, making you wonder if you're really alone in this place. The trail heads north and south from here.",
        n_to OverGrownCemetery,
        se_to ForestTrail,
        d_to Cellar,
  has light scored;

Room Cellar "Cellar"
  with name 'cellar',
        description "The musty air is thick with the smell of decay, and the dim light of your electric torch reveals cobwebbed corners and forgotten debris. You can hear the scurrying of rats and the faint dripping of water in the distance. As you explore further, you notice strange symbols etched into the walls. Old wooden barrels and rusted tools are scattered about the cellar floor. Some barrels appear to be intact, while others have long since rotted, spilling their contents onto the floor. You can hear whispers, but you can't make out what they're saying. It could be a language you've never heard before. In a corner is a dark alcove where a mysterious ancient tome glimmers in the light of your torch. Narrow, creaky stairs lead up to the ruins of the abandoned village.",
        u_to [;
          if (electrictorch in player && electrictorch has on) {
            give electrictorch ~on;
            give electrictorch ~light;
            print "You turn off your electric torch to conserve battery power.";
            new_line;
          }
          return Ruins;
        ],
   has ~light scored;

Outside OverGrownCemetery "Overgrown Graveyard"
  with name 'overgrown' 'graveyard',
        description "The winter sun casts a pale light over the graveyard. The entrance gate is rusted and half-broken, with overgrown ivy vines creeping over its once-sturdy frame. The gravestones, some standing upright and others toppled over, are covered in thick moss and lichen. They are so old and weathered that it is difficult to read the inscriptions, but you can make out some of the names and dates carved into the stone. It seems like nobody's been buried here after 1597, which gives you an idea about how long the village to the south is abandoned. It's unusually quiet, as if the whole forest prevents this forgotten ground. The silence is so complete that you can hear your own breathing, and every footstep you take seems to echo through the stillness.",
        s_to Ruins,
  has light scored;

Room Undercroft "Undercroft"
  with name 'undercroft',
        description "Your electric torch illuminates ancient stone walls that seem to stretch into infinity. The architecture is imposing and grand, with soaring arches and vaulted ceilings. In the dim light, you can make out rusted weapons and tools hanging on the walls - longswords and halberds that once saw battle, pikes and spears that once defended the manor, and maces and morning stars that could crush a man's skull with a single blow. You also notice old tapestries and flags hanging from the walls, faded and tattered with time. They depict battles and heroic deeds, and you wonder how many of the people depicted in them might have once walked the halls of the manor. In one corner you see an ancient suit of armour, its once shining metal now covered in rust and cobwebs. You can almost hear the ghostly clanking of its joints as it marches off to war. Stone steps lead up to the chapel. A door leads to the northeast.",
        e_to SecretPassage,
        ne_to Crypt,
        u_to [;
          if (electrictorch in player && electrictorch has on) {
            give electrictorch ~on;
            give electrictorch ~light;
            print "You turn off your electric torch to conserve battery power.";
            new_line;
          }
          return TheChapel;
        ],
   has ~light scored;

Room SecretPassage "Secret Passage"
  with name 'secret' 'passage',
        description "As you make your way through the narrow passage, you notice that the walls are rough and uneven, carved out of solid stone. There is a heavy smell of damp earth and moss in the air. You can hear the sound of water dripping from the walls and ceiling, forming small puddles on the uneven floor. The tunnel twists and turns, curving around sharp corners, occasionally sloping down or up, stretching further east and west from here.",
        e_to Dungeon,
        w_to Undercroft,
   has ~light scored;

Room Crypt "Crypt"
  with name 'crypt',
        description "The smell of decay fills the air. You can barely make out the outlines of several stone coffins, neatly arranged in rows around the perimeter of the room, elaborately decorated with wonderful carvings and engravings. One coffin has been opened, revealing the remains of an ancient shroud and the faint outline of bones. To the west is a large door leading back to the undercroft. The ceiling is vaulted and made up of large stone slabs, some of which have cracked over time. You can also see rusted iron sconces on the walls, some of which still bear the remnants of long extinguished candles.",
        s_to Undercroft,
  has ~light scored;

Room Dungeon "Dungeon"
  with name 'dungeon',
        description "As you peer into the dungeon, you feel a chill run down your spine. The air is thick with the stench of decay. The room is small and claustrophobic, with low ceilings and walls that seem to press in on you from all sides. As your eyes adjust to the gloom, you see a set of chains dangling from the ceiling in one corner. They creak softly in the silence, as if beckoning you closer. Moving towards them, you catch sight of a wooden table covered in a macabre array of torture tools. A pair of pincers lies beside a rack, while a branding iron glows ominously in the light of your electric torch. You can't help but notice the stains which you suspect to be dry blood that mar the surface of the table, hinting at the unspeakable horrors that have taken place here. The very air itself seems to vibrate with a dark energy, as if the room is alive with malevolence. A cell door with rusty but solid metal bars to the east leads back to the passage from which you came. Someone spilled a line of white crystalline powder in front of it.",
        w_to SecretPassage,
   has ~light scored;

Room Corridor "Grand Hallway"
  with name 'corridor',
        description "You are standing in Blackwood's grand hallway, with fine tapestries and beautiful oil paintings on the walls, depicting landscapes and scenes from a bygone era. To the north, an old wooden door leads to the lounge. To the south, you get to the Solar. The bathroom is to the northwest. As you walk along the patterned carpet runner, you can feel the soft, plush fibres under your feet and notice that the design is in the repeating pattern of a Scottish tartan. The borders of the carpet are decorated with delicate flowers and vines. A spiral stone staircase leads down to the vestibule.",
        n_to Lounge,
        s_to Solar,
        nw_to Bathroom,
        d_to Vestibule,
  has light scored;

Room Solar "The Solar"
  with name 'solar' ,
        description "You find yourself in the solar, a grand chamber that was once the private quarters of the lord and lady of the manor. The room is filled with the trappings of luxury and refinement, from the rich tapestries that adorn the walls to the antique furniture. In one corner of the room is a large four-poster bed with heavy velvet curtains drawn around it. Next to the bed is a small bedside table with an oil lamp. There are also a few modern items scattered around the room. A rotary telephone sits on a small table beside the bed, and a portable television with rabbit-ear antennas stands on a low shelf in the corner. The window at the end of the chamber lets you look far across the manor lands. Doors lead east to the wardrobe and north back to the corridor.",
        n_to Corridor,
        e_to Wardrobe,
        cheap_scenery
             12 'manor' 'lands' 'window' "Rolling hills and forests now blanketed in a thick layer of snow stretch out before you, dotted with clumps of trees and winding streams."
             38 'thick' 'winding' 'rolling' 'layer' 'snow' 'hills' 'forest' 'forests' 'streams' 'clumps' 'trees' "The view takes your breath away."
             1 'rosie'
             [;
                Call:
                  <<Take telephone>>;
                default:
                  "Rosie is not here.";
             ],
  has light scored;

Object -> telephone "rotary telephone",
  with name 'rotary' 'bakelite' 'telephone' 'phone',
        description "This is a very old British bakelite telephone, probably from the 1940s. You wouldn't expect anything less in Blackwood. It seems that every piece in the manor has a story to tell.^^Use it with [call person].",
    before [;
        Take:
          if (PROGRESS_LEVEL < 2) "There's currently no need to call Rosie.";
          if (PROGRESS_LEVEL == 2) {
              PROGRESS_LEVEL = 3;
              if (~~ note in player or Solar) "You don't have Rosie's phone number.";
              print "You call Rosie. It seems as if the dial tone rings for an eternity but then she picks up the phone.^^You: Hey Rosie! I'm sorry to bother you. I have a question, and it may seem a little strange. Can you think of anything in relation to Blackwood about a man with a scar on his hand?^^Rosie: That's indeed a strange question, Sir Thomas. However, I think you should have a look at the gallery of ancestors. You may be interested in the painting of Lord Archibald Bain. The people here believe that he was not a good man. They claim he wiped out an entire village during the great Scottish witch hunts and was even responsible for the disappearance of some children in the manor's parish. Probably just old wives' tales, but there is a grain of truth in every tale they say.^^You: Indeed doesn't sound like a very pleasant fellow. Thank you Rosie, I'll have a look.^^You end the conversation.";
              new_line; rtrue;
          }
          else "The line is dead. Strange.";
    ],
  has scenery;

Room Wardrobe "Dressing Room"
  with name 'dressing' 'room',
        description "As you enter the dressing room, you are greeted by a warm, rustic atmosphere. The area is lit by a few small windows with thick wooden frames that allow just enough light to filter in. Along one wall is a row of old-fashioned wardrobes made of dark, polished wood, their doors adorned with floral motifs and brass handles. Some modern elements stand out among the traditional decor. A small clock radio sits on a shelf near the armchair and a computer workstation is tucked away in a corner of the room. You brought the computer, an Apple Macintosh Plus, with you from New York. A printer and some floppy discs are neatly arranged on the desk next to it. To the west, a door leads back to the solar.",
        w_to Solar,
  has light scored;

Room Bathroom "Bathroom"
  with name 'bathroom',
        description "The bathroom is illuminated by a crystal chandelier, casting a warm and inviting glow on the marble floors and walls. In the centre of the room is a luxurious claw-foot bath with shiny silver taps and a pristine black iron drain. Against the wall is a polished wooden cabinet with gleaming brass handles. To your left, you can see a silver basin with a crystal-clear mirror above it. In the corner of the room is a sleek, modern toilet with a streamlined design. A stunning Celtic knot is carved into a stone that's embedded in the wall above the bath. A door to the south leads back to the grand corridor.",
        s_to Corridor,
        cheap_scenery
             11 'dusty' 'niche' 
             [;
              Examine, Touch, Smell:
                if (statuette in garnets) {
                  "You can't see such a thing here. What are you talking about?";
                }
                else "Ugh!";
             ],
  has light scored;

Object -> knot "celtic knot",
  with name 'celtic' 'knot' 'endless' 'stone',
    description "With amazement you admire the work of art perpetuated in the stone. It is certainly a lot older than the rest of the bathroom. Celtic knots, in Scottish Gaelic ~snaidhm Ceilteach~, are loops that have no beginning or end, symbolising infinity. They are known for their adaption for use in the ornamentation of early Christian monuments and manuscripts, such as the Lindisfarne Gospels, a book produced around the years 715-720 in the monastery at Lindisfarne, off the coast of the Kingdom of Northumbria. In this context it makes sense to find Insular art here, as it likely dates back to the time when Blackwood was a monastery. Under the knot there are four garnets set into the stone.",
    before [;
        Push, Pull:
          "The knot itself doesn't move at all.";
    ],
  has scenery transparent;

Object -> -> garnets "garnets",
  with name 'gems' 'gemstones' 'garnet' 'garnets',
    description "The use of garnets as gemstones has been common since the Bronze Age. Garnets and other heavy minerals occur in many of the sedimentary beds of the North of England and Scotland. So it is not surprising to find them here alongside this wonderful Celtic knot.",
    before [;
        Pull:
          "You cannot pull the garnets. And you probably shouldn't anyway.";
        Touch:
          "You gently touch the garnets and feel the fine cut. They are incredibly beautiful.";
        Push:
          if (PROGRESS_LEVEL == 7) "You needn't worry about this anymore. You've already got what you came for.";
          if (~~ PROGRESS_LEVEL == 6) "It seems you can actually push in the garnets but no matter what you try, they always return to their original position.";
          "You need to be more precise on this one [push garnets in order 1234].";
        PushInOrder:
          if (PROGRESS_LEVEL == 7) <<Push garnets>>;
          if (~~ PROGRESS_LEVEL == 6) <<Push garnets>>;
          if (second == 4423) {
            PROGRESS_LEVEL = 7;
            move statuette to location;
            IncrementScore();
            "You can hear the sound of an ancient mechanism being activated behind the wall. Part of the Celtc knot opens to reveal a bronze statuette lying in a dusty niche behind it.";
          }
          "That didn't work. A wrong combination maybe?";
    ],
  has scenery;

Object -> -> -> statuette "bronze statuette",
  with name 'bronze' 'statuette' 'angel' 'archangel' 'gabriel' 'horn',
    initial "In a dusty niche inside the knot you see a bronze statuette.",
    description "You admire an angel blowing a horn. Could it be the Archangel Gabriel announcing Judgement day? Anyway, the statuette looks like it is part of something. The challenge will be finding that something.",
    before [;
      Take:
        if (self in panel) "You've put it where it belongs, so you don't need to pick it up again.";
        if (self.player_progress == false) {
          self.player_progress = true;
          move self to player;
          "As soon as you move the bronze statuette, the mechanism activates and hides the niche behind the knot again. But you were quick enough to pull out the statuette which is now in your possession.";
        }
        rfalse;
    ],
    player_progress false,
  has scored;

Room Lounge "Lounge"
  with name 'lounge' 'leaded' 'window',
        description "The decor of the lounge is traditional, with dark wooden paneling and plush furniture, such as the comfortable armchairs, perfect for cozying up with a good book or a glass of whiskey. In one corner of the room, you notice a stereo system with a turntable and several vinyl records scattered nearby. A set of speakers flank the turntable. Nearby, you spot a well-stocked bar, with an array of liquors and mixers on offer. The bar is made of polished wood and has several high-backed stools pulled up to it. On the opposite side of the room, you see a large TV with a VHS system and video tapes stacked neatly nearby. A grandfather clock breaks the silence with its ticks. An exit to the south leads back to the grand corridor.",
        s_to Corridor,
        cheap_scenery
            4 'speaker' 'speakers' 'Wharfedale' 'Diamonds' "Wow, these are Wharfedale Diamonds, every hi-fi enthusiast's dream."
            6 'tapes' 'movies' 'films' 'VHS' 'tape' 'video'
            [;
              Examine:
                "So many great films in one place. It won't get boring here if the manor is snowed in over the holidays.";
              Touch:
                "You feel plastic. What did you expext?";
              SwitchOn, Take:
                print "(in order to watch a film)^";
                <<SwitchOn vhssystem>>;
            ]
            4 'vinyl' 'record' 'records' 'covers' 
            [;
              Examine: 
                "It seems that Cora found some of your albums in one of the moving boxes and brought them here for you. You can see Joy Division, New Model Army, Tears for Fears, Depeche Mode, Skeletal Family, The Cure and Killing Joke among others. Look at all those wonderful covers.";
              Smell:
                "Nothing beats the smell of vinyl records, the scent of your generation.";
              Touch: 
                "Feels like vinyl.";
              SwitchOn, Take, Open:
                print "(in order to listen to some music)^";
                <<SwitchOn stereosystem>>;
            ],
  has light scored;

Object -> vhssystem "VHS system",
  with name 'VHS' 'system' 'recorder' 'Ferguson' 'video',
        description "It's a Ferguson VHS video recorder, probably from 1982. Ferguson products are made in Britain and back when this recorder came out it roughly cost a monthly wage. The build quality is exceptional.",
    before [;
      SwitchOn:
          if (PROGRESS_LEVEL < 4) "You're currently not in the mood to watch a film.";
          else "VHS follow-up not implemented." ;
    ],
    player_progress false,
  has scenery;

Object -> stereosystem "stereo system",
  with name 'stereo' 'system' 'turntable' 'player' 'Pioneer' 'casing' 'metal',
    description "It's a state-of-the-art Pioneer system. Something you didn't expect to find here in Blackwood.",
    before [;
        Touch:
          "Your hand glides over the metal casing in awe.";
        Push, Pull:
          "What a glorious idea. Better not. You don't want to break it.";
        SwitchOn:
          if (Rosie in vestibule) "Before you're going to relax here, you should talk to Rosie first. She's probably still waiting in the vestibule for you.";
          if (self.player_progress == false) {
            print "You decide to play ~No Rest for the Wicked~ by New Model Army, one of your favourite records. You sit down in one of the armchairs and close your eyes. The ethereal guitars with heavy chorus effects gently lull you to sleep. Further and further you are carried away, back to a life lived long ago.^^You see the world with different eyes now. Not your eyes. Hastily you move through a garden that you somehow know. It is summer, and the scent of wild flowers fills the air. You walk purposefully towards a tower-like building, driven by one of the strongest emotions, fear.^^Inside, it takes a moment for your eyes, or rather the eyes through which you see, to adjust to the changed lighting conditions. Somehow the place seems historic, as if you have travelled back hundreds of years in time. You gaze up and see numerous birds lurking in holes in the walls. Then you look down and notice strange hands burying something. You can't make out what it is, but on the back of the hand, which is definitely not yours, you recognise a large and very distinctive scar.^^Before you can make out any more details, something pulls you back into your body. As you open your eyes, you feel the warm morning sun shining in your face through the leaded window. Obviously Cora didn't want to wake you last night and covered you with a warm blanket instead.^^What a strange dream it was. A mysterious place that seemed somehow familiar, the fear, the unknown object being buried, the scar. You have to catch up with Cora. She's probably still asleep in the solar, or already making breakfast.";
            new_line;
            move snowstorm to Limbo; ! snowstorm is over now
            PROGRESS_LEVEL = 1; ! progress bump, environment to waking up after the dream
            move Cora to Solar;
            self.player_progress = true;
            IncrementScore(); rtrue;
          }
          if (self.player_progress == true )"You don't feel like listening to music now.";
    ],
    player_progress false,
  has scenery;

[EndingSequence;
  print "^Just as quickly as it began, it ended. What a shame.";
  deadflag = 2; ! you have won!
];

[ Initialise; ! init vars and start the game
  score = 0; ! your score is at 0 when you start the game, makes sense, right? 
  notify_mode = false; ! deactivate score notifications if set false
  lookmode = 2; ! activate verbose look mode
  move Rosie to vestibule;
  InitTalk(); ! initialize the talk menu extension

  thedark.description = "It is pitch black. You are likely to be eaten by... actually, you don't really want to know what's lurking here in the dark."; ! I hail you, Infocom!

  player.before = PlayerBefore; ! player.before override, see routine implementation
  player.parse_name = PlayerParseName; ! own routine that parses the player's name
  player.description = "Look at you. The author that came to Scotland to find inspiration. For some reason you feel like being stuck in a bad story now.";

  QuoteBox(quote_1);
  print "Writer's block. And everyone waits for the next big thing. Oh, how you still love New York but it cast a dark veil over your inspiration. The change of scenery was needed. You didn't hesitate for a second when Cora told you about a historic manor house for sale near her home in the Scottish Highlands. It's only been a few days since you moved in and you haven't had much time to explore the property. Christmas is just around the corner and moving boxes are still piling up everywhere, but a bit of chaos is fine. The seclusion out here makes up for it. You might even manage to write something during the contemplative days. How about a ghost story?^^";
];

#ifdef DEBUG; 
  [ CheatmodeSub; ! hic sunt dracones... trigger with 'aenima' magic verb in debug mode
    PROGRESS_LEVEL = 7;
    move snowstorm to Limbo;
    move Rosie to Limbo;
    stereosystem.player_progress = true;
    MoveFloatingObjects();
    move Cora to Solar;
    PlayerTo(TheChapel);
  ];
#endif;

! adding the ability of making non-animate objects referred to with "him" or "her"
[ PronounNotice p_object;
	if (p_object == 0 or player or Directions) return;
	if (p_object has pluralname) {
    themobj = p_object;
	}
  else if (p_object has male or female or neuter) {
    SetPronoun(p_object);
  }
  else if (p_object has animate) {
    SetPronoun(p_object);
	}
  else itobj = p_object;
];

[ SetPronoun p_object;
  if (p_object has female) herobj = p_object;
	else if (p_object has neuter) itobj = p_object;
	else himobj = p_object;
];

#ifV5;
! we use our own routine as Puny's might cause Infocom z5 terps to misbehave
[ DrawStatusLine width posa s1 s2; !
   _StatusLineHeight(statusline_height); @set_window 1; @set_cursor 1 1; style reverse;
   width = 0->33;
   FastSpaces (width);
   @set_cursor 1 2; _PrintObjName(location);
   if (width > 76)
   {
	   s1 = NumWidth(status_field_1);
	   s2 = NumWidth(status_field_2);
	   posa = width-26;
	   @set_cursor 1 posa;
	   print " Score: ", status_field_1;
	   FastSpaces(6-s1);
       print "Moves: ", status_field_2;
	   FastSpaces(6-s2);
   }
   else if (width > 39)
   {
	   s1 = NumWidth(status_field_1);
	   s2 = NumWidth(status_field_2);
	   posa = width - 9 - s1 - s2;
	   @set_cursor 1 posa;
	   print " Score: ", status_field_1, "/", status_field_2;
	   @print_char ' ';
   }
   @set_cursor 1 1; style roman; @set_window 0;
];

[NumWidth num width;
	width = 1;
	if(num < 0) {
		width++;
		num = -num;
	}
	if(num >= 10)
	{
	   width++;
	   if(num >= 100)
	   {
		   width++;
		   if(num >= 1000)
		   {
			   width++;
		   }
	   }
	}
	return width;
];
#endif;

[ AboutSub;
  print_ret (string)Story, " is copyright (c) 2023 by Stefan Vogt and Moonmist Entertainment.^^You may freely distribute the game, but you have to link to <8bitgames.itch.io>. This work may not be sold or included in any for-profit collection without written permission from the author.^^Please send bug reports to ", "<stefan@@64","8-bit.info>.^^For acknowledgements and credits, please type CREDITS.";
];

[ CreditsSub;
  print_ret (string)Story, " is dedicated to Andreas Gustafsson.^^~Night gathers, and now my watch begins. For this night and all the nights to come.~^^-- George R.R. Martin^   A Song of Ice and Fire^^Beta ghost hunters: Ant Stiller, Marco Innocenti, Soenke Wolfgramm and Fredrik Ramsberg.^^This game was created using the Inform language by Graham Nelson and it utilizes PunyInform, a custom library by Fredrik Ramsberg and Johan Berntsson.";
];

[ UseSub;
  "This game does not support the [use] verb. You need to be more specific.";
];

[ HelpSub;
  "Try to [examine] everything that is mentioned in room descriptions, otherwise you might miss important hints. Note that [examine] and [search] trigger different actions in this game.^^Your actions may also create different results based on the game's progress level. For example, why would you [dig] something if you don't know what you're looking for? If you [examine] a certain object, it could reveal new information once you learned something in the course of the game.^^The game's package contains a PlayIF card that explains basic gameplay and many synonyms will enhance what you read on it.^^Conversations are not as complex as in Infocom titles. It is sufficient to type [talk to NPC] or [ask NPC].^^Use the [save] command to store your progress and [restore] to load it again.";
];

[ KissSub;
  "Excellent idea. Let's just make out with literally everything and everyone you come across.";
];

[ XyzzySub;
  "You perceive a mysterious whisper. Coming from everywhere and nowhere, yet present, it says ~Nice try.~";
];

[ LookUnderSub;
  "You don't have to look under any objects in this game. The only two verbs you need in this context are [examine] and [search].";
];

#ifV3;
[ UndoSub;
  "This version of the game does not support the UNDO command.";
];
#endif;

[ UnlockWithoutKeySub;
  if (noun has lockable) print_ret "Try being more specific. You probably want to unlock ", (the) noun, " with a certain object?";
  "You cannot unlock this object.";
];

[ TurnAroundSub;
  print_ret (string)MESS_TURN_SELF;
];

[ PlayerBefore;
    Turn:
      print_ret (string)MESS_TURN_SELF;
    Attack:
      "As hopeless as the situation may be, this is never a way out.";
    default:
      rfalse;
];

[ PlayerParseName n;
		n=0;
		while (NextWord()=='me' or 'myself' or 'self' or 'yourself' or 'Thomas') {n++;}
		return n;
];

[ FillSub;
  "You are not referring to liquid.";
];

[ FillErrorSub;
  "The fill command wants you to be more specific.^^[fill object with something]";
];

[ DigSub;
  "That's not something you can dig.";
];

[ DigErrorSub;
  "The dig command wants you to be more specific.^^[dig something with object]";
];

[ CallSub;
  if (location ~= Solar) "There must be telephone nearby to call someone.";
  else {
    if (noun == 'Cora' or Cora) "There's no need to call your wife.";
    if (noun == 'Ghostbusters') {
      "Voice on the phone: Hi, thanks for dialing 555-2368. You've reached the Ghostbusters hotline. We're not in right now, we're busy catching ghosts. You may leave your message after the beep. We'll get back to you as soon as possible. In urgent matters say to yourself: I ain't afraid of no ghost.";
    }
    if (noun == 'Cthulhu') {
      "Voice on phone: Hello, the office of the Great Old Ones, department Master of R'lyeh is temporarily unmanned. His deity, the great dreamer Cthulhu, is currently on a higher cosmic plane enjoying his weekly tentacle massage. Please leave a message after the tone.";
    }
    else "That's not a valid person to call.";
  }
];

[ PushInOrderSub;
  "That's not something you can push in order.";
];

[ PushOrderErrorSub;
  "If you want to push something in order, you need to add a sequence [push object in order 1234].";
];

! grammar starts here

#ifdef DEBUG;
Verb 'aenima' * -> Cheatmode;
#endif;

Verb 'about' * -> About;
Verb 'credits' * -> Credits;
Verb 'help' * -> Help;

Verb 'kiss' * -> Kiss
            * noun -> Kiss;

Verb 'use' * -> Use
           * noun -> Use
           * noun 'with'/'on'/'in'/'at' noun -> Use;

Verb 'xyzzy' * -> Xyzzy;

Verb 'call' * -> Call
            * noun -> Call
            * special -> Call;

#ifV3;
Verb 'undo' * -> Undo;
#endif;

Extend 'turn' * 'around' -> TurnAround;
Extend 'turn' * 'over' noun -> Turn;

Extend 'examine' * 'back' 'of' noun -> Turn;

Extend 'push' * noun 'in' 'order' special -> PushInOrder
              * noun 'in' 'order' -> PushOrderError;

Extend 'look' * 'under' noun -> LookUnder;
Extend 'look' * 'through'/'out' noun -> Examine;
Extend 'look' * 'out' 'of' noun -> Examine;

Extend 'unlock' first * noun -> UnlockWithoutKey;

Extend 'attack' * held 'at'/'on' noun -> Attack;

Extend 'fill' replace
  * -> FillError
  * noun -> FillError
  * held 'with' noun -> Fill
  * noun 'in'/'into' held -> Fill;

Extend 'dig' replace
  * -> DigError
	* noun -> DigError
	* noun 'with' held -> Dig;

Extend 'speak' replace
  * noun -> Talk
  * 'to'/'with' noun -> Talk;

Extend 'ask' replace
  * noun -> Talk
  * noun 'for' noun -> AskFor;

Verb meta 'continue' = 'again';
Verb 'grab' = 'take';
Verb 'kick' = 'attack';
Verb 'flip' = 'turn';
Verb 'ring' = 'call';
Verb 'dial' = 'call';
Verb 'consult' = 'open';
Verb 'pour' = 'fill';
Verb 'play' = 'switch';

end;
