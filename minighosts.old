!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $MAX_ABBREVS=96
!% $TRANSCRIPT_FORMAT=1
!% $ZCODE_LESS_DICT_DATA=1
!% -r
!% -e
!% $OMIT_SYMBOL_TABLE=1
! ----------------------------------------------------------------------------
!  MINI GHOSTS OF BLACKWOOD MANOR
!  An interactive Horror by Stefan Vogt
!  Copyright (c) 2023 Moonmist Entertainment
!  and Fusion Retro Books / ZZAP!64 
!  http://8bitgames.itch.io
! ----------------------------------------------------------------------------

! IFID (http://babel.ifarchive.org) -- generate with https://uuidgenerator.net
Array UUID_ARRAY string "UUID://76f253f4-9cfd-411d-a7a2-fd28d3e08401//";
#Ifdef UUID_ARRAY; #endif;

Constant Story    "Mini Ghosts of Blackwood Manor";
Constant Headline "^An interactive Horror by Stefan Vogt^\
                   (c) 2023 Moonmist Entertainment^\
                   and Fusion Retro Books / ZZAP!64^";
Release 1;

! game-specific defines
!Constant DEBUG; ! deactivate for release
Constant CUSTOM_ABBREVIATIONS;
Constant MAX_CARRIED = 20;
Constant OPTIONAL_SCORED;
Constant OPTIONAL_FULL_SCORE;
Constant OBJECT_SCORE = 5;
Constant ROOM_SCORE = 5;
Constant MAX_SCORE 415;
Constant OPTIONAL_FULL_DIRECTIONS;
Constant OPTIONAL_MANUAL_REACTIVE;
Constant OPTIONAL_MANUAL_SCOPE;
Constant OPTIONAL_MANUAL_SCOPE_BOOST;
!Constant OPTIONAL_PROVIDE_UNDO;
Constant INITIAL_LOCATION_VALUE = GreatHall;

! 0 production build, 1 notification on errors, 2 full error messages
Constant RUNTIME_ERRORS = 0;

Include ">abbrvs.h"; ! load optimized abbreviations
Include "globals.h"; ! Puny library file import
Include ">punyhacks.h"; ! Moonmist library file import

! 0 = snowstorm raging / game begins
! 1 = waking up after falling asleep in lounge
! 2 = consult Rosie for the man with the scar
! 3 = Rosie consulted, line becomes dead
! 4 = searched gallery and found portrait of Lord Bain
! 5 = Turned portrait and found elevation
! 6 = cut portrait and found parchment
! 7 = endless knot riddle solved
! 8 = Saints riddle in crypt solved
! 9 = Amulet investigated, research necessary
!10 = found reference of amulet and Saint 
!11 = voice advised to meet in forest
!12 = found evidence and location of the lost village
!13 = talked with Ysabella about the ritual
!14 = Cora choice is made (it better is a good choice)
Global PROGRESS_LEVEL = 0; ! tracks the game's progress level
!Global Choice_Ending_Bad = false;
Global Choice_Ending_Neutral = false;
Global Choice_Ending_Good = false;
Global Ritual_started = false;

! declare recurring properties to optimize story file size for Z-machine v5 spec
Property cheap_scenery;      ! used by Puny's cheap scenery extension
Property cheap_scenery_pt2;  ! additional cheap scenery to work around the 32 entry limit of properties
Property cheap_scenery_pt3;  ! additional cheap scenery to work around the 32 entry limit of properties
Property player_progress;    ! common property to be used when the player makes progress with an object
Property countdown;          ! property which can be used together with daeons and timers
Property talk_start;         ! used by Puny's talk menu extension

! Constants for puny extensions
Constant FLAG_COUNT 42;
Constant TM_MSG_TOPICS_DEPLETED "You've run out of topics to talk.";
Constant TM_MSG_EXIT "You decide to catch up later.";

! import Puny extensions
Include "ext_quote_box.h";
Include "ext_flags.h";
Include "ext_talk_menu.h";
Include "ext_cheap_scenery.h";

! Z-machine colour Globals (colour alloc see init routine)
! 2 black / 3 red / 4 green / 5 yellow / 6 blue / 7 magenta / 8 cyan / 9 white
!Global clr_default_fg = CLR_WHITE;
!Global clr_emphasis_fg = CLR_CYAN;

[PrintRank; ! this uses Puny's PrintRank feature
  print ", earning you the rank: ";
  !ChangeFgColour(clr_emphasis_fg);
  if (score >= 415) print "The Exorcist";
  else if (score >= 300)  print "Ectoplasma Connoisseur";
  else if (score >= 50)  print "Ghost Hunter Apprentice";
  else print "Absolute Zero";
  !ChangeFgColour(clr_default_fg);
  if (~~ deadflag) "."; else print ".";
  new_line; new_line;
  print "You have reached the";
  !ChangeFgColour(clr_emphasis_fg);
  !if (Choice_Ending_Bad == true) print " [bad] ";
  if (Choice_Ending_Neutral == true) print " [neutral] ";
  if (Choice_Ending_Good == true) print " [good] ";
  !ChangeFgColour(clr_default_fg);
  "ending.";
];

Array quote_start --> 7 28
"How you have fallen from"
"heaven, morning star, son of"
"the dawn! You have been cast" 
"down to the earth, you who"
"once laid low the nations!"
"                            "
"             -- Isaiah 14:12";

Array quote_part1 --> 5 31
"PART I: Once upon the Cross"
"                            "
"Heaven wept, time stood still."
"High above, the wrath of god,"
"below, three crosses on a hill.";

Array quote_part2 --> 6 31
"PART II: The Ritual"
"                            "
"As wolf among sheep I wandered,"
"seeding lies and temptation."
"Watch me as I become flesh,"
"reverting God's creation.";

! will be triggered when the room is described
! [ LookRoutine;
! ];

! will be triggered when you visit a new room
! [ NewRoom;
!   if (PROGRESS_LEVEL == 13 && location == TheChapel) {
!     PROGRESS_LEVEL = 14;
!     IncrementScore();
!     print "As you enter, you see Rosie and Cora waiting for you. Rosie seems very agitated.^^Rosie: Sir Thomas, thank God you're here at last. I have been so worried about you since you called me and asked about Lord Bain. Blackwood is an unusual place, where long forgotten secrets lurk in the shadows. Sometimes they reveal themselves and draw you into a thicket of dark fates and unfulfilled dreams. This is happening to you right now. I am worried, so I wanted to offer Lady Cora to accompany me home until you have done what you have to do here. Would that be okay for you?^^";
!     !ChangeFgColour(clr_emphasis_fg);
!     print "[You now have the choice to let your wife Cora go with Rosie. Do support Cora leaving with Rosie? Please answer YES or NO.]^";
!     !ChangeFgColour(clr_default_fg);
!     if (YesOrNo() == true) {
!       print "^You: Darling, I think Rosie is right. Something is happening here that is hard to put into words and I want you to be safe. Please trust me.^Cora: Of course I trust you. I'll go with Rosie. When all this is over you better have a pretty good excuse why there's a goddamn hole in the floor of the chapel. I love you!^^Cora waves to you and leaves the manor with Rosie. Now you are on your own in...^";
!       Choice_Ending_Bad = true;
!     }
!     else {
!       print "You: Thank you, Rosie, but I don't think that's necessary.^Rosie: I understand. You're probably right and I'm most likely just getting carried away.^^Rosie bows and then leaves, noticeably worried.^^Cora: What is this all about?^You: Strictly speaking, Rosie is right. There is something going on here that I can't explain. I want you to be safe but for some reason my gut told me it's not a good idea to go with Rosie. Instead I want you to take your car and drive to your sister in Inverness. Please trust me.^Cora: Of course I trust you. I'll take the car. When all this is over you better have a pretty good excuse why there's a goddamn hole in the floor of the chapel. I love you!^^Cora waves to you and leaves the manor alone. Now you are on your own in...^";
!       move car to Limbo;
!       move LordBain to Dovecote;
! 	    scope_modified = true;
!     }
!   }
! ];

Include "puny.h"; ! puny library file import
Include ">moonmist.h"; ! Moonmist library file import

Class SaintPlate
  with name 'saint' 'St' 'St.' 'plate' 'silver',
    article "the",
    after [;
      Take:
        self.player_progress = false;
      Insert:
        if (self == cuthbert && second == lindisfarne) self.player_progress = true;
        if (self == kessog && second == inchtavannach) self.player_progress = true;
        if (self == adomnan && second == iona) self.player_progress = true;
        if (self == nathalan && second == tullich) self.player_progress = true;
        StartTimer(self, 0);
    ],
    time_out [;
      PlateAllocCheck();
    ],
    time_left,
    player_progress false,
  has scored;

Class Mural
  with name 'painting' 'picture' 'mural' 'murals' 'square' 'recess',
    before [;
      Touch:
        "You touch it very carefully so as not to damage the painting, but you feel nothing unusual.";
      Receive:
        if (CheckForPlate() == false) rtrue;
        if (CheckIfOccupied(self) == false) rtrue;
    ],
  has scenery container open;

! note that flags used in talk_array must have id 32-299
Constant F_ROSIE_TALKED_CORA 32;                      !1
Constant F_ROSIE_TALKED_HOLIDAYS 33;                  !2
Constant F_ROSIE_TALKED_SNOWSTORM 34;                 !3
Constant F_Y_TALKED_FIRE 35;                          !4
Constant F_Y_TALKED_LIVING 36;                        !5
Constant F_Y_TALKED_DEAD 37;                          !6
Constant F_Y_TALKED_BLOOD 38;                         !7

! talk_menu array -- STATUS [ID 300-600] TOPIC PLAYERSAYS NPCSAYS [FLAGREF|UNLOCKREF|ROUTINE]*
! customize: TM_ADD_BEFORE_AND_AFTER, TM_ADD_AFTER, TM_ADD_BEFORE. reaction line after NPC respone
Array talk_array -->
TM_NPC Rosie
30 "Preparations" "Hey Rosie, how are the preparations going?" "I am finished now, Sir Thomas. The manor is cleaned, I've made bread and biscuits for you and Lady Cora and the fridge is stocked with fresh vegetables and meat. It will be a lovely Christmas." 1 2 3
0 "Cora" "Is Cora back?" "The Lady has not yet returned from her visit to town. She wanted to buy a few things and come back in the afternoon, so she should be home soon." F_ROSIE_TALKED_CORA RosieFinalTopicVestibule
0 "Holidays" "I hope you enjoy your time off." "We are going to take it easy, just like every year. That's what I love about Christmas. The world slows down a bit and you reflect on the essentials. I'll be back for you and the Lady in the new year." F_ROSIE_TALKED_HOLIDAYS RosieFinalTopicVestibule
0 "Snowstorm" "It's quite a heavy snowstorm out there. Can you make it home?" "No worries, Sir Thomas. As you know, our house belongs to the manor. It's only a ten minute walk. It'll be fine." F_ROSIE_TALKED_SNOWSTORM RosieFinalTopicVestibule
0 300 "Blackwood" "Thank you, Rosie. A merry Christmas for you and your loved ones. Since Blackwood is new to us, may I call you in case we have a question?" "Of course, Sir Thomas! I left you my number in the kitchen." RosieLeavesManor
TM_NPC Cora
30 "Shopping" "Good morning, darling! I hope you slept well. How was the shopping?" "Hi, honey! I don't think I've ever slept better. This bed is so incredibly comfortable. I probably bought too much stuff though. And Inverness hasn't changed at all." 1
0 "Inverness" "Did you enjoy being home again?" "You know I haven't been there since Mum died in 1975. It felt so strange to be walking these streets after more than eleven years. I'll probably get used to it again." 1
0 "Breakfast" "Would you like some breakfast?" "Thank you, sweetie. I am not hungry. I had a coffee and now all I need to be happy is this bed and this book." 1 2
0 "Book" "What are you reading?" "Well, I found this book about the Scottish witch hunts lying around. Pretty morbid, isn't it?" IncrementScore
0 "Dream" "I had a strange dream tonight. There was a building with holes in the walls where birds were nesting." "Sounds like an old dovecote. You know we have one on the manor grounds, don't you?" IncrementScore
TM_NPC Ysabella
30 "Graveyard" TM_ADD_AFTER "What's your name? What are you doing here?" "She seems to be filled with infinite sadness. Without knowing her fate, you feel very sorry for her." "My name is Ysabella. I am waiting for my son to return." 1 IncrementScore
0 "Son" "Your son? What happenend?" "Lord Bain's henchmen brought him to the manor after burning down our village. They wanted to purge the evil from him. What evil you may ask yourself? There was no evil in him. He was just a boy and did nothing wrong. When they were done with him, they buried him next to me, here on this forgotten ground. But his spirit seems to be trapped in Blackwood Manor, as if something is preventing him from leaving. And so I wait for him. Desperately. Together we can leave this place and step into the light but I don't want to let got until I have my little boy back. How can people be so cruel? Doesn't the Bible teach you to love your neighbour as yourself?" "She sobs and shakes her head." 1 2 3 IncrementScore
0 "Time" TM_ADD_BEFORE "What you are about to ask breaks your heart." "Are you aware that you have been waiting here for almost 400 years?" "Has it been that long? I don't know. I no longer have any sense of time. Maybe it was that long? Was it? Oh no."
0 "Lord Bain" "So Lord Bain did this to you?" "To do God's will. He said an angel had appeared to him and ordered him to destroy our village, all of us, to prevent the reversal of God's creation. Evil would walk among us and it would open the gates of chaos if he wouldn't prevent this from happening. If you ask me, what appeared to him was certainly no angel. Maybe he was just mad." IncrementScore
0 "Help" "I am so sorry to hear this. What can I do to help you?" "You have access to the manor. There is a secret dungeon in Blackwood where my boy must be trapped. As I already said, something prevents him from leaving. Find out what it is. Look for something unusual in the dungeon itself. You can reach the dungeon from the undercroft underneath the chapel. There is a stone with a signet. When you push it, a secret passage should open. After my son has been freed, we must also perform a ritual here on this graveyard that will lead him the way back to me. It will be his guiding light." 1 IncrementScore RevealSignetStone
0 "Ritual" "What is needed for this ritual?" "For the guiding light ritual you need fire, something for the living, something for the dead and the blood of an innocent." "Ysabella looks at you and notices that you're confused." 1 2 3 4 IncrementScore YsabellaLevelUp
0 "Fire" "What do you mean with ~fire~?" "A few lit candles on my son's gravestone will do." F_Y_TALKED_FIRE
0 "Something for the living" "What do you mean with ~something for the living~?" "You'll need an item that keeps the living alive, a loaf of bread for example." F_Y_TALKED_LIVING
0 "Something for the dead" "What do you mean with ~something for the dead~?" "You are looking for an item that helps the sick and dying transition into the light. It could be oil for the final anointing or frankincense. You should find that somewhere in the chapel." F_Y_TALKED_DEAD
0 "Blood of an innocent" "I hope ~blood of an innocent~ doesn't mean what I think it means." "You need blood from a noble soul. Your wife seems to be a kind-hearted person. Ask her if she can give you a few drops of her blood. No one has to die for fulfilling this part of the ritual. It's a ritual of love." F_Y_TALKED_BLOOD
TM_NPC TheBoy
30 "Dungeon" "Hey, I am Thomas, I am here to help you getting out of this dungeon. Your mother sent me." "Mother? I miss her so much." "He looks sad. But his eyes... you feel cold shivers running down your spine as they follow you." IncrementScore
TM_NPC 0;

[ IncrementScore; ! routine to increment score in-game
  score = score + 5;
];

[RevealSignetStone;
  move sleepingCora to Limbo;
  move signetstone to Undercroft;
  scope_modified = true;
];

[YsabellaLevelUp;
  PROGRESS_LEVEL = 14;
];

[ RosieFinalTopicVestibule;
  if (FlagIsSet(F_ROSIE_TALKED_CORA, F_ROSIE_TALKED_HOLIDAYS, F_ROSIE_TALKED_SNOWSTORM)) {
    ActivateTopic(Rosie, 300);
  }
];

[ RosieLeavesManor;
	talk_menu_talking = false; 
  move Rosie to Limbo;
  scope_modified = true;
  IncrementScore();
	"^Rosie waves goodbye and leaves the manor.^^It will probably take a while for Cora to get back from the city. You wonder how you are going to pass the time until she returns, which reminds you that you wanted to check out the lounge.";
];

! limbo for objects that we don't need anymore, beware of the grues!
Object Limbo;

Object -> signetstone "signet stone",
  with name 'signet' 'stone' 'death' 'skeleton' 'cloak' 'bagpipe' 'jester',
    initial "You can see the signet stone that Ysabella mentioned.",
    description "A ~Danse Macabre~ theme is engraved on it. Death in the form of a skeleton dressed in a long cloak plays a bagpipe while a jester dances to it.",
    before [;
      Push:
        move entrance to location;
        move self to Limbo;
		    scope_modified = true;
        "You push the signet stone and it vanishes into the wall. An entrance to a secret passage opens.";
    ]
  has static;

Object -> -> entrance "entrance",
  with name 'entrance' 'secret' 'passage',
    article "an",
    initial "An entrance to a secret passage is to the east.",
    description "You stare at the entrance and think of something that Publius Vergilius Maro once said: ~The gates of hell are open night and day. Smooth the descent, and easy is the way. But to return, and view the cheerful skies, in this the task and mighty labor lies.~",
    before [;
        Enter:
          <<Go FAKE_E_OBJ>>;
    ],
  has static;

Object -> silverplates "silver plates",
  with name 'silver' 'plates' 'set' 'of' 'opening',
    initial "There is an opening in the wall. Inside lies a set of silver plates that have probably been hidden for centuries.",
    description "The plates are square, about 4 inches in diameter, and they appear to have something drawn or engraved on them. You should take them in case you want to examine them more closely.",
    before [;
      Take:
        move silverplates to Limbo;
        move cuthbert to player;
        move kessog to player;
        move adomnan to player;
        move nathalan to player;
		    update_moved = true;
		    scope_modified = true;
        "You take the plates with you.";
    ]
  has pluralname;

Object -> eerievoice "eerie voice",
  with name 'eerie' 'voice',
    article "an",
    initial "An eerie voice echoes through the garden. It seems to be calling you.",
    description "How can you describe something you can't even see? You can't even tell where the voice is coming from.",
    before [;
      Talk, Listen:
        if (PROGRESS_LEVEL == 10) {
          PROGRESS_LEVEL = 11;
          move Cora to Limbo;
          move sleepingCora to Solar;
          move witchbook to Solar;
		      scope_modified = true;
          IncrementScore();
        }
        "Voice: Thomas, meet me in the forgotten village in the forest.";
      Take:
        "You can't be serious.";
      Examine:
        rfalse;
      default:
        "It is impossible to have such an interaction with something that hasn't manifested.";
    ],
  has animate static;

! NPCs without starting location 
Object Cora "Cora",
  with name 'Cora',
    initial "Cora has snuggled up in bed and is reading a book.",
    description "Your wife Cora is a strikingly beautiful woman with fiery red hair and emerald green eyes. You love her lilting Scottish accent, and her words are always filled with warmth.",
    life [;
      ThrowAt:
        "Cora: Don't you dare!";
    ],
    before [;
        Take, Touch, Push, Pull, Turn:
          "This is not that kind of game.";
        Kiss:
          "You kiss Cora. ~What was that for?~ She looks a little surprised, but definitely charmed.";
        Attack, Burn:
          "What's wrong with you? Just no.";
    ],
    talk_start 0,
  has proper animate female transparent;

Object -> witchbook "witch hunt chronology", ! when the game starts, Cora is reading this book
  with name 'witch' 'hunt' 'chronology' 'book',
    initial "The book Cora had been reading is now on the bedside table.",
    description "Chronology of the Great Scottish Witch Hunts from 1590 to 1662. The book looks quite old and very likely belongs to the manor's library. A rather sombre read.",
    before [;
      Take:
        if (self in Cora) "Cora is currently reading it, you don't want to take it away from her.";;
      Push, Pull:
        if (self in Cora) "Not as long as Cora has it.";
      Search, Open:
        if (self in Cora) "Maybe when Cora is finished with it. Witch hunts are not high on your list of favourite reads anyway.";
        if (self.player_progress == false) {
          IncrementScore();
          PROGRESS_LEVEL = 12;
          self.player_progress = true;
          move eerievoice to Limbo;
		      scope_modified = true;
          "Oh, interesting. Lord Bain, the man with the scar, was responsible for wiping out an entire settlement near Blackwood Manor during the great Scottish Witch Hunt of 1597. According to the records, the inhabitants of this settlement were said to have made a pact with the devil himself. On a map you can even see that the settlement was in the nearby forest. You should now be able to find it if there is anything left of it.";
        }
        "You look for more clues in the book but find no new insights.";
    ],
    player_progress false,
  has scored;

Object sleepingCora "Cora",
  with name 'Cora',
    initial "Cora lies in bed and sleeps. The last few days have been very exhausting.",
    description "Your wife Cora is a strikingly beautiful woman with fiery red hair and emerald green eyes. You could watch her sleep for hours. Oh, how you love her.",
    before [;
        Examine:
          rfalse;
        default:
          "You should let her sleep."; 
    ],
  has proper animate female;

Object Ysabella "Ysabella",
  with name 'ghost' 'Ysabella' 'woman' 'presence' 'ethereal' 'medieval' 'wisps' 'mist' 'translucent' 'specter',
    initial "You encounter the ethereal presence of a medieval ghost woman, standing admidst the graves.",
    description "She is a specter shrouded in a tattered, flowing gown that appears to be made of wisps of mist, billowing around her in an otherworldly fashion. Her form is translucent, revealing glimpses of pale skin.",
    before [;
      Talk:
        if (Ritual_started == true) "Ysabella: Not now, we need to focus on the ritual!";
        if (FlagIsSet(F_Y_TALKED_FIRE, F_Y_TALKED_LIVING, F_Y_TALKED_DEAD) && FlagIsSet(F_Y_TALKED_BLOOD)) {
          if (TheBoy in Dungeon) "Ysabella: I feel that my boy is still trapped in the manor's dungeon. Please free him before we can proceed to the ritual. Look for something that prevents him from leaving.";
          print "She looks at you expectantly.^Ysabella: Are you ready for the ritual?^";
          if (YesOrNo()) {
            if (~~ candles in player && frankincense in player && loafbread in player && bloodlinen in player) "^Ysabella's appraising eyes measure you.^Ysabella: You say you are ready for the ritual but I see you're not. Come back when you carry everything I asked for.";
            if (~~ lighter in player) "Ysabella: I see you carry everything I asked for but you miss something to light the candles. Come back when you have it.";
            Ritual_started = true;
            move gravestone to OverGrownCemetery;
			      scope_modified = true;
            "^Ysabella: Come here and put the candles on the gravestone.^Ysabella floats to a gravestone that has no name on it.";
          }
          "^Ysabella: It's no problem you're not ready yet. Remember what is needed for the ritual: fire, something for the living and the dead and innocent blood.";
        }
    ],
    talk_start 0,
  has proper animate female;

Object -> gravestone "gravestone",
  with parse_name [w n a len;
          n = wn;
          w = NextWord();
            if (w == 'gravestone') {
              a = WordAddress(n);
              len = WordLength(n);
              if (len == 10 && a->9 == 'e')
              return 1;
            }
            if (w == 'grave') return 1;
            if (w == 'stone') return 1;
    ],
    describe [;
      print "^A gravestone looms ominously. A foreboding presence in the pale light";
      if (parent(candles) == gravestone) {
        print ". Some ";
        if (candles.player_progress == true) print "burning ";
        print "candles are placed on it";
      }
      ".";
    ],
    before [;
      Receive:
        if (~~ noun == candles) "You shouldn't put random stuff on the gravestone. You already know what needs to be placed on it.";
    ],
    description "Its surface is etched with sinister markings that evoke a sense of unease. The absence of a name adds to the unsettling aura.",
  has static supporter;

Object -> -> bowl "bowl",
  with name 'bowl' 'blood',
    description "A pretty ordinary bowl which contains lots of blood. A worrying amount of blood.",
    before [;
      Fill:
        if (~~ second == leadcoffin) rfalse;
        <<Insert self leadcoffin>>;
      Smell:
        "The ferrous smell of the blood has a disturbing effect on you.";
    ],
  has scored;

Object -> -> leadcoffin "lead coffin",
  with name 'lead' 'coffin' 'metal',
    description "You stare at a medieval lead coffin, weathered by time. The air around it feels even colder, as if an unearthly chill is emanating from the body inside. You notice that the engravings on its sides are not just ornate patterns and symbols, but rather grotesque and macabre scenes.",
    before [;
      Smell:
        "Oh god.";
      Close:
        "That's probably what you should do but on the other hand you're too curious now to see where this is going.";
      Receive:
        if (~~ noun == loafbread or bloodlinen or frankincense or leatherbag or salt or bowl ) "You shouldn't put things in the coffin that are not meant to be there. Ysabella told you what is needed.";
        switch (ObjectCapacity(self)-children(self)) {
          4:
            if (~~ noun == loafbread) "This object is not what Ysabella considered ~something for the living~. Please choose a different object.";
            move loafbread to leadcoffin;
			      scope_modified = true;
            IncrementScore();
            "You put the bread in the lead coffin.^Ysabella: Auferte panem a discipulis Nazareni! Transibunt plagae terram suam!^^She raises her hands in an almost threatening gesture. Then she smiles at you. A smile that somehow seems false.^Ysabella: Now something for the dead, Thomas.";
          3:
            if (~~ noun == frankincense) "This object is not what Ysabella considered ~something for the dead~. Please choose a different object.";
            IncrementScore();
            move frankincense to leadcoffin;
			      scope_modified = true;
            print "You use the lighter to make the frankincense smoulder, then you place it in the coffin.^Ysabella: Aperi portas inferi mortuorum. Ultimus Saturnus hac nocte fulgebit in firmamento, clarior sidus in odio Bethleem. Adventum tenebrarum incarnati praedicabit!^^Ysabella: And now the blood of an innocent";
            ! if (Choice_Ending_Bad == true) {
            !   print ". Don't use the blood you brought with you. We have something better. Look behind you.^^You turn around and notice a bowl, obviously containing blood.^Ysabella: Pour it into the coffin";
            !   move bowl to OverGrownCemetery;
			      !   scope_modified = true;
            ! }
            ".";
          2:
            ! if (Choice_Ending_Bad == true) {
            !   if (~~ noun == bowl) "This is not the right object. Ysabella wants the blood from the bowl to be poured into the coffin.";
            !   print "You pour the blood into the coffin and the boy inside starts to breathe.";
            ! }
            ! else {
              if (~~ noun == bloodlinen) "This is not the right object for the ~blood of an innocent~ part of ritual.";
              move bloodlinen to leadcoffin;
			        scope_modified = true;
              print "You put the bloody linen fabric into the coffin and the boy inside starts to breathe. Mew life seems to flow into the dead body. How is that possible?";
              Choice_Ending_Neutral = true;
              StartDaemon(self);
            ! }
            IncrementScore();
            new_line;
            !if (Choice_Ending_Bad == true) EndingSequence();
            rtrue;
        }
    ],
    capacity 5,
    countdown 3,
    daemon [;
        self.countdown--;
        switch (self.countdown) {
          2: 
            print "^Ysabella mumbles something. She still seems to be busy with the ritual.";
            !ChangeFgColour(clr_emphasis_fg);
            print "^^[You can now WAIT for Ysabella to finish the ritual. Alternatively, you can try to intervene, if that is what you want. What will you do?]";
            !ChangeFgColour(clr_default_fg);
            new_line; rtrue;
          1:
            if (~~ salt in leadcoffin) {
              if (lighter has on) print "Ysabella, standing behind you, takes the ligher away from you. It would have been better to distract her with something before you try to disturb the ritual.^";
              EndingSequence(); rtrue;
            }
            !ChangeFgColour(clr_emphasis_fg);
            print "^[You feel that something is about to happen and your next move has to be a good one.]^";
            !ChangeFgColour(clr_default_fg);
          0:
            print "^A strong wind comes up and blows the salt away.^";
            EndingSequence(); rtrue;
        }
    ],
  has static container open;

Object -> -> -> "body",
  with name 'inside' 'body' 'corpse' 'boy',
    description "From your point of view, it is impossible that the body of this boy can be so well preserved after hundreds of years. It's either a miracle or a bad omen.",
    before [;
        Touch, Push, Pull, Smell, Take, Kiss, Remove: 
          "Um... no. Absolutely not. What's wrong with you?";
    ];

Object LordBain "Lord Bain"
  with name 'ghost' 'lord' 'bain' 'spectral' 'figure',
    initial "In the center of the dovecote, you behold a spectral figure. It is Lord Bain.",
    description "Dressed in regal attire befitting his noble station, the spirit stands tall and proud. His tattered yet ornate tartan kilt drapes around his legs and a weathered sporran hangs from his waist.",
    life [;
        Show, Give:
          if (noun ~= witchbook) "Lord Bain looks at the object with little interest. It does not seem to jog his memory.";
          if (LordBain.player_progress == false) {
            LordBain.player_progress = true;
            IncrementScore();
          }
          "You show Lord Bain the chronology.^Lord Bain: I remember. I have seen true evil.^You: The boy?^^Lord Bain: SHe is Lucifer's offspring. We were able to separate his body from his evil mind and trap him inside the dungeon. The villagers used a forbidden book to conceive the child with Lucifer. Search for this book and try to destroy it at the right time. Remember what your eyes can't see, your ears might hear.^You nod and then turn away from him.";
    ],
    before [;
      Touch, Attack, Kiss, Pull, Push, Turn, Burn: 
        "There would be no point in doing that to someone that has no physical form.";
      Smell:
        "Why would you want to sniff a ghost? That's really disturbing.";
      Talk:
        if (self.player_progress == false) {
          "You: Lord Bain?^Lord Bain: I think that was my name once upon a time. Whatever you want from me, I probably won't be able to help you. I have to admit that my memory has faded. I wish I could remember why I'm still here.";
          }
          "Lord Bain: You know what to do. Find the source of evil and destroy it when the time is right. What the eyes can't see, your ears might hear.";
    ],
    talk_start 0,
    player_progress false,
  has proper animate;

Room GreatHall "Great Hall" ! -- locations start here
  with name 'great' 'hall' 'manor' 'room' 'exit' 'white' 'layer',
      description [;
        print "This part of the manor dates back to the 8th century. Blackwood was built on the remains of an ancient monastery. Today,it is a sitting room. Flames dance in the fireplace. Below the gallery of ancestors stands a Christmas tree. Through the windows you can see ";
        if (PROGRESS_LEVEL == 0) print "a snowstorm raging, the wind is howling as waves of snowflakes streak by.";
        else print "the manor's feral garden and the surrounding forest blanketed in a white layer.";
        print " The only exit is to the north, leading to the vestibule.";
        if (self hasnt visited) {
          !ChangeFgColour(clr_emphasis_fg); 
          print "^^[Welcome to Ghosts of Blackwood Manor. To learn more about how to play the game, type HELP. First time players should also type ABOUT.]";
          !ChangeFgColour(clr_default_fg);
        }
        new_line; ],
      n_to Vestibule,
      cheap_scenery
          3 'fireplace' 'fire' 'flames' "For a second you lose yourself in the flames. How many generations must have warmed themselves here over the centuries? What were their worries, their hardships, as they lingered here?"
          32 'christmas' 'xmas' 'scottish' 'fir' 'tree' "The tree, a Scottish fir, looks wonderful. Cora did an amazing job."
          4 'windows' 'window' 'pane' 'panes' 
          [;
            Examine:
              if (PROGRESS_LEVEL == 0) print_ret (string)MESS_STORM;
              "You stand at the window, peering out into the winter landscape beyond. The world outside is covered in a thick blanket of snow.";
          ]
          11 'feral' 'garden' "You definitely have to take care of the garden next spring."
          11 'surrounding' 'forest' "The forest surely holds many secrets, some of which may one day be rediscovered, others will remain hidden forever, buried and forgotten witnesses to a dark past.",
  has light;

Constant MESS_STORM "The window panes rattle from the force of the storm. Heavy snowflakes swirl in the air and blanket the manor's feral garden and the surrounding forest in a white layer.";

Object -> gallery "gallery of ancestors",
  with name 'gallery' 'of' 'ancestors' 'portraits' 'lords' 'ladies' 'pictures' 'paintings',
    description [;
      print "Fascinated, you look at the many portraits of the former Lords and Ladies of Blackwood. They are all long gone. The way of all flesh.";
      if (PROGRESS_LEVEL == 3) {
        new_line; new_line;
        if (bainportrait in self) <<Search self>>;
      }
      new_line; rtrue;
    ],
    before [;
      Search:
        if (bainportrait in self && PROGRESS_LEVEL == 3) {
          move bainportrait to location;
		      scope_modified = true;
          PROGRESS_LEVEL = 4;
          IncrementScore();
          "On closer inspection, you can see the portrait Rosie was talking about. Lord Bain's cold gaze is hard to resist. Bain is holding a hand to his chest, and on the back of his hand you recognise the same distinctive scar you saw in your dream. How can this be?";
        }
        else rfalse;
    ],
  has scenery;

Object -> -> bainportrait "portrait of Lord Bain",
  with name 'portrait' 'of' 'lord' 'bain' 'archibald' 'picture' 'scar' 'painting',
    initial "One of the paintings in the gallery stands out. The portrait of the man with the scar on his hand, Lord Archibald Bain.",
    description "You wonder if what they say about him is true. Did he really wipe out an entire village because its inhabitants had allegedly made a pact with the devil? So much bloodshed, all in the name of God?",
    before [;
        Pull, Turn, Open, Search: 
          if (PROGRESS_LEVEL == 4 or 5) {
            give self transparent;
			      scope_modified = true;
            if (PROGRESS_LEVEL == 4) IncrementScore();
            PROGRESS_LEVEL = 5;
            "You turn the portrait and on the back you notice a small elevation under the canvas as if there is something hidden underneath. You cannot open the canvas with your bare hands though. You need something like a small and very sharp knife or a cutter to not destroy the picture.";
          }
          else "No need to bother with it again.";
        Cut, Attack:
          if (self has transparent) <<Cut canvas>>;
    ],
  has static;

Object -> -> -> canvas "canvas",
  with name 'canvas' 'elevation',
    description "You wonder what is hidden underneath.",
    before [;
      Pull, Turn, Open, Search:
        <<Open bainportrait>>;
      Cut, Attack:
        if (~~ boxcutter in player or GreatHall) "You don't carry a suitable object.";
        if (~~ parchment in self) "No need to cut it again.";
        IncrementScore();
        PROGRESS_LEVEL = 6;
        move parchment to GreatHall;
	    	scope_modified = true;
        "You carefully cut the canvas. A parchment falls from the portrait.";
    ]
  has scenery;

Object -> -> -> -> parchment "parchment",
  with name 'parchment' 'writing',
    initial "A parchment has fallen from the portrait and is now lying on the floor.",
    description "The writing is ornate and looks old-fashioned: Behind the endless knot I was laid to rest. Four, four, two, three, find me and you will see.",
  has scored;

Object -> snowstorm,
  with name 'snowstorm' 'wind' 'snowflakes' 'streaks' 'storm',
        description [;
          print_ret (string)MESS_STORM;
        ],
    before [;
      Examine:
        rfalse;
      Smell:
        "You can't actually smell the snowstorm from here.";
      default:
        print_ret (string)MESS_SCENERY; 
    ],
  has scenery;

Room TheChapel "The Chapel"
  with name 'chapel' 'sacred' 'place' 'floor',
        description "Rows of pews stretch out before the ancient altar. Behind the altar are niches with stained glass windows. Doors to the south and southwest lead to the Oratory and Sacristy, while the exit to the north leads back to the vestibule.",
        n_to Vestibule,
        s_to Sacristy,
        sw_to Oratory,
        d_to [;
          if (opening in self) {
            print "Slowly you descend until you are completely engulfed in darkness.^";
            return Undercroft;
          }
          "You cannot go that way.";
        ],
        cheap_scenery
            12 'stained' 'glass' 'windows' "The colours of the glass are muted and subdued, casting a soft, warm light on the chapel floor."
            21 'soft' 'warm' 'light' "You could watch this for hours."
            2 'door' 'doors'
            [;
              Examine:
                "Man, they knew how to build doors back in the day.";
              Enter, Open, Go:
                "Just go into the desired direction to reach one of the locations behind the doors.";
            ]
            3 'rows' 'of' 'pews' "You wonder what it was like when Blackwood Manor was still a monastery. The pews full of clergymen, devoutly absorbed in prayer, their whispers echoing through the sacred halls, the chapel bathed in candlelight while rain is pouring down outside.",
   has light scored;

Object -> altar "altar",
  with name 'large' 'ancient' 'altar',
    description "The altar is weathered and worn, its edges smoothed by countless hands over the centuries. On the back of the altar you notice a bronze panel.",
    before [;
        Receive:
          if (receive_action == ##PutOn) "There's no need to put something on the altar at the moment.";
          if (receive_action ~= ##Insert) rfalse;
          <<Insert noun panel>>;
        Climb, Enter:
          "Hell no, you're too old for that.";
    ],
  has scenery supporter transparent;

Object -> -> panel "bronze panel",
  with name 'bronze' 'panel' 'spot' 'indention' 'silhouette' 'humanoid',
    description [;
        print "The panel depicts the chaotic events of the end of the world. The Four Horsemen ride through the sky, their steeds galloping at breakneck speed. ";
        if (statuette in self) "The panel is now complete. The bronze statuette of the Archangel Gabriel blowing his trumpet to announce the Day of Judgement fitted perfectly into the indentation.";
        "There is a spot with an indentation in the panel where something seems to be missing. The indentation has the shape of a humanoid silhouette.";
    ],
    before [;
        Receive:
          if (~~ receive_action == ##Insert) rfalse;
          if (~~ noun == statuette) "That's definitely the wrong object. You are looking for something that fits the spot in the bronze panel.";
          move statuette to self;
          move opening to TheChapel;
          give self transparent;
		      scope_modified = true;
          IncrementScore();
          "You put the statuette in the bronze panel and it fits perfectly. The next moment, a hidden mechanism activates and the altar slides backwards, revealing an opening below that leads into the blackest darkness.";
        Push, Pull:
          <<Push altar>>;
    ],
  has scenery;

Object -> -> -> opening "opening",
  with name 'opening',
    article "an",
    initial "An opening that was hidden below the altar leads down to a very dark place.",
    description "Fascinated you stare at the opening.",
    before [;
        Enter:
          <<Go FAKE_D_OBJ>>;
    ],
  has static;

Room Oratory "Oratory"
  with name 'oratory' 'room' 'exit',
        description "The oratory was meant for the Lord's private prayer. You notice a single pew. A stained glass window depicts a scene of a noble woman washing the feet of the poor. An exit to the north leads back to the chapel.",
        n_to TheChapel,
        cheap_scenery
             2 'pew' 'carvings' "The pew is decorated with wonderful carvings."
             26 'noble' 'stained' 'glass' 'window' 'scene' 'woman' 'feet' 'poor' "The symbolism behind this is very strong."
             ,
  has light scored;

Object -> "statue of a saint",
  with name 'alcove' 'statue' 'saint' 'St' 'St.' 'Margaret' 'plaque',
    describe [;
      print "^An alcove to the left catches the eye with a beautifully carved statue of a saint, depicted with a serene expression, her hands directed hopefully towards the heavens";
      if (parent(goldenkey) == hands) print ", holding a golden key";
      print ".";
      new_line; rtrue;
    ],
    description "A small plaque beside the statue reads ~Saint Margaret of Scotland.~",
  has static transparent;

Object -> hands "hands of Saint Margaret",
  with name 'hands' 'hand' 'palm' 'palms',
    description "The way the hands are held suggests that you can place something into them.",
    before [;
        Receive:
          if (~~ noun == goldenkey) "This object doesn't fit into the hands.";
    ],
    after [;
        Receive:
          if (~~ self.player_progress == true) {
            self.player_progress = true;
            IncrementScore();
            move silverplates to Oratory;
			      scope_modified = true;
            "The key fits. A stone in the wall slides aside and reveals a set of silver plates.";
          }
          else rfalse;
    ],
    player_progress false,
  has scenery pluralname open container;

Room Vestibule "Vestibule"
  with name 'vestibule' 'room',
        description "The vestibule is dimly lit by bulbs in sconces. To the west, you can see a wooden door leading to the outside, its surface polished by centuries of use. The libary lies to the north, the great hall to the south and the kitchen to the east. To the southwest, an olden hinged door leads to the chapel. A staircase spirals upwards.",
        n_to Library,
        s_to GreatHall,
        e_to Kitchen,
        w_to [;
          if (PROGRESS_LEVEL == 0) "A snowstorm is raging outside. It's not a good time to leave the manor now.";
          return Forecourt;
        ],
        sw_to TheChapel,
        u_to Corridor,
        cheap_scenery
            4 'light' 'bulbs' 'sconces' 'spots' "It looks like spots in the walls were once meant for torches. That was a long time ago. Even the light bulbs seem antiquated by now."
            11 'wooden' 'door'
            [;
              Examine:  
                "The door looms before you, exuding an aura of age and history."; 
              Open, Enter:
                <<Go FAKE_W_OBJ>>;
            ]
            1 'staircase'
            [;
              Examine:
                "It leads to the upper part of the manor, which was built much later, on the ruins of the monastery.";
              Enter, Go:
                <<Go FAKE_U_OBJ>>;
            ]
            21 'olden' 'hinged' 'door' 
            [;
              Examine:
                "The manor's chapel lies behind this door.";
              Open, Enter:
                <<Go FAKE_SW_OBJ>>;
            ]
            ,
  has light scored;

Object -> Rosie "Rosie",
  with name 'Rosie' 'housekeeper',
    initial "Rosie is here. It seems as if she has been waiting for you.",
    description "Rosie has been Blackwood's housekeeper for many years. Despite her age, her eyes are sharp and she seems to notice every detail.",
    before [;
      Touch, Attack, Burn: 
        "That wouldn't be very gentlemanly.";
    ],
    talk_start 0,
  has proper animate female;

Room Sacristy "Sacristy"
  with name 'sacristy' 'room' 'exit',
        description "In the centre of the sacristy is a large wooden table with a chalice and a paten, which appear to be made of gold. You notice a small cabinet. A single exit to the north leads back to the chapel.",
        n_to TheChapel,
        cheap_scenery
          3 'chalice' 'paten' 'gold' "This golden decadence has nothing in common with the humble life of Jesus Christ. Ironic.",
  has light scored;

Object -> cabinet "small cabinet",
  with name 'small' 'cabinet',
    description "It's quite an old cabinet.",
    before [;
        Open, Search:
          if (PROGRESS_LEVEL == 13 or 14 && frankincense in self) {
            IncrementScore();
            move frankincense to player;
			      update_moved = true;
			      scope_modified = true;
            "You find frankincense which you take with you. The resin seems to be quite old, but will do its job."; 
          }
          "At the moment there is nothing in it useful to you.";
    ],
  has scenery;

Object -> -> frankincense "frankincense",
  with name 'frankincense' 'aromatic' 'resin' 'incense',
    description "Frankenincense. For thousands of years, the aromatic resin is harvested from Boswellia trees in the Middle East.",
    before [;
        Smell:
          "It has a characteristic smell which you really like.";
        Take:
          if (self in leadcoffin) "The frankincense is part of the ritual. It needs to stay in the lead coffin.";
    ],
  has proper scored;

Room Kitchen "Kitchen"
  with name 'kitchen' 'vast' 'space',
        description "The kitchen retains its old world charm, reflecting its storied past. The fireplace has been replaced by a modern cooker, but the original mantelpiece and hearth remain. Rough-hewn stone worktops add to the kitchen's character, while the electric lights brighten the room. There are exits to the north, south, east and west.",
        n_to Storeroom,
        s_to Buttery,
        e_to Pantry,
        w_to Vestibule,
        cheap_scenery
            3 'fireplace' 'mantelpiece' 'hearth' "You love the fact that the fireplace and hearth have been preserved."
            11 'modern' 'cooker' "For the people of the medieval times, a cooker like this one would probably have been witchcraft."
            32 'electric' 'warm' 'white' 'lights' 'light' "The warm white light bathes the kitchen.",
  has light scored;

Object -> worktops "worktops",
  with name 'rough' 'rough-hewn' 'stone' 'worktops' 'worktop',
    description [;
      print "One can only admire this craftsmanship. You wonder how old these worktops are, probably many centuries.";
      if (note in self) {
        new_line;
        <<Search self>>;
      }
      new_line; rtrue;
    ],
    before [;
      Touch:
        "You feel cold stone.";
      Push, Pull, Turn:
        "No chance to move them, which is no surprise. They must weigh a ton.";
      Search:
        if (note in self) {
          move note to location;
		      scope_modified = true;
          "^Rosie seems to have left a note for you on one of the worktops.";
        }  
        else rfalse;
    ],
  has pluralname scenery;

Object -> -> note "note",
  with name 'note',
    initial "A note is lying on one of the worktops.",
    description "It has Rosie's phone number on it.",
  has scored;

Room Pantry "Pantry"
  with name 'pantry' 'ceiling' 'room',
        description "Tall shelves are lined with jars and baskets filled with food. A commercial fridge stands tall and a long wooden table covered with an array of breads. The door to the west leads back to the kitchen.",
     cheap_scenery
            13 'tall' 'shelves' 'jars' 'baskets' "The shelves, made of metal, don't seem to be very old. And what you see here in these jars and baskets, according to Rosie, is not only edible, but a culinary journey through the specialities of Scotland."
            23 'stainless' 'commercial' 'fridge' 'steel' 'finish' [;
              Examine:
                "Its stainless steel finish is smooth and its humming is unobtrusive.";
              Open:
                "You open the fridge and notice it is stocked with a variety of fresh vegetables, meat and dairy products. Nothing you would need now so you close the fridge again.";
            ]
            1 'door' [;
              Examine: 
                "One of the many old, wooden doors that can be found here around Blackwood Manor.";
              Open, Enter:
                <<Go FAKE_W_OBJ>>;
            ]
            21 'long' 'wooden' 'table' "A nice table, nothing special."
            ,
        w_to Kitchen,
  has light scored;

Object -> breads "array of breads",
  with name 'array' 'breads' 'bread' 'loaf' 'of',
    description "An assortment of fresh breads, the epitome of culinary craftsmanship.",
    before [;
      Take:
        if (PROGRESS_LEVEL < 13) <<Eat self>>;
        if (loafbread in self) {
          move loafbread to player;
		      update_moved = true;
		      scope_modified = true;
          "You take a fresh loaf of bread with you.";
        }
        else "You already took a loaf, that's enough.";
      Eat:
        "You are not hungry at the moment.";
      Smell:
        "The breads smell fantastic.";
    ],
  has pluralname scenery;

Object -> -> loafbread "loaf of bread",
  with name 'loaf' 'of' 'bread',
    description "A fresh and delicious loaf of bread taken from the manor's pantry.",
    before [;
      Eat:
        "You really don't want to eat right now.";
      Take:
        if (self in leadcoffin) "The bread loaf is part of the ritual and needs to remain in the lead coffin.";
    ]
  has scored;

Room Buttery "Buttery"
  with name 'buttery',
        description "Wooden shelves are lined with bottles, each containing a different type of Scotch whisky, dimly lit by candlelight, casting a warm glow over the amber liquid inside. A small wooden table is littered with empty glasses. Go north to get back to the kitchen.",
        n_to Kitchen,
        before [;
          Smell:
            "One deep breath is enough to get drunk.";
        ],
        cheap_scenery
          11 'wooden' 'shelves' "These shelves are solid but basic. The magic of the buttery is not coming from its interior, it's all about the booze."
          14 'amber' 'bottles' 'Scotch' 'whisky' 'liquid' 
          [;
            Examine:
              "Some sealed bottles here are almost 100 years old. What a collection.";
            Open, Eat, Drink, Take:
              "Let's not add alcohol abuse to the list of themes the game exploits and leave the booze here.";
          ]
          13 'warm' 'glow' 'candlelight' 'candles'
          [;
            Examine: 
              "Candlelight so much warmer than any light bulb ever could be.";
            Take:
              "You should leave these candles. It's the only source of light here.";
          ]
          32 'small' 'empty' 'wooden' 'table' 'glasses' "There is already so much dust in some of the glasses on the table that you could only fill them halfway.",
  has light scored;

Object -> lighter "lighter",
  with name 'lighter',
    short_name "lighter",
    describe [;
      if (self hasnt moved) {
        print "^You see a lighter ";
        if (self has on) print "(lit) ";
        "next to one of the candles.";
      }
    ],
    description [;
        print "In contrast to everything you've seen so far in Blackwood, this is a fairly modern and generic lighter, currently ";
        if (self hasnt on) print "not ";
        "lit.";
    ],
    before [;
        Burn, SwitchOn:
          if (~~ self in player) {
            print "(taking the lighter first)^";
            move lighter to player;
			      update_moved = true;
			      scope_modified = true;
          }
          if (self has on) "The lighter is already lit.";
          if (self.time_left ~= 0) "The lighter is too hot, you need to wait a bit.";
          give self on;
          self.short_name = "lighter (lit)";
          StopTimer(self);
          StartTimer(self, 2);
          print "You light up the lighter.^";
          if (location == thedark) <<Look>>;
          else rtrue;
        SwitchOff:
          if (self hasnt on) "The lighter is already unlit.";
          give self ~on;
          self.short_name = "lighter";
          StopTimer(self);
          "The lighter is now unlit again.";
    ],
    time_out [;
      if (self has on) {
        self.short_name = "lighter";
        StartTimer(self, 3);
        give self ~on;
        "^Ouch, the lighter became too hot in your hands and is now unlit.";
      }
    ],
    time_left,
    player_progress false,
has switchable scored;

Room Storeroom "Storeroom"
  with name 'storeroom' 'various' 'items',
        description "The storeroom is cluttered with decades old items. In front of you is a large chest of drawers with a mirror on top. The drawers are labelled with various items such as ~linen~, ~candles~ and ~cutlery~. A row of coat hangers with some old jackets catches your eye. You can leave by heading south.",
        s_to Kitchen,
        before [;
          Smell:
            if (noun ~= oldjackets) "Oh my god, it smells bloody awful.";
        ],
        cheap_scenery
          1 'mirror' "The mirror has become dull through the many years down here. Your reflection is a blurred shadow."
          11 'blurred' 'shadow' "Is that really you? Spooky!",
  has light scored;

Object -> oldjackets "old jackets",
  with name 'old' 'jackets' 'jacket' 'coat' 'hangers' 'row' 'of',
    description "The old jackets are quite moth-eaten and definitely no longer suitable for wearing.",
    before [;
        Take, Wear:
          "Um, no. What's wrong with you?";
        Smell:
          "Wow, they smell pretty musty and there is also a different scent coming from it. Disgusting. Did a rat die in it?";
        Touch:
          "Wonderful. Apparently they are also sticky.";
        Search:
          if (~~ electrictorch in self) "No need to search it again. It stinks.";
          move electrictorch to Storeroom;
		      scope_modified = true;
          "Oh, there's an electric torch in one of the pockets that might come in handy.";
    ],
  has pluralname scenery;

Object -> -> electrictorch "electric torch",
  with name 'electric' 'torch' 'flashlight' 'Ever Ready',
    describe [;
      if (self hasnt moved) {
        print "^You notice an electric torch ";
        if (self has on) print "(providing light) ";
        "sticking out of the pocket of an old jacket.";
      }
    ],
    article "an",
    description [;
        print "It's an Ever Ready electric torch, probably from the 1970s, ";
        if (self hasnt on) "currently off.";
        "shining brighter than the Morning Star.";
    ],
    before [;
        Drop:
          if (location == thedark) "It's not a good idea to drop your only source of light here.";
        SwitchOff:
          if (self in Cellar or Undercroft or Crypt or Dungeon or SecretPassage) "It won't be a good idea to switch off your only source of light here while not carrying it. You won't be able to find it again.";
    ],
    after [;
        SwitchOn: 
          give self light;
          scope_modified = true;
        SwitchOff: 
          give self ~light;
          scope_modified = true;
    ],
has switchable scored;

Object -> drawers "drawers",
  with name 'linen' 'candles' 'cutlery' 'drawers' 'drawer' 'chest',
    description "Properly labelled. Commendable.",
    before [;
        Open, Search:
          if (PROGRESS_LEVEL == 5 && boxcutter in self) {
            move boxcutter to location;
			      scope_modified = true;
            IncrementScore();
            "Your search reveals a box cutter, which you place on the ground.";
          }
          if (PROGRESS_LEVEL == 13 or 14) {
            if (linen in self) {
              move linen to player;
              move candles to player;
			        update_moved = true;
			        scope_modified = true;
              IncrementScore();
              "You find a piece of linen fabric and a few candles, which you take with you.";
            }
          }
          "You look inside and find nothing of use.";
    ],
  has pluralname scenery;

Object -> -> linen "piece of linen fabric",
  with name 'linen' 'fabric' 'piece' 'of',
    description "A piece of linen which doesn't seem to be machine made, probably an older fabric.",
    before [;
        PutOn:
          if (second == blood) {
            if (linen in location) print "(taking the linen fabric first)^";
            <<Rub blood linen>>;
          }
    ],
  has scored;

Object -> -> candles "set of candles",
  with name 'candle' 'candles' 'set' 'of',
    description [;
        print "A set of pretty ordinary candles";
        if (self.player_progress == true) print ", currently lit";
        ".";
    ],
    before [;
      Take:
        if (parent(self) == gravestone) "The candles are where they are supposed to be.";
      Burn:
        if (second == nothing) "Not the worst idea you've had. But how are you going to light the candles?";
        if (~~ second == lighter) "You can't light the candles with this.";
        if (~~ parent(self) == gravestone) "The candles must be securely placed before being lit.";
        if (lighter hasnt on) {
          print "You should first ";
          !ChangeFgColour(clr_emphasis_fg);
          print "[ignite the lighter]";
          !ChangeFgColour(clr_default_fg);
          ".";
        }
        self.player_progress = true;
        move leadcoffin to OverGrownCemetery;
		    scope_modified = true;
        IncrementScore();
        "You light the candles on the gravestone and the earth shifts to uncover a medieval lead coffin. As if by magic, the lid opens to reveal the dead body of the boy whose ghost you saw in the dungeon. He looks as if he is peacefully asleep. ^^Ysabella: now place something for the living in the coffin.";
      SwitchOn:
        print "You cannot turn on the candles but you probably want to ";
        !ChangeFgColour(clr_emphasis_fg);
        print "[light candles with something]";
        !ChangeFgColour(clr_default_fg);
        "?";
    ],
    player_progress false,
  has pluralname scored;

Object -> -> boxcutter "box cutter",
  with name 'box' 'cutter' 'Stanley',
    initial "A box cutter lies on the ground.",
    description "That's an old Stanley box cutter. Built to last forever unlike the cheap plastic these days.",
  has scored;

Object -> -> -> blood "few drops of blood",
  with name 'few' 'drops' 'of' 'blood',
    initial "You notice a few drops of your blood on the ground.",
    description "You wouldn't call yourself a noble soul but your blood should be sufficient for the ritual.",
    before [;
      Take:
        print "You can't just pick up the blood with your hands. Maybe you can ";
        !ChangeFgColour(clr_emphasis_fg);
        print "[wipe blood with something]";
        !ChangeFgColour(clr_default_fg);
        "?";
      Rub:
        if (second == nothing) "Which object do you want to use to wipe up the blood? Try being more specific.";
        if (~~ second == linen) "This is not a good object to wipe the blood with. You can surely find something better.";
        IncrementScore();
        move bloodlinen to player;
        move blood to Limbo;
        move linen to Limbo;
		    update_moved = true;
		    scope_modified = true;
        "You wipe the blood with the linen fabric.";
    ],
  has static pluralname;

Object -> -> -> -> bloodlinen "bloody linen fabric"
  with name 'bloody' 'linen' 'fabric',
    description "A piece of linen fabric with your blood on it. The day is getting better and better.",
    before [;
        Take:
          if (self in leadcoffin) "The bloody linen fabric is part of the ritual and needs to remain in the lead coffin.";
    ],
  has scored;

Room Library "Library"
  with name 'library' 'ceiling' 'shapes' 'sizes',
        description "The walls are lined with bookshelves that reach to the ceiling, filled with books of all shapes and sizes and the air is thick with the scent of leather and old paper. A door to the south leads back to the vestibule.",
        s_to Vestibule,
        cheap_scenery
          1 'door' 
          [;
            Examine:
              "Another massive wooden door.";
            Open, Enter:
              <<Go FAKE_S_OBJ>>;
          ],
  has light scored;

Object -> books "books",
  with name 'books' 'bookshelves' 'shelves' 'book' 'bindings',
    description "This is a most impressive collection of books, many of them very old and certainly invaluable. Some of the editions date back to the time when Blackwood Manor was a monastery. These handwritten and richly decorated early historical works were written by monks and scholars in darkened chambers with a quill by candlelight.",
    before [;
        Smell:
          "Old paper and leather, you would actually love a perfume that smells like that.";
        Touch:
          "History at your fingertips.";
        Burn:
          "What the hell is wrong with you?";
        Search:
          if (PROGRESS_LEVEL == 11) "You look for clues about a village in the forest but unfortunately find nothing at all.";
          if (PROGRESS_LEVEL ~= 9) "At the moment, you don't really know what you want to look up, and now is not the time for casual reading.";
          PROGRESS_LEVEL = 10;
          move eerievoice to FeralGarden;
		      scope_modified = true;
          amulet.player_progress = true;
          IncrementScore();
          "You find a clue about the amulet when you discover a drawing of Saint Cuthbert, probably the most famous saint of early Britain. You can see him wearing the amulet. Legend has it that the stone came from Golgotha. At the very moment Jesus Christ died, lightning struck the earth and created what you suspected to be trinitite. It reflects a moment when a bridge was built between the world of the living and the world of the dead.^^The amulet is said to have enabled its bearer to communicate with those who passed on to the other side.^^As you are about to leave, you hear a distant voice calling you from the garden, but somehow it seems so far away. Beyond the realms of existence.";
    ],
  has pluralname scenery;

Outside Forecourt "Forecourt"
  with name 'forecourt' 'manor' 'house',
        description "The forecourt allows a glance at the manor house, overgrown with moss and ivy. Its entrance to the east is a massive wooden door. From here, a stone bridge leads to a feral garden to the northeast.",
        e_to Vestibule,
        ne_to FeralGarden,
        cheap_scenery
          1 'ivy' "Cascading gracefully, ivy clings to the manor house. Its vibrant green leaves weave a tapestry of life against the weathered backdrop."
          22 'massive' 'wooden' 'door' 'oak'
          [;
            Examine:
              "It seems to be made from oak.";
            Open, Enter:
              <<Go FAKE_E_OBJ>>;
          ]
          2 'feral' 'garden' "The feral garden lies to the northeast, in case you want to take a closer look."
          11 'stone' 'bridge' "A stone bridge, old and weathered, gracefully defies the seasons for hundreds of years."
          ,
  has light scored;

Object -> car "car",
  with name 'car' 'Vauxhall' 'Astra' 'trunk',
    initial "Cora's car, a 1980 Vauxhall Astra, is parked here.",
    description "It's actually a nice car. Cora bought it a few days ago, when you moved from the US to Scotland.",
    before [;
        Enter, SwitchOn:
          "It makes no sense to use the car. There has been a lot of snow during the night and the only road that leads from the manor house back to civilisation is impassable at the moment.";
         Push, Pull, Turn: 
           "Doesn't move a bit.";
        Close:
          "The car is already closed.";
        Open, Search:
          if (foldingspade in self) {
            move foldingspade to Forecourt;
			      scope_modified = true;
            "You search the car and notice your folding spade is in the trunk. Seems that Cora borrowed it when she bought the Christmas tree.";
          }
          else "You take another look in the trunk but find nothing of interest.";
    ],
  has static;

Object -> -> foldingspade "folding spade",
  with name 'folding' 'spade',
    initial "You see your folding spade lying in the trunk of the car.",
    description [;
      print "A US Army folding spade, pretty indestructible.";
      print"^^Note: You actually don't need to ";
      !ChangeFgColour(clr_emphasis_fg);
      print "[fold]"; 
      !ChangeFgColour(clr_default_fg);
      print " or ";
      !ChangeFgColour(clr_emphasis_fg);
      print "[unfold]";
      !ChangeFgColour(clr_default_fg);
      print " this spade in the cause of this game. Just ";
      !ChangeFgColour(clr_emphasis_fg);
      print "[dig object with spade]";
      !ChangeFgColour(clr_default_fg);
      ".";
    ],
  has scored;

Outside FeralGarden "Feral Garden"
  with name 'feral' 'garden',
        description "You are standing in the feral garden, upon frost-covered grass. To the west is an old dovecote, its structure weathered but still standing. The trail to the north leads deeper into the forest, disappearing into the shadows of the trees. A cobbled path leads back southwest to the forecourt.",
        n_to [;
          if (PROGRESS_LEVEL < 11) "You shouldn't wander off. You don't know where to go.";
          if (PROGRESS_LEVEL == 11) "You don't know where this village is that the voice is talking about.";
          if (winterjacket hasnt worn) "You should find something warm to wear before going further.";
          if (self.player_progress == false) {
            self.player_progress = true;
            QuoteBox(quote_part2);
          }
          return ForestTrail;
        ],
        w_to Dovecote,
        sw_to Forecourt,
        player_progress false,
        cheap_scenery
          11 'frost-covered' 'grass' "Once vibrant grass, now frozen, crunches under your feet."
          12 'old' 'dovecote' 'structure' "The dovecote is a relic from the dark past. Take a closer look by going west."
          2 'trail' 'shadows' "The trail beckons, promising long forgotten mysteries hiding in the shadows of the forest."
          3 'forest' 'tree' 'trees' "Countless stories surround this forest. Sad fates are said to have been sealed here."
          12 'cobbled' 'path' 'cobblestones' "The cobblestones are icy and you slip quickly. You should be careful.
          ",
  has light scored;

Outside ForestTrail "Forest Trail"
  with name 'forest' 'trail',
        description [;
            print "Snow-covered trees tower above you. The trail is winding its way further northwest, disappearing into the dense flora of the woods, flanked on either side by knarled trees, their leafless branches like skeletal fingers. Further south, the path leads back Blackwood's feral garden.^";
            if (self hasnt visited) {
              !ChangeFgColour(clr_emphasis_fg);
              print "^[HINT: You're advised to save here. From this point on, your actions and decisions can influence how the game ends. You may want to come back later and try a different path.]";
              !ChangeFgColour(clr_default_fg);
              new_line;
            }
        ],
        s_to FeralGarden,
        nw_to Ruins,
        cheap_scenery
          34 'leafless' 'gnarled' 'snow-covered' 'trees' 'branches' 'snow' 'woods' "The trees, ancient giants that droop slightly under the weight of the snow, create a mesmerizing scene as the sunlight filters through the frosty canopy."
          3 'sunlight' 'sun' 'light' "The sun is barely peeking through. It is colder here than it was in the feral garden."
          1 'trail' "You suspect that nobody has been here in a long time. The trail is worn and overgrown."
          11 'dense' 'flora' "Here in the heart of the forest, where time treads cautiously upon the snow-kissed land, the flora dons a wondrous cloak of winter enchantment."
          ,
  has light scored;

Room Dovecote "Dovecote"
  with name 'dovecote' 'shed' 'inside',
        description "The dovecote is cluttered with garden tools. The nesting holes have been blocked off. A door to the east leads back to the garden.",
        e_to FeralGarden,
        cheap_scenery
          11 'garden' 'tools' "A million garden tools but neither a shovel nor a spade. This is hilarious."
          58 'regal' 'weathered' 'tattered' 'ornate' 'moth-eaten' 'attire' 'legs' 'sporran' 'waist' 'faded' 'velvet' 'doublet' 'torso' [;
            Examine:
              if (~~ LordBain in Dovecote) print_ret (string)MSG_PARSER_NOSUCHTHING;
              "Pretty much out of fashion by now.";
          ]
          2 'tartan' 'kilt'
          [;
            Examine:
              if (~~ LordBain in Dovecote) print_ret (string)MSG_PARSER_NOSUCHTHING;
              "Is he wearing knickers underneath?";
          ]
          11 'deep-set' 'eyes'
          [;
            Examine:
              if (~~ LordBain in Dovecote) print_ret (string)MSG_PARSER_NOSUCHTHING;
              "So much sadness in his eyes.";
          ]
          CS_ADD_LIST Dovecote (cheap_scenery_pt2),
        cheap_scenery_pt2
          11 'nesting' 'holes' "Times change. Birds are apparently no longer wanted here."
          1 'door' 
          [;
            Examine: 
              "You should consider writing a book about old, wooden doors.";
            Open, Enter:
              <<Go FAKE_E_OBJ>>;
          ]
          ,
  has light scored;

Object -> spot "spot",
  with name 'spot',
    initial "You recognise the dovecote from your dream, even though it looked different. You notice the spot where something has been buried.",
    description "If something was buried here, it was probably a long time ago. The spot looks absolutely unsuspicious.",
    before [;
      Dig:
        if (second == foldingspade) {
          move leatherbag to Dovecote;
          move self to Limbo;
		      scope_modified = true;
          "You dig at the spot with the folding spade and after a little while you find a leather pouch.";
        }
      DigWithHand:
        "The ground is too hard to dig with your hands.";
    ],
  has static;

Object -> -> leatherbag "leather pouch",
  with name 'old' 'leather' 'pouch',
    describe [;
      if (self hasnt moved) "^A leather pouch lies on the ground.";
    ],
    description "The leather pouch looks very old, almost medieval. The many years in the ground have not done it any harm.",
    before [;
      Receive:
        if (~~ noun == goldenkey or salt) "That object doesn't fit into the leather pouch.";
      Open:
        if (leatherbag.player_progress == true) rfalse;
        self.player_progress = true;
        PROGRESS_LEVEL = 2; ! ready to consult someone about the man with the scar
        give self open;
		    scope_modified = true;
        "You open the leather pouch, revealing a mysterious golden key. A thought occurs to you. Something was indeed buried in the dovecote. That means the man with the scar must be real. You should consult someone who knows more about the manor.";
      Insert, Fill:
        if (~~ second == leadcoffin) rfalse;
        if (~~ salt in self) rfalse;
        print "(assuming you meant the salt)^";
        <<Insert salt leadcoffin>>;  
    ],
    player_progress false,
    capacity 1,
  has container openable scored;

Object -> -> -> goldenkey "golden key",
  with name 'golden' 'key' 'Christian' 'symbolism',
    description "A golden key, richly decorated with Christian symbolism. Definitely medieval. There is an inscription on it: ~The holy Pearl of Scotland holds the key.~",
  has scored;

Outside Ruins "Ruins"
  with name 'ruins' 'trail',
        description "You see the ruins of a small village, with only overgrown remains of what were once a few houses, covered in a thick layer of snow and ice. The ruins seem to be in a deep slumber, frozen in time. Strange shapes and shadows lurk in the corners of your vision, making you wonder if you're really alone. The trail heads north and southeast from here.",
        n_to OverGrownCemetery,
        se_to ForestTrail,
        d_to [;
          if (cellarentrance in self) {
            print "You slowly descend to the darkest place you've ever been to.^";
            return Cellar;
          }
          "You cannot go that way.";
        ],
        before [;
          Listen:
            if (LordBain.player_progress == false) rfalse;
            if (cellarentrance in cellar) {
              move cellarentrance to Ruins;
			        scope_modified = true;
              IncrementScore();
              "You hold your breath. Then you hear it. A bloodcurdling whisper, spoken in a demonic tongue, leads you to the cellar entrance. The rotten door set into the ground slams into the darkness as you pull to open it.";
            }
        ],
        cheap_scenery
          56 'few' 'small' 'overgrown' 'remains' 'ruins' 'village' 'houses' 'settlement' "So this is all that is left of the lives once lived here, a faded brushstroke on a long forgotten canvas, abandoned relics and witnesses of a dark age."
          14 'thick' 'snow' 'ice' 'layer' 'of' "The winter keeps this place a prisoner under its merciless, icy veil."
          2 'masonry' 'arches' "It is unbelievable that after hundreds of years there is still something left of this settlement."
          13 'elusive' 'strange' 'shapes' 'shadows' "The elusive shadows seem to defy the laws of reality, as if they exist on a plane of existence not meant for mortal eyes. Whenever you turn to face them directly, they vanish, only to reappear when your attention wavers. This is very disturbing. You don't feel safe here."
          ,
  has light scored;

Room Cellar "Cellar"
  with name 'cellar' 'dim' 'light' 'dripping' 'of' 'water',
        description "The musty air is thick with the smell of decay, and the dim light of your electric torch reveals cobwebbed corners and forgotten debris. You can hear the scurrying of rats and the faint dripping of water in the distance. As you explore further, you notice strange symbols etched into the walls. Old wooden barrels and rusted tools are scattered about the cellar floor. Some barrels appear to be intact, while others have long since rotted, spilling their contents onto the floor. You can hear whispers, but you can't make out what they're saying. It could be a language you've never heard before. Narrow, creaky stairs lead up to the ruins of the abandoned village.",
        u_to [;
          if (electrictorch in player && electrictorch has on) {
            give electrictorch ~on;
            give electrictorch ~light;
			      scope_modified = true;
            print "You turn off your electric torch to conserve battery power.";
            new_line;
          }
          return Ruins;
        ],
        before [;
            Listen:
              "As much as you try to focus on the whispers, you don't understand what they are saying. They seem to come from a very dark place, where absent light makes all hope die.";
        ],
        cheap_scenery
          14 'musty' 'air' 'smell' 'of' 'decay'
          [;
            Examine, Smell:
              "It smells like something crept in here to die and now it's rotting somewhere in the dark. The flesh decomposes while the maggots gorge themselves on it. It's hard to keep the contents of your stomach down. And you certainly don't want to find out where that smell is coming from.";
          ]
          22 'cobwebbed' 'forgotten' 'corners' 'debris'
          [;
            Examine, Rub:
              "Even extensive cleaning would be hopeless. Disgusting.";
          ]
          1 'rats' "You fortunately can't see any rats. You hate rats. In the Middle Ages, they brought with them such wonderful gifts as the plague, which wiped out much of humanity."
          11 'strange' 'symbols' "The symbols are causing you a deep sense of unease and discomfort. What has happened here? Who etched them into the walls? And what is their purpose?"
          1 'whispers'
          [;
            Examine:
              "You hear them but you can't see where they are coming from.";
            Listen:
              <<Listen>>;
          ]
          CS_ADD_LIST Cellar (cheap_scenery_pt2),
        cheap_scenery_pt2
          33 'old' 'wooden' 'rusted' 'barrels' 'tools' 'contents'
          [;
            Examine, Search:
              "Just old crap. You shouldn't waste time with it.";
          ]
          21 'narrow' 'creaky' 'stairs'
          [;
            Examine:
              "The stairs are probably the best thing about this godforsaken cellar. You can't wait to get out of here.";
            Go, Enter:
              <<Go FAKE_U_OBJ>>;
          ]
          ,
   has ~light scored;

Object -> ancienttome "ancient tome",
  with name 'mysterious' 'ancient' 'tome' 'book' 'de' 'vermis' 'mysteriis',
    article "an",
    initial "In a corner is a dark alcove where a mysterious ancient tome glimmers in the light of your torch.",
    description "The title is ~De Vermis Mysteriis~. It seems to be written partly in Latin and partly in a runic language you don't know. In fact, you have heard of this book. It was written by Ludvig Prinn, a Belgian necromancer who was captured and executed for heresy during the 9th Crusade in 1271. The book was based on rites and incantations that Prinn learned from Shaitan cultists in ancient Syria. There were a few transcripts, but they are said to be all destroyed by the church. Could this be Prinn's original book?",
    before [;
        Open:
          "You leaf through the tome a little, then close it again. You can't understand everything written in it, the unknown runic language remains a mystery to you, but some of the drawings next to the runes are immensely disturbing.";
        Burn:
          if (second == nothing) "With what do you want to burn the tome?";
          if (second ~= lighter or candles) "You cannot burn the tome with this object.";
          if (second == lighter && lighter hasnt on) "The lighter is not lit.";
          if (leadcoffin.countdown == 2) "You try to ignite the tome but Ysabella, standing behind you, takes it away from you. It would probably have been better to distract her with something first.";
          if (leadcoffin.countdown ~= 1) "Let's assume for a moment that Lord Bain is not batshit crazy or bloody mental as people in this part of the world would say. If that really is the case, it's not right time to burn the tome now.";
          IncrementScore();
          move ancienttome to Limbo;
		      scope_modified = true;
          StopDaemon(leadcoffin);
          print "Within a second, the ancient tome catches fire. Startled by the intensity of the flames, you drop it on the ground.^";
          Choice_Ending_Neutral = false;
          Choice_Ending_Good = true;
          EndingSequence(); rtrue;
    ],
  has scored;

Object -> cellarentrance "cellar entrance",
  with name 'cellar' 'entrance' 'blackest' 'darkness',
    initial "A cellar entrance leads further down into the blackest darkness.",
    description "You can probably imagine how reluctant I am to go down there. Great, I'm already talking to myself. This won't end well.",
    before [;
        Enter:
          <<Go FAKE_D_OBJ>>;
    ],
  has static;

Outside OverGrownCemetery "Overgrown Graveyard"
  with name 'overgrown' 'graveyard',
        description [;
            print "The winter sun casts a pale light over the graveyard. The entrance gate is rusted and half-broken, with ivy vines creeping over its once-sturdy frame. The gravestones, some standing upright and others toppled over, are covered in thick moss and lichen. They are so old and weathered that it is difficult to read the inscriptions. It's unusually quiet, as if the whole forest prevents this forgotten ground. The silence is so complete that you can hear your own breathing, and every footstep you take seems to echo through the stillness. From here, you can only head south and return to the ruins.^";
            if (self hasnt visited) print "^The voice whispers to you: Come to me. You can wear the amulet now.^";
        ],
        s_to [;
          if (Ritual_started == true) "You can't just walk away now while the ritual is not completed.";
          return Ruins;
        ],
        cheap_scenery
            12 'sinister' 'surface' 'markings'
            [;
              Examine:
                if (gravestone in OverGrownCemetery) "What the hell is this?";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            21 'sickly' 'green' 'hue'
            [;
                if (leadcoffin in OverGrownCemetery) "It is fascinating and repulsive in equal measure.";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            55 'twisted' 'distorted' 'grotesque' 'macabre' 'ornate' 'engravings' 'patterns' 'symbols' 'scenes' 'faces'
            [;
                if (leadcoffin in OverGrownCemetery) "Why would someone engrave such disturbing scenes on a coffin?";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            CS_ADD_LIST OverGrownCemetery (cheap_scenery_pt2),
        cheap_scenery_pt2
            13 'pale' 'winter' 'sun' 'light' "It seems almost darker here than near the ruins. Of course, the pale winter sun is not very strong, but somehow this place seems to devour the light."
            31 'pale' 'tattered' 'flowing' 'gown' "There is something wrong with her. Yes, she is a ghost, but there is more to it than that."
            12 'of' 'signs' 'corrosion'
            [;
                if (leadcoffin in OverGrownCemetery) "The corrosion is not severe. Considering how long the lead coffin has been in the ground, it is still in a surprisingly good shape.";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            CS_ADD_LIST OverGrownCemetery (cheap_scenery_pt3),
        cheap_scenery_pt3
            3 'entrance' 'gate' 'frame'
            [;
              Examine:
                "So many centuries have passed and yet this gate still stands as a steadfast guardian, separating the world of the living from the world of the dead.";
              Open, Close:
                "Since the gate is half-broken, it makes no sense to open or close it. One can pass it without any problems.";
              Enter, Go:
                "You are already at the graveyard. In case you want to leave, go south instead.";
            ]
            14 'thick' 'ivy' 'vines' 'moss' 'lichen' "You don't feel this is the right place for botanical excursions."
            ,
  has light scored;

Object -> gravestones "gravestones",
  with parse_name [w n a len;
          n = wn;
          w = NextWord();
            if (w == 'gravestones') {
              a = WordAddress(n);
              len = WordLength(n);
              if (len == 11 && a->10 == 's')
              return 1;
            }
            if (w == 'graves') return 1;
            if (w == 'inscriptions') return 1;
            if (w == 'stones') return 1;
    ],
    description "You can make out some of the names and dates carved into the stone. It seems like nobody's been buried here after 1597, which gives you an idea about how long the village to the south is abandoned. ",
    before [;
      Examine:
        rfalse;
      Smell:
        "Walking around, sniffing graves... is that what you enjoy? You have a sick mind.";
      default:
        print_ret (string)MESS_SCENERY;
    ],
  has pluralname scenery;

Room Undercroft "Undercroft"
  with name 'undercroft',
        description "Your electric torch illuminates old walls that seem to stretch into infinity. The architecture is imposing and grand, with soaring arches and vaulted ceilings. In the dim light, you can make out old tapestries and flags hanging from the walls, faded and tattered with time, depicting battles and heroic deeds. You wonder how many of the these heroes might have once walked the halls of the manor. In one corner you see an ancient suit of armour, its once shining metal now covered in rust and cobwebs. You can almost hear the ghostly clanking of its joints as it marches off to war. Stone steps lead up to the chapel. A door leads to the northeast.",
        e_to [;
          if (entrance in self) return SecretPassage;
          "You can't go that way.";
        ],
        ne_to Crypt,
        u_to [;
          if (electrictorch in player && electrictorch has on) {
            give electrictorch ~on;
            give electrictorch ~light;
			      scope_modified = true;
            print "You turn off your electric torch to conserve battery power.";
            new_line;
          }
          return TheChapel;
        ],
        cheap_scenery 
          27 'old' 'heroic' 'battles' 'deeds' 'heroes' 'hero' 'scenes' 'tapestries' 'flags' "One wonders whether these are real heroes and battles or whether these scenes have sprung from the artist's imagination, depicting Scottish mythology."
          24 'shining' 'ancient' 'metal' 'suit' 'of' 'armour' "This would make you the king at your next Halloween party."
          11 'dim' 'light' "You hate dark places but hey, it could be worse."
          CS_ADD_LIST Undercroft (cheap_scenery_pt2),
        cheap_scenery_pt2
          1 'door' 
          [;
            Examine:
              "Yes, that's definitely a door. Another door. Everything is full of doors and it's your passion to examine them.";
            Open, Enter, Go:
              <<Go FAKE_NE_OBJ>>;
          ]
          11 'old' 'walls' 
          [;
            Examine:
              <<Examine walls>>;
          ]
          2 'rust' 'cobwebs' "It seems to be just flash rust. The armour would definitely look good in the great hall. But preferably without the cobwebs."
          2 'stone' 'steps'
          [;
            Examine:
              "The steps lead back up to the chapel.";
            Enter, Go:
              <<Go FAKE_U_OBJ>>;
          ]
          24 'soaring' 'vaulted' 'architecture' 'ceilings' 'ceiling' 'arches' "This place is so impressive. When the real estate agent showed you around the manor, he didn't mention it. You are pretty sure nobody has been here for centuries."
          ,
   has ~light scored;

Room SecretPassage "Secret Passage"
  with name 'secret' 'narrow' 'passage' 'tunnel',
        description "As you make your way through the narrow passage, you notice that the walls are rough and uneven, carved out of solid stone. There is a heavy smell of damp earth and moss in the air. You can hear the sound of water dripping from the walls and ceiling, forming small puddles on the uneven floor. The tunnel twists and turns, curving around sharp corners, occasionally sloping down or up, stretching further east and west from here.",
        e_to Dungeon,
        w_to Undercroft,
        before [;
          Smell:
            "Here in this gloomy place, there is something soothing about the familiar smell of nature.";
        ],
        cheap_scenery
         3 'solid' 'stone' 'ceiling' "It is very reminiscent of a mine tunnel, quite oppressive."
         13 'small' 'puddles' 'puddle' 'water' "Hopefully it won't get any worse with the water down here. Your shoes are winterproof but not necessarily waterproof."
         12 'sharp' 'corners' 'corner' "You can easily see from these sharp corners that this tunnel was only functionally carved into the stone."
         24 'heavy' 'damp' 'smell' 'earth' 'moss' 'air'
         [;
          Examine, Smell:
            <<Smell>>;
         ]
        ,
   has ~light scored;

Room Crypt "Crypt"
  with name 'crypt' 'room' 'perimeter' 'cracks',
        description "The smell of decay fills the air. You can barely make out the outlines of several stone coffins, neatly arranged in rows around the perimeter of the room, strikingly not decorated or inscribed. One coffin has been opened, revealing the remains of an ancient shroud and the faint outline of bones. To the south is a large door leading back to the undercroft. The ceiling is vaulted and made up of large stone slabs, some of which have cracked over time. You can also see rusted iron sconces on the walls, some of which still bear the remnants of long extinguished candles. On one side of the crypt are several murals depicting a monastery, an island, an abbey and a church.",
        s_to Undercroft,
        cheap_scenery
          'air' 'decay'
          [;
            Examine, Smell:
              "Gross, really gross.";
          ]
          11 'secret' 'compartment' 
          [;
            Examine, Search, Touch, Smell:
              if (PROGRESS_LEVEL < 8) {
                print_ret (string)MSG_PARSER_NOSUCHTHING;
              }
              else rfalse;
          ]
          3 'stone' 'coffins' 'rows' "It is easy to see, especially from the lack of decoration on the coffins, that no lords of the manor were buried here. Rather, the graves seem to be significantly older and to date from the time when Blackwood was a monastery."
          11 'ancient' 'shroud' "The shroud is in great shape considering how old it must be."
          1 'crosier' "Very simply worked, not as pompous as what you see in the hand of a bishop today."
          11 'large' 'door'
          [;
            Examine:
              "Another massive door you should catalog for your upcoming bestseller ~The Doors of Blackwood Manor~.";
            Enter, Open:
              <<Go FAKE_S_OBJ>>;
          ]
          CS_ADD_LIST Crypt (cheap_scenery_pt2),
        cheap_scenery_pt2
          23 'large' 'vaulted' 'ceiling' 'stone' 'slabs' "Impressive. This crypt has been built to last forever."
          22 'long' 'extinguished' 'remnants' 'candles' "What is partially preserved in these scones can no longer be called a candle. It is just a lump of something that bears a distant resemblance to wax."
          21 'rusted' 'iron' 'sconces' "The ravages of time have taken their toll on them."
        ,
  has ~light scored;

Object -> coffin "stone coffin",
  with name 'coffin' 'remains' 'bones' 'deceased' 'stone' 'faint' 'outline' 'body' 'of',
    description "A man of God seems to have been buried here, as evidenced by the crosier protruding from the shroud. The peace of the deceased has undeniably been undeniably disturbed. Someone was looking for something. But what could be the reason for such an act?",
    before [;
        Search:
          "If there was something to be found here, it probably has been found a long time ago. So you're not very surprised that your search reveals nothing of interest.";
        Touch:
          "The stone feels smooth and cold. The body you won't touch. No way.";
        Climb, Enter:
          "Are you planning to take a nap next to the body?";
    ],
  has scenery;

Object -> -> amulet "opaque amulet",
  with name 'opaque' 'amulet' 'green' 'greenish' 'shimmer' 'glass' 'molten' 'trinitite' 'leather' 'cord',
    article "an",
    initial "Inside of a secret compartment, you can see an opaque amulet. Something about it fills you with awe, even though you don't know why.",
    description [;
      if (self.player_progress == false) {
        print "Something about it fills you with awe, though you can't quite put your finger on it. Tied to an old leather cord, you recognise what at first glance could be molten glass. It has a greenish shimmer and is opaque. You have seen something like this before. It looks to you like trinitite, the glassy residue left on the desert floor after the plutonium-based Trinity bomb test in New Mexico back in 1945. Only extremely high temperatures can melt sand into trinitite.^^However, the leather cord does not fit the chronology. The Trinity bomb was detonated in 1945 and the leather looks very old. The fact that the amulet has been hidden here for many centuries also does not support your theory. You should try to find out more about the amulet. Perhaps there are records of it here in Blackwood? That would be obvious.^";
        PROGRESS_LEVEL = 9;
        IncrementScore();
      }
      else print "The chronicles say that the amulet belonged to none other than Saint Cuthbert of Lindisfarne. It is made of trinitite, which was created by a bolt of lightning on Golgotha at the very moment Jesus Christ died. According to the legend, Cuthbert used the amulet to communicate with the dead saints and martyrs of ancient Britain. Who knows what other secret powers it has.^";
    ],
    before [;
      Touch:
        "As soon as you touch the trinitite, you feel flooded with emotion. This is no ordinary amulet.";
      Wear:
        if (self.player_progress == false) "You actually know nothing about it. You rather don't want to wear it at this point.";
        if (~~ location == OverGrownCemetery) "Something in you still resists wearing the amulet. Is it the messenger of an evil premonition?";
        if (~~ self in player) {
          print "(taking the amulet first)^";
          move amulet to player;
		      update_moved = true;
		      scope_modified = true;
        }
        IncrementScore();
        give amulet worn;
        print "You put on the amulet. A strong, vibrant energy flows through your body, sharpening your senses. You feel the thin veil that separates this world from the other side being lifted.^";
        move Ysabella to OverGrownCemetery;
		    scope_modified = true;
        <<Look>>;
      Disrobe:
        if (self has worn) "You try to take remove the amulet, but something deep inside you prevents you from doing so. You just feel that now is not the right time to take it off.";
      Drop:
        if (self has worn) <<Disrobe self>>;  
    ],
    player_progress false,
  has clothing scored;

SaintPlate -> -> cuthbert "Saint Cuthbert plate",
  with name 'cuthbert' 'crowned' 'head' 'otters',
    description "A bishop with a halo has been engraved into the plate. He is holding a crowned head in his hands and is accompanied by otters. A beautiful and detailed work. It says ~Saint Cuthbert~."; ! Lindisfarne

SaintPlate -> -> kessog "Saint Kessog plate",
  with name 'kessog' 'soldier' 'habit' 'bow' 'arrow' 'soldier^s',
    article "the",
    description "A man with a soldier's habit has been engraved into the plate. He is holding a bow bent with an arrow in it. It says ~Saint Kessog~."; ! Inchtavannach, Loch Lommond

SaintPlate -> -> adomnan "Saint Adomnan plate",
  with name 'adomnan' 'candle' 'book',
    article "the",
    description "An abbot engraved into the plate. He is holding a candle while reading a book. A very profound symbolism. It says ~Saint Admonan~."; ! Iona Abbey

SaintPlate -> -> nathalan "Saint Nathalan plate",
  with name 'nathalan' 'grain' 'poor',
    article "the",
    description "A nobleman is engraved into the plate. He is depicted sharing grain with the poor. The act of charity is portrayed very poignantly. It says ~Saint Nathalan~."; ! Tullich church  

Constant MURALCONTENT "^^There is a square recess in the mural, containing ";

Mural -> lindisfarne "monastery mural", ! St. Cuthbert, Lindisfarne Monastery
  with name 'lindisfarne' 'monastery' 'sanctuary' 'stone' 'narrow' 'windows',
    description [;
      print "A monastery has been painted on the wall. You admire a weathered stone sanctuary with narrow windows peeking from high walls. The mural says: ~Lindisfarne Monastery~.";
      print (string)MURALCONTENT;
      if (~~ child(self) == nothing) PrintContents(0, self);
      else print "nothing";
      ".";
    ];

Mural -> inchtavannach "island mural", ! St. Kessog, Inchtavannach, Loch Lommond
  with name 'inchtavannach' 'island' 'loch' 'lommond' 'oak' 'trees',
    description [;
      print "A beautiful, secluded island has been painted on the wall. It's comparatively steep and lofty, covered with oak trees. The mural says: ~Inchtavannach, Loch Lommond~.";
      print (string)MURALCONTENT;
      if (~~ child(self) == nothing) PrintContents(0, self);
      else print "nothing";
      ".";
    ];

Mural -> iona "abbey mural", ! St. Adomnan, Iona Abbey
  with name 'iona' 'abbey' 'celtic' 'cross',
    description [;
      print "An impressive abbey has been painted on the wall with a massive celtic cross standing tall in front of it. The mural says ~Iona Abbey~.";
      print (string)MURALCONTENT;
      if (~~ child(self) == nothing) PrintContents(0, self);
      else print "nothing";
      ".";
    ];

Mural -> tullich "church mural", ! St. Nathalan, Tullich Church
  with name 'tullich' 'church' 'landscape' 'cross' 'slab' 'pitctish' 'symbol' 'stone',
    description [;
      print "The painting shows a small, picturesque church embedded in a beautiful landscape. Leaning against the church you see a cross slab and a Pictish symbol stone. The mural says: ~Tullich Church~.";
      print (string)MURALCONTENT;
      if (~~ child(self) == nothing) PrintContents(0, self);
      else print "nothing";
      ".";
    ];

[ CheckForPlate;
  if (noun ~= cuthbert or kessog or adomnan or nathalan) {
    print "That object doesn't fit into the mural's recess.";
    new_line; rfalse;
  }
  else rtrue;
];

[CheckIfOccupied senderobj;
  if (child(senderobj) == cuthbert or kessog or adomnan or nathalan) {
    print "There already is ";
    PrintContents(0, senderobj);
    print " in the recess of ", (the) senderobj, ". You need to take it out again before putting ", (the) noun, " in.";
    new_line; rfalse;
  }
  else rtrue;
];

[PlateAllocCheck;
  if (PROGRESS_LEVEL > 7) rtrue;
  if (nathalan.player_progress == true && adomnan.player_progress == true &&
      kessog.player_progress == true && cuthbert.player_progress == true) {
      move amulet to crypt;
	    scope_modified = true;
      PROGRESS_LEVEL = 8;
      IncrementScore();
      "^A click can be heard behind you. On the outside of the ransacked coffin a secret compartment has opened. Inside you notice an amulet.";
  }
];

Object Dungeon "Dungeon"
  with name 'dungeon' 'room',
        description "As you peer into the dungeon, you feel a chill run down your spine. The room is small and claustrophobic, with low ceilings and walls covered with ice crystals that seem to press in on you from all sides. You you catch sight of a wooden table covered in a macabre array of torture tools, glows ominously in the light of your electric torch. The stains which you suspect to be dry blood that mar the surface of the table, hint at the unspeakable horrors that have taken place here. You feel dark energy, as if the room is alive with malevolence. A cell door to the east leads back to the passage from which you came.",
        w_to SecretPassage,
        cheap_scenery
          24 'bare' 'bittersweet' 'eyes' 'tears' 'feet' 'aura'
          [;
            Examine:
              if (~~ TheBoy in Dungeon) print_ret (string)MSG_PARSER_NOSUCHTHING;
              "Everything about this boy fills you with deep sadness. But there is more. Something is not right, your gut tells you. Could it just be a facade? What an absurd thought.";
          ]
          18 'cold' 'stone' 'floor' 'ground' 'ceiling' 'ceilings' 'walls' 'ice' 'crystals'
          [;
            Examine, Touch:
              "Everything here in this dungeon is so cold. So much more than it should be. You feel the icy cold rising from the ground through your shoes. Even the ceiling and the walls are covered with ice crystals.";
          ]
          12 'wooden' 'table' 'surface' "Despite the creepy environment, it's just a table. What's placed on it is more terrifying."
          CS_ADD_LIST Dungeon (cheap_scenery_pt2),
        cheap_scenery_pt2
          14 'macabre' 'array' 'of' 'torture' 'tools' "The evil that people do to each other is immeasurable and repulsive."
          12 'dry' 'stains' 'blood' "How likely is it that such stains next to an array of torture tools are actually blood? Right. Quite likely."
          'cell' 'door'
          [;
            Examine:
             "How many people may have been trapped here, facing this door, the insurmountable obstacle that stood between them and the world outside. Left behind in a hideous hole surrounded by darkness.";
            Open, Enter:
              <<Go FAKE_W_OBJ>>;
          ]
          1 'light' "What a dark place."
        ,
   has ~light scored;

Object -> salt "salt",
  with name 'crystalline' 'powder' 'salt' 'white',
    initial "Someone spilled a line of white crystalline powder in front of the cell door.",
    description "You've read about this in one of the books here in Blackwood. Salt can stop ghosts and evil entities from entering a room if one simply spills a line of it in front of all entry points, like windows or doors. It can also be poured or spilled into a circle around a victim to create an area of protection that keeps spirits from attacking. A ghost can also be encircled by salt to prevent it from escaping, much like a devil's trap.",
    before [;
      Take:
        if (salt in leadcoffin) "The salt has been poured into the lead coffin. It's impossible to take it again.";
        if (salt in leatherbag && leatherbag in location) {
          print "(taking the container instead)^";
          move leatherbag to player;
		      update_moved = true;
		      scope_modified = true;
          "You take the leather pouch containg the salt.";
        }
        if (salt in leatherbag && leatherbag in player) "You already have it.";
        "You cannot loosely add this to your inventory. You need something to put it in.";
      Insert, Fill: 
        if (~~ second == leatherbag or leadcoffin) "That's not a good object to put the salt in.";
        if (second == leadcoffin) {
          if (leadcoffin.countdown ~= 2) "You might have a good idea but the timing is not right.";
          move salt to leadcoffin;
		      scope_modified = true;
          print "You pour the salt into the leadcoffin. The thing inside, which you are now certain is not human, twitches and writhes in pain as bloodcurdling screams alternate with guttural cries, more demonic and evil than anything you have ever heard. The eyes are fixed on you, vicious and hateful, not the eyes of a child, but those of a reptile. Like twin orbs of malevolence, they flicker with an otherworldly intensity, possessing an unsettling depth, as if peering into them could reveal the darkest secrets of the universe.^^The irises, a chilling shade of crimson, seem to pulsate with an ominous energy that sends a shiver down your spine. Within those crimson irises, you can discern a twisted reflection of your own fears and doubts, distorted and amplified. It's as if these eyes, which you understand now can only be the eyes of the Antichrist, have the power to see through your very soul, exposing your vulnerabilities and exploiting them for sinister purposes.^^As you try to look away, you notice the faintest hint of a maleficent spark, like dying embers of a wicked flame, conveying a sense of ancient knowledge, an awareness of the secrets that lie beyond mortal comprehension. In these depths, you can almost perceive the echoes of countless souls who have fallen under the Antichrist's sway.^^Ysabella yells at you: No! Why did you do that?^^Suddenly, a low howl begins to rise, growing in intensity. A strong wind whips through the ancient graveyard, rustling the tangled branches of the surrounding forest. The wind carries a haunting melody, as if a thousand voices cry in pain.";
          new_line; rtrue;
        }
        if (second == leatherbag) {
          if (self in leadcoffin) <<Take self>>;
          move salt to leatherbag;
		      scope_modified = true;
          print "You put the salt in the leather pouch.";
        }
        if (TheBoy in dungeon) {
          move TheBoy to Limbo;
		      scope_modified = true;
          print " The boy walks through the cell door and disappears.";
        }
        new_line; rtrue;
      Drop:
        if (self in leatherbag && leatherbag in player) {
          print "(dropping the container instead)^";
          move leatherbag to location;
		      scope_modified = true;
          "You drop the leather pouch containing the salt.";
        }
    ]
  has proper scored;

Object -> TheBoy "boy"
  with name 'ghost' 'boy' 'spirit' 'little' 'apparition',
    initial "The ghost of a little boy is here. He looks at you with uncertainty and deep sadness.",
    description "As you study the apparition, you discern the boy's visage, frozen in eternal youth. His features bear a sense of innocence, yet there is a haunting sadness etched into his translucent countenance. His eyes, wide and doe-like, hold a glimmer of unshed tears, reflecting the torment he endured in life's cruel grasp. His bare feet hover just above the stone floor, as if reluctant to touch the cold ground that once held him captive. In his presence, the air grows heavy with a bittersweet aura, as if the weight of his unfulfilled dreams and unfinished journey permeates the very atmosphere, yearning for release from the confines of this desolate dungeon and the torments that bound him here. But there is also something you can't quite grasp. Somehow you feel uncomfortable around him.",
    before [;
      Touch, Attack, Kiss, Pull, Push, Turn, Burn: 
        "You should refrain from such intentions. The boy has suffered more than enough.";
      Smell:
        "Hell, could you stop trying to smell ghosts?";
    ],
    talk_start 0,
  has animate;

Room Corridor "Grand Hallway"
  with name 'corridor' 'grand' 'hallway',
        description "You are standing in Blackwood's grand hallway, with fine tapestries and beautiful oil paintings on the walls, depicting landscapes and scenes from a bygone era. To the north, an old wooden door leads to the lounge. To the south, you get to the Solar. The bathroom is to the northwest. As you walk along the patterned carpet runner, you can feel the soft, plush fibres under your feet and notice that the design is in the repeating pattern of a Scottish tartan. The borders of the carpet are decorated with delicate flowers and vines. A spiral stone staircase leads down to the vestibule.",
        n_to Lounge,
        s_to Solar,
        nw_to Bathroom,
        d_to Vestibule,
        cheap_scenery
          11 'fine' 'tapestries' "The tapestries are breathtakingly beautiful."
          23 'beautiful' 'oil' 'paintings' 'landscapes' 'scenes' "You are pretty sure that some of these landscapes can be found around Blackwood. Great artists were at work here. The paintings literally come to life before your very eyes, like peaking out of the window into a long forgotten time."
          21 'old' 'wooden' 'door'
          [;
            Examine:
              "These old doors here in Blackwood are masterpieces of craftsmanship.";
            Open, Enter:
              <<Go FAKE_N_OBJ>>;
          ]
          33 'patterned' 'soft' 'plush' 'carpet' 'runner' 'fibres' "It is such a pleasure to walk on it. So soft and comforting."
          CS_ADD_LIST Corridor (cheap_scenery_pt2),
        cheap_scenery_pt2
          26 'repeating' 'scottish' 'design' 'pattern' 'tartan' 'borders' 'flowers' 'vines' "Scottish folk art is so diverse. How can you not love it?"
          4 'solar' 'bathroom' 'vestibule' 'lounge'
          [;
            Examine, Enter:
              "You may reach this location by going into the suggested direction.";
          ]
          3 'spiral' 'stone' 'staircase' "It leads down to the lower part of the manor."
        ,
  has light scored;

Room Solar "The Solar"
  with name 'solar' 'grand' 'chamber' 'corner' 'doors' 'door',
        description "You find yourself in the solar, a grand chamber that was once the private quarters of the lord and lady of the manor. The room is filled with the trappings of luxury and refinement, from the rich tapestries that adorn the walls to the antique furniture. In one corner of the room is a large four-poster bed with heavy velvet curtains drawn around it. Next to the bed is a small bedside table with an oil lamp. There are also a few modern items scattered around the room. A rotary telephone sits on a small table beside the bed, and a portable television with rabbit-ear antennas stands on a low shelf in the corner. The window at the end of the chamber lets you look far across the manor lands. Doors lead east to the dressing room and north back to the corridor.",
        n_to Corridor,
        e_to dressingroom,
        cheap_scenery
          12 'manor' 'lands' 'window' "Rolling hills and forests now blanketed in a thick layer of snow stretch out before you, dotted with clumps of trees and winding streams."
          38 'thick' 'winding' 'rolling' 'layer' 'snow' 'hills' 'forest' 'forests' 'streams' 'clumps' 'trees' "The view takes your breath away."
          1 'rosie'
          [;
            Call:
              <<Take telephone>>;
            default:
              "Rosie is not here.";
          ]
          23 'rich' 'antique' 'furniture' 'tapestries' 'trappings' "With such a historic setting filled with wonderful crafts and antiques waiting here for you, it wasn't hard to leave all furniture in New York when you moved here."
          CS_ADD_LIST Solar (cheap_scenery_pt2),
        cheap_scenery_pt2
          21 'heavy' 'velvet' 'curtains' "These curtains are so classy. You really feel like a nobleman when you wake up and the morning sun shines gently through."
          11 'bedside' 'table' "It's a nice, old piece but it's just a table, nothing special about it."
          21 'large' 'four-poster' 'bed' "This is by far the most comfortable bed you've ever slept in."
          'oil' 'lamp'
          [;
            Examine, Open:
              "Mmmmh. The lamp is missing oil. It's useless in its current state.";
            Take:
              "It makes no sense taking it with you as long as you're not sure if there is oil around to operate it.";
            Push, Pull:
              "Nah, you don't want to break it.";
            Smell:
              "You smell residues of oil that once in the oil lamp.";
            Burn:
              "You'd love to do that. With oil. You don't have oil.";
          ]
          'modern' 'items' "People in the Middle Ages would certainly have loved these too."
          5 'portable' 'rabbit-ear' 'tv' 'television' 'antennas'
          [;
            Examine:
              "The TV is a cheap no-name model, but it actually has a great picture. The antennas are cute.";
            SwitchOn:
              "You're really not in the mood to watch TV now.";
            SwitchOff:
              "It is already off.";
          ]
          11 'low' 'shelf' "Just a shelf, nothing to worry about."
        ,
  has light scored;

Object -> telephone "rotary telephone",
  with name 'rotary' 'bakelite' 'telephone' 'phone',
        description [;
          print "This is a very old British bakelite telephone, probably from the 1940s. You wouldn't expect anything less in Blackwood. It seems that every piece in the manor has a story to tell.^^Use it with ";
          !ChangeFgColour(clr_emphasis_fg);
          print "[call person]";
          !ChangeFgColour(clr_default_fg);
          print ".^^";
          !ChangeFgColour(clr_emphasis_fg);
          print "[Note: There are a some not-so-obvious call recipients available for your amusement. Calling these does not affect the story nor grant you points. Some make sense in the context of this game. Can you find out who it is? For one call recipient, you might need to be a fan of a certain album called ~Ride the Lightning~.]";
          !ChangeFgColour(clr_default_fg);
          new_line;
        ],
    before [;
        Take:
          if (PROGRESS_LEVEL < 2) "There's currently no need to call Rosie.";
          if (PROGRESS_LEVEL == 2) {
              if (~~ note in player or Solar) "You don't have Rosie's phone number.";
              PROGRESS_LEVEL = 3;
              print "You call Rosie. It seems as if the dial tone rings for an eternity but then she picks up the phone.^^You: Hey Rosie! I'm sorry to bother you. I have a question, and it may seem a little strange. Can you think of anything in relation to Blackwood about a man with a scar on his hand?^^Rosie: That's indeed a strange question, Sir Thomas. However, I think you should have a look at the gallery of ancestors. You may be interested in the painting of Lord Archibald Bain. The people here believe that he was not a good man. They claim he wiped out an entire village during the great Scottish witch hunts and was even responsible for the disappearance of some children in the manor's parish. Probably just old wives' tales, but there is a grain of truth in every tale they say.^^You: Indeed doesn't sound like a very pleasant fellow. Thank you Rosie, I'll have a look.^^You end the conversation.";
              new_line; rtrue;
          }
          else "The line is dead. Strange.";
    ],
  has scenery;

Room dressingroom "Dressing Room"
  with name 'dressing' 'room' 'area' 'light' 'corner',
        description "As you enter the dressing room, you are greeted by a warm, rustic atmosphere. The area is lit by a few small windows with thick wooden frames that allow just enough light to filter in. Along one wall is a row of old-fashioned wardrobes made of dark, polished wood, their doors adorned with floral motifs and brass handles. Some modern elements stand out among the traditional decor. A small clock radio sits on a shelf near the armchair and a computer workstation is tucked away in a corner of the room. You brought the computer, a Commodore Amiga 1000, with you from New York. A printer and some floppy disks are neatly arranged on the desk next to it. To the west, a door leads back to the solar.",
        w_to Solar,
        cheap_scenery
          12 'small' 'windows' 'window' "You wonder if these little windows once had a defensive purpose? They remind you of loopholes in old castles."
          21 'thick' 'wooden' 'frames' "These were certainly installed much later."
          12 'floral' 'motifs' 'flowers' "How vivid these wonderful, hand painted motifs are. You're pretty sure these flowers are not fictitious but specimens from the Scottish flora."
          'modern' 'elements' "You love the contrast between history and modern age."
          12 'small' 'clock' 'radio' "The radio even has a digital clock, so you're probably looking at the most modern item in the entire manor right now, with the exception of your computer of course."
          1 'shelf' "It's just a shelf, no need to worry about it."
          CS_ADD_LIST dressingroom (cheap_scenery_pt2),
        cheap_scenery_pt2
          1 'armchair' "It's a beautiful armchair and totally useless in case you want to progress in this game."
          5 'computer' 'workstation' 'Commodore' 'Amiga' '1000'
          [;
            Examine:
              "The Commodore Amiga is all the rage. You hope to write your next book on this computer. It has insane 512 kilobytes of RAM. You're pretty sure that one day, Commodore will rule the world.";
            SwitchOn, Open:
              "You're currently not in the mood to write or play something.";
            SwitchOff:
              "Your Amiga is already turned off.";
          ]
          1 'printer' "In New York, you could wake up the neighbours with this printer. Here in Scotland there are no neighbours. Problem solved."
          4 'floppies' 'floppy' 'disks' 'disk' "You take a look at the floppies. Oh, there is ~Zork - The Great Underground Empire~. Infocom's games are very sophisticated, unlike this one."
          1 'zork' "Yaaay, Zork! ~You are standing in an open field west of a white house, with a boarded front door. There is a small...~ Damn, you're rambling."
          1 'desk' "It's a lovely desk and it does the job well. One needs to consider that when it was built, people couldn't even imagine what a computer is."
          1 'door' "Another door and it looks surprisingly modern. You suspect that this room once was part of the solar before it had been separated."
        ,
  has light scored;

Object -> wardrobe "wardrobe",
  with name 'old-fashioned' 'wardrobes' 'dark' 'polished' 'wood' 'doors' 'brass' 'handles' 'wardrobe',
    description "The wardrobes most likely date from the early 18th century. You admire the outstanding craftsmanship in awe.",
    before [;
      Search, Open:
        if (winterjacket in self) {
          move winterjacket to location;
		      scope_modified = true;
          "You find your winter jacket and hang it on the wardrobe. You really need it in Scotland. Not necessarily here at the manor, where the open fire provides a cosy warmth, but once in a while you have to go out into the cold.";
        }
        else "You take another look but don't find anything of interest.";
    ],
  has scenery;

Object -> -> winterjacket "winter jacket",
  with name 'winter' 'jacket' 'purple' 'triple' 'fat' 'goose',
    initial "Your winter jacket is hanging on one of the wardrobes.",
    description "It's a purple Triple Fat Goose jacket, which you bought in New York last year. Cheeky 80s fashion in vibrant colours assures you're not getting lost in the snow. Hopefully.",
  has clothing scored;

Object Bathroom "Bathroom"
  with name 'bathroom' 'vaulted' 'ceiling' 'corner' 'centre' 'room' 'of',
        description "The bathroom is illuminated by a crystal chandelier, casting a warm and inviting glow on the marble floors and walls. In the centre of the room is a luxurious claw-foot bathtub with shiny silver taps and a pristine black iron drain. Against the wall is a polished wooden cabinet with gleaming brass handles. To your left, you can see a silver basin with a crystal-clear mirror above it. In the corner of the room is a sleek, modern toilet with a streamlined design. A stunning Celtic knot is carved into a stone that's embedded in the wall above the bathtub. A door to the south leads back to the grand corridor.",
        s_to Corridor,
        cheap_scenery
          11 'dusty' 'niche' 
          [;
            Examine, Touch, Smell:
              if (statuette in garnets) {
                print (string)MSG_PARSER_NOSUCHTHING;
              }
              else "Ugh!";
          ]
          3 'crystals' 'crystal' 'chandelier' "The crystal chandelier hangs suspended from the vaulted ceiling. As you step closer, a faint breeze stirs the air, causing the crystals to dance and twinkle like stars in the night sky, and you find yourself momentarily lost in its timeless beauty."
          3 'warm' 'inviting' 'glow' "The soft radiance seems to transcend the bounds of reality. It draws you in, evoking a sense of wonder and nostalgia."
          5 'marble' 'floor' 'floors' 'walls' 'wall' "Marble! This bathroom is worthy of a king."
          14 'luxurious' 'claw-foot' 'bath' 'bathtub' 'tub' "Cora loves this bathtub. Hot water, ethereal bath additives, a good tea, she literally celebrates it."
          CS_ADD_LIST Bathroom (cheap_scenery_pt2),
        cheap_scenery_pt2
          13 'shiny' 'silver' 'taps' 'basin' "You wouldn't be surprised if this is real silver."
          22 'pristine' 'black' 'iron' 'drain' "You love it. And it's absolutely nothing you need to worry about."
          33 'polished' 'wooden' 'gleaming' 'cabinet' 'brass' 'handles'
          [;
            Examine, Open:
              "You open it and find only towels inside. You don't need towels, so you close it again.";
          ] 
          12 'crystal-clean' 'mirror' 'reflection'
          [;
            Examine, Search:
              "You look in the mirror and a very familiar face stares back at you. What was that? For a moment it seemed to you that your reflection was smiling at you? An eerie smile, not a friendly one.";
          ]
          1 'door' 
          [;
            Examine:
              "You take a closer look and find... nothing at all. It's just a door!";
            Open, Enter:
              <<Go FAKE_S_OBJ>>;
          ]
          CS_ADD_LIST Bathroom (cheap_scenery_pt3),
        cheap_scenery_pt3
          21 'sleek' 'modern' 'toilet'
          [;
            Examine:
              "This particular toilet makes every session a pleasure.";
            Enter:
              "You don't feel like relieving yourself right now.";
          ]  
        ,
  has light scored;

Object -> knot "celtic knot",
  with name 'celtic' 'knot' 'endless' 'stone',
    description "With amazement you admire the work of art perpetuated in the stone. It is certainly a lot older than the rest of the bathroom. Celtic knots, in Scottish Gaelic ~snaidhm Ceilteach~, are loops that have no beginning or end, symbolising infinity. They are known for their adaption for use in the ornamentation of early Christian monuments and manuscripts, such as the Lindisfarne Gospels, a book produced around the years 715-720 in the monastery at Lindisfarne, off the coast of the Kingdom of Northumbria. In this context it makes sense to find Insular art here, as it likely dates back to the time when Blackwood was a monastery. Under the knot there are four garnets set into the stone.",
    before [;
        Push, Pull:
          "The knot itself doesn't move at all.";
    ],
  has scenery transparent;

Object -> -> garnets "garnets",
  with name 'gems' 'gemstones' 'garnet' 'garnets',
    description "The use of garnets as gemstones has been common since the Bronze Age. Garnets and other heavy minerals occur in many of the sedimentary beds of the North of England and Scotland. So it is not surprising to find them here alongside this wonderful Celtic knot.",
    before [;
        Pull:
          "You cannot pull the garnets. And you probably shouldn't anyway.";
        Touch:
          "You gently touch the garnets and feel the fine cut. They are incredibly beautiful.";
        Push:
          if (PROGRESS_LEVEL > 7) "You needn't worry about this anymore. You've already got what you came for.";
          if (~~ PROGRESS_LEVEL == 6) "It seems you can actually push in the garnets but no matter what you try, they always return to their original position.";
          print "You need to be more precise on this one ";
          !ChangeFgColour(clr_emphasis_fg);
          print "[push garnets in order 1234]";
          !ChangeFgColour(clr_default_fg);
          ".";
        PushInOrder:
          if (PROGRESS_LEVEL == 7) <<Push garnets>>;
          if (~~ PROGRESS_LEVEL == 6) <<Push garnets>>;
          if (second == 4423) {
            PROGRESS_LEVEL = 7;
            move statuette to location;
			      scope_modified = true;
            IncrementScore();
            "You can hear the sound of an ancient mechanism being activated behind the wall. Part of the Celtc knot opens to reveal a bronze statuette lying in a dusty niche behind it.";
          }
          "That didn't work. A wrong combination maybe?";
    ],
  has pluralname scenery;

Object -> -> -> statuette "bronze statuette",
  with name 'bronze' 'statuette' 'angel' 'archangel' 'gabriel' 'horn',
    initial "In a dusty niche inside the knot you see a bronze statuette.",
    description "You admire an angel blowing a horn. Could it be the Archangel Gabriel announcing Judgement day? Anyway, the statuette looks like it is part of something. The challenge will be finding that something.",
    before [;
      Take:
        if (self in panel) "You've put it where it belongs, so you don't need to pick it up again.";
        if (self.player_progress == false) {
          self.player_progress = true;
          move self to player;
		      update_moved = true;
		      scope_modified = true;
          "As soon as you move the bronze statuette, the mechanism activates and hides the niche behind the knot again. But you were quick enough to pull out the statuette which is now in your possession.";
        }
        rfalse;
    ],
    player_progress false,
  has scored;

Room Lounge "Lounge"
  with name 'lounge' 'leaded' 'window' 'opposite' 'side' 'room' 'exit',
        description "The decor of the lounge is traditional, with dark wooden paneling and plush furniture, such as the comfortable armchairs, perfect for cozying up with a good book or a glass of whisky. In one corner of the room, you notice a stereo system with a turntable and several vinyl records scattered nearby. A set of speakers flank the turntable. Nearby, you spot a well-stocked bar, with an array of liquors and mixers on offer. On the opposite side of the room, you see a large TV with a VHS system and video tapes stacked neatly nearby. A grandfather clock breaks the silence with its ticks. An exit to the south leads back to the grand corridor.",
        s_to [;
          if (PROGRESS_LEVEL == 1 && self.player_progress == false) {
            self.player_progress = true;
            QuoteBox(quote_part1);
          }
          return Corridor;
        ],
        cheap_scenery
          4 'speaker' 'speakers' 'Wharfedale' 'Diamonds' "Wow, these are Wharfedale Diamonds, every hi-fi enthusiast's dream."
          6 'tapes' 'movies' 'films' 'VHS' 'tape' 'video'
          [;
            Examine:
              "So many great films in one place. It won't get boring here if the manor is snowed in over the holidays.";
            Touch:
              "You feel plastic. Was it what you expected to find?";
            SwitchOn, Take:
              print "(in order to watch a film)^";
              <<SwitchOn vhssystem>>;
          ]
          5 'vinyl' 'record' 'records' 'covers' 'music'
          [;
            Listen:
              <<SwitchOn stereosystem>>;
            Examine: 
              "It seems that Cora found some of your albums in one of the moving boxes and brought them here for you. You can see Joy Division, New Model Army, Tears for Fears, Depeche Mode, Skeletal Family, The Cure and Killing Joke among others. Look at all those wonderful covers.";
            Smell:
              "Nothing beats the smell of vinyl records, the scent of your generation.";
            Touch: 
              "Feels like vinyl.";
            SwitchOn, Take, Open:
              print "(in order to listen to music)^";
              <<SwitchOn stereosystem>>;
            ]
            33 'plush' 'dark' 'wooden' 'paneling' 'decor' 'furniture' "Just like in a time-honoured gentlemen's club."
            CS_ADD_LIST Lounge (cheap_scenery_pt2),
          cheap_scenery_pt2
            11 'comfortable' 'armchairs'
            [;
              Examine:
                "These chairs are amazing. You could dwell in them forever.";
              Enter:
                "You love the idea but you have other things to do.";
            ]
            15 'well-stocked' 'bar' 'array' 'of' 'liquors' 'mixers' "Sometimes you feel like Scotland is all about the booze. Which is notably not a bad thing."
            11 'grandfather' 'clock' "The clock is probably from the 19th century. It ticks away like a metronome, in its indispensable tock washing away the present, devouring moments while one loses the sense of time."
            'tv' 'television' 
            [;
              Examine:
                "It's huge. Movies come to life on this one.";
              SwitchOn:
                print "(in order to watch a film)^";
                <<SwitchOn vhssystem>>;
            ]
            ,
        player_progress false,    
  has light scored;

Object -> vhssystem "VHS system",
  with name 'VHS' 'system' 'recorder' 'Ferguson' 'video',
        description "It's a Ferguson VHS video recorder, probably from 1982. Ferguson products are made in Britain and back when this recorder came out it roughly cost a monthly wage. The build quality is exceptional.",
    before [;
      SwitchOn:
          "You're currently not in the mood to watch a film.";
    ],
    player_progress false,
  has scenery;

Object -> stereosystem "stereo system",
  with name 'stereo' 'system' 'turntable' 'player' 'Pioneer' 'casing' 'metal',
    description "It's a state-of-the-art Pioneer system. Something you didn't expect to find here in Blackwood.",
    before [;
        Touch:
          "Your hand glides over the metal casing in awe.";
        Push, Pull:
          "What a glorious idea. Better not. You don't want to break it.";
        SwitchOn:
          if (Rosie in vestibule) "Before you're going to relax here, you should talk to Rosie first. She's probably still waiting in the vestibule for you.";
          if (self.player_progress == false) {
            print "You decide to play ~No Rest for the Wicked~ by New Model Army, one of your favourite records. You sit down in one of the armchairs and close your eyes. The ethereal guitars with heavy chorus effects gently lull you to sleep. Further and further you are carried away, back to a life lived long ago.^^You see the world with different eyes now. Not your eyes. Hastily you move through a garden that you somehow know. It is summer, and the scent of wild flowers fills the air. You walk purposefully towards a tower-like building, driven by one of the strongest emotions, fear.^^Inside, it takes a moment for your eyes, or rather the eyes through which you see, to adjust to the changed lighting conditions. Somehow the place seems historic, as if you have travelled back hundreds of years in time. You gaze up and see numerous birds lurking in holes in the walls. Then you look down and notice strange hands burying something. You can't make out what it is, but on the back of the hand, which is definitely not yours, you recognise a large and very distinctive scar.^^Before you can make out any more details, something pulls you back into your body. As you open your eyes, you feel the warm morning sun shining in your face through the leaded window. Obviously Cora didn't want to wake you last night and covered you with a warm blanket instead.^^What a strange dream it was. A mysterious place that seemed somehow familiar, the fear, the unknown object being buried, the scar. You have to catch up with Cora. She's probably still asleep in the solar, or already making breakfast.";
            new_line;
            move snowstorm to Limbo; ! snowstorm is over now
            PROGRESS_LEVEL = 1; ! progress bump, environment to waking up after the dream
            move Cora to Solar;
			      scope_modified = true;
            self.player_progress = true;
            IncrementScore(); rtrue;
          }
          if (self.player_progress == true )"You don't feel like listening to music now.";
    ],
    player_progress false,
  has scenery;

[EndingSequence;
  ! if (Choice_Ending_Bad == true) print "^You watch in fascination as the boy's body twitches in the coffin while new life flows through him. Was this Ysabella's plan all along? Wasn't the ritual meant to unite her with him? How is something like that even possible? The boy was dead. The next moment, a familiar voice behind you snaps you out of your thoughts.^^Rosie: You have done very well, Sir Thomas.^You: Rosie! You are here? You were right with everything you said about Blackwood. No wonder people have gone mad here over the centuries. I understand now.^^Rosie starts laughing uproariously.^You: What's so funny about that?^^Rosie: You understand? What do you think has happened here today?^You: It's a miracle. The only way I can explain it is that God gave the boy a second chance because of his sad fate.^^Rosie: God did indeed have something to do with the boy's fate. But what happened here today was neither God's work nor the work of the disciples of the hated Nazarene.^You: Why do you say that? Wait a minute... Where's Cora?^^Rosie hugs you and whispers in your ear: You just poured the rest of her into the coffin. Oh how she pleaded for her life. You really think you understood what happened here today? You understand nothing.^^You push Rosie away and try to scream, but all you can get out is a gurgling sound. Then you see the knife in her hand, the one she used to cut your throat. You grab your neck and feel warm blood oozing from the wound.^^Dazed, you sink to the ground. You look to the sky as you feel your spark of life leaving you. You don't feel ready to let go. Could one ever be ready for this? You try to understand how it came this far, but the real reasons that influenced the events of today will remain a mystery to you, unfathomable and more insignificant with each passing second in this dying body.^^The last thing you can think of fills you with a deep uncertainty. Will there be light at the end of the tunnel? Or what is it that comes next? Are you going to be stranded between the worlds? Another ghost forever condemned to roam the grounds of Blackwood Manor?";
  if (Choice_Ending_Neutral == true) print "^You turn around and look at Ysabella but what you see is not what you expected. She has changed and no longer appears in the translucent form of a ghost. You stand before a being that emanates an aura of profound eeriness. Her form is an unsettling blend of ethereal grace and twisted malevolence that defies comprehension and challenges your sanity. Her flesh is a sickly pallor, marked with pulsating veins that writhe beneath her ash-gray skin.^^You: Who are you?^Her lips, full and seductive, curve into an enigmatic smile that speaks of both allure and danger: Oh, like I already said, I'm just a loving mother. I have been given many names since I walk the earth. Feel free to choose one. I prefer Morning Star or Light Bearer.^^Her eyes, devoid of colour and depth, glow with an eldritch light that seems to penetrate you. They follow you with an unsettling emptiness, like two windows into a void that devours all hope and joy. Jagged obsidian horns curl menacingly from her forehead, casting sinister shadows across her twisted face. When she moves, her wings unfurl with grotesque grace, their desecrated feathers resembling shredded flesh. Each feather drips with an insidious darkness, oozing a viscous ichor. They writhe and twitch, animated by an evil energy, whispering in tongues that echo with the agonised cries of the damned.^^You try to run away but you are petrified. Lucifer is slowly making her way towards you. Then she is very close.^^Lucifer: I think I haven't introduced you properly to my son yet?^Ice-cold hands wrap around your neck from behind, choking you mercilessly. The world around you turns into a sea of deepest blackness as your screams echo through the forest.^^Cora: Thomas, wake up!^You open your eyes and recognise your wife, bent over you, looking at you anxiously.^^You: Cora? How did you find me in the forest?^Cora: Forest? I don't know what you're talking about. You had a bad dream. I heard you scream when I came through the door. You're obviously a little confused, so let me help you. It's the 23rd of December 1986 and I just came back from Inverness. Apparently you were waiting for me and put on some music here in the lounge. And then you fell asleep.^^You: Wow, I just had the worst nightmare of my life. It felt so real.^Cora: Please tell me everything about it. Let's talk tomorrow. It is already very late. I'm going to bed. Are you coming?^You: Yes, I'll be with you in a moment.^^Drowsy, you go to the bathroom and wash yourself with cold water. Your gaze wanders to the mirror and with horror you recognise the strangulation marks on your neck. It was just a dream, you say to yourself. Just a dream. Wasn't it?";
  if (Choice_Ending_Good == true) print "^The Antichrist's haunting screams ceased the very moment the ancient tome began to burn. Then you realise why. Wisps of smoke rise from the lead coffin, twisting and curling as if driven by an otherworldly force. A haunting wind howls through the graveyard, carrying with it the scent of burning flesh, a faint, unsettling odor. You approach the coffin and your heart races as you observe the macabre scene inside. The unholy boy's features are twisted in an expression of eternal torment. Flames dance mercilessly across his form, leaping in a grotesque ballet, consuming what little is left of his earthly vessel. His eyes, empty sockets now, seem to pierce through the veil between worlds, staring into the evil void from which he came.^^You turn slowly and Ysabella stands before you. Her eyes, pools of darkness, seem to hold both the weight of forgotten ages and the fire of a thousand stars. Time bends around her, and with a single heartbeat, the world transforms. The air grows even colder, and the icy breeze cuts with rage through the graveyard, carrying with it a mournful symphony of whispers and sighs. Before your very eyes, Ysabella shifts and twists into a portrait of malice, a visage that echoes the primordial terror of mankind's deepest nightmares. Gone is the semblance of elegance. The pure evil that emanates from her is palpable, a noxious miasma that curls and coils around your senses, a suffocating embrace that fills the air with impending doom.^^It is then, in that chilling momenChoice_Ending_Badt, that realization dawns like a jagged lightning bolt. The woman before you is not merely a specter of the past, nor a mere haunt of this forsaken ground. No, she is something far more ancient, far more insidious. Lucifer, the Morning Star, the embodiment of all that is forbidden and accursed. Her lips part, and a sound emerges, a voice that seems to reverberate not only in the confines of your mind but also in the very marrow of your bones.^^Lucifer: You will regret this.^She hisses, her words both a venomous caress and a sinister threat that echoes through the forest. A brief interlude that seems to last for aeons until she vanishes before you.^^And then you see him. The ghost of an old man who apparently watched your confrontation with Lucifer and her offspring, bathed in a faint, ethereal glow, emerging from the halo above is head.^^Cuthbert: You have done well, my son. I am Cuthbert of Lindisfarne.^You: I have so many questions.^^Cuthbert: And I'm afraid I don't have many answers, nor can I linger here for long. Everything happened as it had to happen, both then and now.^He extends a hand towards you.^Cuthbert: I'll take the amulet with me. It has served its purpose.^^You: Oh, the amulet. I understand that I had to find it in order to speak to Lord Bain. Without it, I'm sure I would have made a terrible mistake. But why did Lucifer want me to wear it here too?^Cuthbert: The Morning Star is a master of deception. In fact, the amulet was also an important component of her unholy ritual. Only a thin veil separates this world from other planes of existence. And the amulet is a key to look behind it. She would have used it to open the gates of hell and plunge the world into chaos forever.^You shiver as you think about it.^^So you remove the amulet and you hold it in your hand for a moment, where it sparkles as it reflects the light of the winter sun. Just a blink of an eye and then everything is gone. The amulet, Cuthbert, the coffin, even the charred remains of the unholy tome.^^As you make your way home, the day begins to fade towards dusk and a sense of calm settles in. The late afternoon sun casts long shadows on the landscape. When you reach the manor's feral garden, the sun inches close to the horizon and bathes the grounds of Blackwood in a warm glow of gold and amber. Snow-capped hills and valleys create a serene panorama, a world untouched by the darkness that had threatened it.";
  deadflag = 2; ! you have won!
];

[ Initialise; ! init vars and start the game
  
  ! set Z-machine colours 
  ! #ifv5; ! 2 black / 3 red / 4 green / 5 yellow / 6 blue / 7 magenta / 8 cyan / 9 white
	! if(HDR_TERPFLAGS->0 & 1) {
	! 	print "Colour Options^--------------^^This game comes with a predefined colour scheme that enhances your gaming experience.^^Would you like to use it? ";
	! 	if(YesOrNo()) {
  !     clr_on = true;
  !     clr_fg = CLR_WHITE;
  !     clr_bg = CLR_BLACK;
  !     clr_fginput = CLR_RED;
  !     clr_fgstatus = CLR_RED;
	! 	}
	! 	ClearScreen(WIN_ALL);
	! }
  ! #endif;

  score = 0; ! your score is at 0 when you start the game, makes sense, right? 
  notify_mode = false; ! deactivate score notifications if set false
  lookmode = 2; ! activate verbose look mode

  InitTalk(); ! initialise the talk menu extension

  thedark.description = TheDarkAdvanced;
  thedark.short_name = "In the dark";

  player.before = PlayerBefore; ! player.before override, see routine implementation
  player.parse_name = PlayerParseName; ! own teroutine that parses the player's name
  player.description = "Look at you. The author that came to Scotland to find inspiration. For some reason you feel like being stuck in a bad story now.";

  QuoteBox(quote_start); ! trigger the initial quote

  print "^Writer's block. And everyone waits for the next big thing. Oh, how you still love New York but it cast a dark veil over your inspiration. The change of scenery was needed. You didn't hesitate for a second when Cora told you about a historic manor house for sale near her home in the Scottish Highlands. It's only been a few days since you moved in and you haven't had much time to explore the property. Christmas is just around the corner, it's the 23rd of December 1986, and moving boxes are still piling up everywhere. A bit of chaos is fine. The seclusion out here makes up for it. You might even manage to write something during the contemplative days. How about a ghost story?^^";
];

[ TheDarkAdvanced;
  if (lighter has on) "The light of the lighter is unfortunately too weak to illuminate this location. And so you still stand here surrounded by darkness, likely to be devoured by a demon or some other soulless malevolence.";
  "You are surrounded by darkness. Some nastiness might drag you into the depths of hell.";
];

[ CreditsSub;
  print (string)Story, " is dedicated to Andreas Gustafsson.";
  !ChangeFgColour(clr_emphasis_fg);
  print "^^~Night gathers, and now my watch begins. For this night and all the nights to come.~^^-- George R.R. Martin^   A Song of Ice and Fire";
  !ChangeFgColour(clr_default_fg);
  "^^Beta ghost hunters: Ant Stiller, Marco Innocenti, Soenke Wolfgramm, Fredrik Ramsberg, Johan Berntsson, Shawn Sijnstra.^^This game was created with PunyInform, a custom library by Fredrik Ramsberg and Johan Berntsson for Graham Nelson's Inform language. Parser grammar and library messages partly adapted from Dialog by Linus Akesson.";
];

[ XyzzySub;
  "You perceive a mysterious whisper. Coming from everywhere and nowhere, yet present, it says ~Nice try.~";
];

[ PlayerBefore;
    Turn:
      "Now you feel dizzy. You've had better ideas than that.";
    Attack, Cut:
      if (PROGRESS_LEVEL == 14 && second == nothing) "Looks like you want to cut yourself with something. Try being more specific.";
      if (PROGRESS_LEVEL == 14 && second == boxcutter) {
        if (blood in boxcutter) {
          IncrementScore();
          move blood to location;
		      scope_modified = true;
          "You cut yourself with the box cutter. Just a little. Some blood drips onto the ground. The wound stops bleeding quickly.";
        }
        else "Once is enough. If you still have the urge to cut yourself, you should see a therapist.";
      }
      else "As hopeless as the situation may be, this is never a way out.";
    default:
      rfalse;
];

[ PlayerParseName n;
		n=0;
		while (NextWord()=='me' or 'myself' or 'self' or 'yourself' or 'you' or 'Thomas') {n++;}
		return n;
];

[ CallSub;
  if (location ~= Solar) "There must be telephone nearby to call someone.";
  else {
    if (noun == 'Cora' or Cora) "There's no need to call your wife.";
    if (noun == 'Ghostbusters') {
      "Voice on the phone: Hi, thanks for dialing 555-2368. You've reached the Ghostbusters hotline. We're not in right now, we're busy catching ghosts. You may leave your message after the beep. We'll get back to you as soon as possible. In urgent matters say to yourself: I ain't afraid of no ghost.";
    }
    if (noun == 'Cthulhu' or 'Ktulu') {
      "Voice on phone: Hello, the office of the Great Old Ones, department Master of R'lyeh is temporarily unmanned. His deity, the great dreamer Cthulhu, is currently on a higher cosmic plane enjoying his weekly tentacle massage. Please leave a message after the tone.";
    }
    if (noun == 999 or 112 or 'emergency' or 'police') {
      "The line's dead. This is a nightmare.";
    }
    if (noun == 666 or 'satan' or 'devil' or 'hell') {
      "Thanks for calling 666, the number of the beast. I am currently out collecting souls. Please leave a message after the beep, I'll send some temptation and sorrow along your way as soon as I can.";
    }
    if (noun == 'god' or 'heaven') {
      "Thanks for calling the heavenly hotline of God. At the moment I am on a surprise visit to see Richard Dawkins in Oxford. Please try again later. If you wanted to confess some sins, I hereby forgive you.";
    }
    else "That's not a valid call recipient.";
  }
];

[ PushInOrderSub;
  "That's not something you can push in order.";
];

[ PushOrderErrorSub;
  print "If you want to push something in order, you need to add a sequence ";
  !ChangeFgColour(clr_emphasis_fg);
  print "[push object in order 1234]";
  !ChangeFgColour(clr_default_fg);
  ".";
];

! game-specific grammar starts here
Verb 'call' * -> Call
            * noun -> Call
            * special -> Call;

Extend 'push' * noun 'in' 'order' special -> PushInOrder
              * noun 'in' 'order' -> PushOrderError;

Verb 'ring' = 'call';
Verb 'dial' = 'call';
Verb 'play' = 'switch';
Verb 'hear' = 'listen';

end;
