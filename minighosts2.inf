!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $MAX_ABBREVS=96
!% $TRANSCRIPT_FORMAT=1
!% $ZCODE_LESS_DICT_DATA=1
!% -r
!% -e
!% $OMIT_SYMBOL_TABLE=1
! ----------------------------------------------------------------------------
!  MINI-GHOSTS OF BLACKWOOD MANOR
!  An interactive Horror by Stefan Vogt
!  Copyright (c) 2023 Moonmist Entertainment
!  and Fusion Retro Books / ZZAP!64 
!  http://8bitgames.itch.io
! ----------------------------------------------------------------------------

! IFID (http://babel.ifarchive.org) -- generate with https://uuidgenerator.net
Array UUID_ARRAY string "UUID://a1180ac7-15dd-4677-aebf-6311ffaf0c21//";
#Ifdef UUID_ARRAY; #endif;

Constant Story    "Mini-Ghosts of Blackwood Manor";
Constant Headline "^An interactive Horror by Stefan Vogt^\
                   PART2 / SIDE B: The Ritual^\
                   (c) 2023 Moonmist Entertainment^\
                   and Fusion Retro Books / ZZAP!64^";
Release 8;

! game-specific defines
!Constant EXTENDEDCUT;
!Constant DEBUG; ! deactivate for release
Constant CUSTOM_ABBREVIATIONS;
Constant MAX_CARRIED = 20;
Constant NO_SCORE;
Constant OPTIONAL_FULL_DIRECTIONS;
Constant OPTIONAL_MANUAL_REACTIVE;
Constant OPTIONAL_MANUAL_SCOPE;
Constant OPTIONAL_MANUAL_SCOPE_BOOST;
Constant INITIAL_LOCATION_VALUE = ForestTrail;

! 0 production build, 1 notification on errors, 2 full error messages
Constant RUNTIME_ERRORS = 0;

Include ">abbrvs.h"; ! load optimized abbreviations
Include "globals.h"; ! Puny library file import
Include ">minihacks.h"; ! Moonmist library file import

Global PROGRESS_LEVEL = 0; ! tracks the game's progress level
Global Choice_Ending_Neutral = false;
Global Choice_Ending_Good = false;
Global Ritual_started = false;

! declare recurring properties to optimize story file size for Z-machine v5 spec
Property cheap_scenery;      ! used by Puny's cheap scenery extension
Property cheap_scenery_pt2;  ! additional cheap scenery to work around the 32 entry limit of properties
Property cheap_scenery_pt3;  ! additional cheap scenery to work around the 32 entry limit of properties
Property player_progress;    ! common property to be used when the player makes progress with an object
Property countdown;          ! property which can be used together with daeons and timers
Property talk_array;         ! used by Puny's talk menu extension

! Constants for puny extensions
Constant TM_MSG_TOPICS_DEPLETED "You've run out of topics to talk about and end the conversation.";
Constant TM_MSG_EXIT "You decide to catch up later and end the conversation.";

! import Puny extensions
Include "ext_talk_menu.h";
Include "ext_cheap_scenery.h";

! will be triggered when the room is described
! [ LookRoutine;
! ];

! will be triggered when you visit a new room
! [ NewRoom;
! ];

Include "puny.h"; ! puny library file import
Include ">minimist.h"; ! Moonmist library file import

! talk_menu arrays -- STATUS [ID 300-600] TOPIC PLAYERSAYS NPCSAYS [FLAGREF|UNLOCKREF|ROUTINE]*
! customize: TM_ADD_BEFORE_AND_AFTER, TM_ADD_AFTER, TM_ADD_BEFORE. reaction line after NPC respone

Array talk_Ysabella --> 
30 "Graveyard" TM_ADD_AFTER "What's your name? What are you doing here?" "She seems to be filled with infinite sadness. Without knowing her fate, you feel very sorry for her." "My name is Ysabella. I am waiting for my son to return." 1
0 "Son" "Your son? What happenend?" "Lord Bain's henchmen brought him to the manor after burning down our village. They wanted to purge the evil from him. What evil you may ask yourself? There was no evil in him. He was just a boy and did nothing wrong. When they were done with him, they buried him next to me, here on this forgotten ground. But his spirit seems to be trapped in Blackwood Manor, as if something is preventing him from leaving. And so I wait for him. Desperately. Together we can leave this place and step into the light but I don't want to let go until I have my little boy back. How can people be so cruel? Doesn't the Bible teach you to love your neighbour as yourself?" "She sobs and shakes her head." 1 2 3 
0 "Time" TM_ADD_BEFORE "What you are about to ask breaks your heart." "Are you aware that you have been waiting here for almost 400 years?" "Has it been that long? I don't know. I no longer have any sense of time. Maybe it was that long? Was it? Oh no."
0 "Lord Bain" "So Lord Bain did this to you?" "To do God's will. He said an angel had appeared to him and ordered him to destroy our village, all of us, to prevent the reversal of God's creation. Evil would walk among us and it would open the gates of chaos if he wouldn't prevent this from happening. If you ask me, what appeared to him was certainly no angel. Maybe he was just mad."
0 "Help" "I am so sorry to hear this. What can I do to help you?" "You have access to the manor. There is a secret dungeon in Blackwood where my boy must be trapped. As I already said, something prevents him from leaving. Find out what it is. Look for something unusual in the dungeon itself. You can reach the dungeon from the undercroft underneath the chapel. There is a stone with a signet. When you push it, a secret passage should open. After my son has been freed, we must also perform a ritual here on this graveyard that will lead him the way back to me. It will be his guiding light." 1
0 "Ritual" "What is needed for this ritual?" "For the guiding light ritual you need fire, something for the living, something for the dead and the blood of an innocent." "Ysabella looks at you and notices that you're confused." 1 2 3 4 YsabellaLevelUp
0 "Fire" "What do you mean with ~fire~?" "A few lit candles on my son's gravestone will do."
0 "Something for the living" "What do you mean with ~something for the living~?" "You'll need an item that keeps the living alive, a loaf of bread for example."
0 "Something for the dead" "What do you mean with ~something for the dead~?" "You are looking for an item that helps the sick and dying transition into the light. It could be oil for the final anointing or frankincense. You should find that somewhere in the chapel."
0 "Blood of an innocent" "I hope ~blood of an innocent~ doesn't mean what I think it means." "You need blood from a noble soul. Your wife seems to be a kind-hearted person. Ask her if she can give you a few drops of her blood. No one has to die to fulfil this part of the ritual. It's a ritual of love."
TM_END;

[YsabellaLevelUp;
  PROGRESS_LEVEL = 14;
];

! limbo for objects that we don't need anymore, beware of the grues!
Object Limbo;

Object -> winterjacket "winter jacket",
  with name 'winter' 'jacket' 'purple' 'triple' 'fat' 'goose',
    initial "Your winter jacket is hanging on one of the wardrobes.",
    description "It's a purple Triple Fat Goose jacket, which you bought in New York last year. Cheeky 80s fashion in vibrant colours assures you're not getting lost in the snow. Hopefully.",
  has clothing ;

Object -> amulet "opaque amulet",
  with name 'opaque' 'amulet' 'green' 'greenish' 'shimmer' 'glass' 'molten' 'trinitite' 'leather' 'cord',
    article "an",
    description "The chronicles say that the amulet belonged to none other than Saint Cuthbert of Lindisfarne. It is made of trinitite, which was created by a bolt of lightning on Golgotha at the very moment Jesus Christ died. According to the legend, Cuthbert used the amulet to communicate with the dead saints and martyrs of ancient Britain. Who knows what other secret powers it has.",
    before [;
      Touch:
        "As soon as you touch the trinitite, you feel flooded with emotion. This is no ordinary amulet.";
      Wear:
        if (self.player_progress == false) "You actually know nothing about it. You rather don't want to wear it at this point.";
        if (~~ location == OverGrownCemetery) "Something in you still resists wearing the amulet. Is it the messenger of an evil premonition?";
        if (~~ self in player) {
          print "(taking the amulet first)^";
          move amulet to player;
		      update_moved = true;
		      scope_modified = true;
        }
        give amulet worn;
        print "You put on the amulet. A strong, vibrant energy flows through your body, sharpening your senses. You feel the thin veil that separates this world from the other side being lifted.^";
        move Ysabella to OverGrownCemetery;
		    scope_modified = true;
        <<Look>>;
      Disrobe:
        if (self has worn) "You try to take remove the amulet, but something deep inside you prevents you from doing so. You just feel that now is not the right time to take it off.";
      Drop:
        if (self has worn) <<Disrobe self>>;  
    ],
    player_progress false,
  has clothing ;

Object -> Ysabella "Ysabella",
  with name 'ghost' 'Ysabella' 'woman' 'presence' 'ethereal' 'medieval' 'wisps' 'mist' 'translucent' 'spectre',
    initial "You encounter the ethereal presence of a medieval ghost woman, standing admidst the graves.",
    description "She is a spectre shrouded in a tattered, flowing gown that appears to be made of wisps of mist, billowing around her in an otherworldly fashion. Her form is translucent, revealing glimpses of pale skin. Sorrowful, hollow eyes seem to pierce through your very soul. Her long, cascading hair is a lustrous silver, floating gently as if moved by a phantom breeze. It frames her delicate face, accentuating her ethereal beauty, yet there is an undeniable air of melancholy that hangs about her.",
    before [;
      Talk:
        if (Ritual_started == true) "Ysabella: Not now, we need to focus on the ritual!";
        if (PROGRESS_LEVEL == 14) {
          print "She looks at you expectantly.^Ysabella: Are you ready for the ritual?^";
          if (YesOrNo()) {
            if (~~ candles in player && frankincense in player && loafbread in player && bloodlinen in player) "^Ysabella's appraising eyes measure you.^Ysabella: You say you are ready for the ritual but I see you're not. Come back when you carry everything I asked for.";
            if (~~ lighter in player) "Ysabella: I see you carry everything I asked for but you miss something to light the candles. Come back when you have it.";
            Ritual_started = true;
            move gravestone to OverGrownCemetery;
			      scope_modified = true;
            "^Ysabella: Come here and put the candles on the gravestone.^Ysabella floats to a gravestone that has no name on it.";
          }
          "^Ysabella: It's no problem you're not ready yet. Remember what is needed for the ritual. We need fire, which can be candles that you put on my son's gravestone. We need something for the living, which can be a loaf of bread. We need something for the dead, which can be oil for final anointing of a dying person or incense. And we need the blood of an innocent, which can be a few drops of blood gifted from your wife.";
        }
      Touch, Attack, Pull, Push, Turn, Burn: 
        "Even if that were a good idea, it wouldn't do much with a ghost, would it?";
      Smell:
        "Ghosts don't smell. At least Ysabella doesn't.";
    ],
    talk_array talk_Ysabella,
  has proper animate female;

Object -> gravestone "gravestone",
  with parse_name [w n a len;
          n = wn;
          w = NextWord();
            if (w == 'gravestone') {
              a = WordAddress(n);
              len = WordLength(n);
              if (len == 10 && a->9 == 'e')
              return 1;
            }
            if (w == 'grave') return 1;
            if (w == 'stone') return 1;
    ],
    describe [;
      print "^A gravestone looms ominously. It stands nameless, a foreboding presence in the pale light of the winter sun";
      if (parent(candles) == gravestone) {
        print ". Some ";
        if (candles.player_progress == true) print "burning ";
        print "candles are placed on it";
      }
      ".";
    ],
    before [;
      Touch:
        "It's so cold. You think it's even colder than it actually should be.";
      Push, Pull:
        "It doesn't move a bit. In fact, it looks like it could easily be knocked over, but the frozen ground holds it firmly in its icy grip.";
      Receive:
        if (~~ noun == candles) "You shouldn't put random stuff on the gravestone. You already know what needs to be placed on it.";
    ],
    description "Its surface is etched with sinister markings that evoke a sense of unease. The absence of a name adds to the unsettling aura, leaving whispered questions unanswered. Time has not been kind, as cracks and erosion mar the gravestone, reflecting the decay of forgotten memories.",
  has static supporter;

Object -> -> leadcoffin "lead coffin",
  with name 'lead' 'coffin' 'metal',
    description "You stare at a medieval lead coffin, weathered by time. The metal is now tarnished to a sickly green hue and shows signs of corrosion. The air around it feels even colder, as if an unearthly chill is emanating from the body inside. You notice that the engravings on its sides are not just ornate patterns and symbols, but rather grotesque and macabre scenes. Twisted figures with distorted faces, writhing in agony, adorn the coffin. Their expressions of fear and despair seem to follow you with hollow eyes.",
    before [;
      Smell:
        "Oh god.";
      Close:
        "That's probably what you should do but on the other hand you're too curious now to see where this is going.";
      Receive:
        if (~~ noun == loafbread or bloodlinen or frankincense or salt) "You shouldn't put things in the coffin that are not meant to be there. Ysabella told you what is needed.";
        switch (ObjectCapacity(self)-children(self)) {
          4:
            if (~~ noun == loafbread) "This object is not what Ysabella considered ~something for the living~. Please choose a different object.";
            move loafbread to leadcoffin;
			      scope_modified = true;
            "You put the bread in the lead coffin.^Ysabella: Auferte panem a discipulis Nazareni! Transibunt plagae terram suam!^^She raises her hands in an almost threatening gesture. Then she smiles at you. A smile that somehow seems false.^Ysabella: Now something for the dead, Thomas.";
          3:
            if (~~ noun == frankincense) "This object is not what Ysabella considered ~something for the dead~. Please choose a different object.";
            move frankincense to leadcoffin;
			      scope_modified = true;
            print "You use the lighter to make the frankincense smoulder, then you place it in the coffin.^Ysabella: Aperi portas inferi mortuorum. Ultimus Saturnus hac nocte fulgebit in firmamento, clarior sidus in odio Bethleem. Adventum tenebrarum incarnati praedicabit!^^Ysabella: And now the blood of an innocent";
            ".";
          2:
            if (~~ noun == bloodlinen) "This is not the right object for the ~blood of an innocent~ part of ritual.";
            move bloodlinen to leadcoffin;
			      scope_modified = true;
            print "You put the bloody linen fabric into the coffin and the boy inside starts to breathe. New life seems to flow into the dead body. How is that possible?";
            Choice_Ending_Neutral = true;
            StartDaemon(self);
            new_line; rtrue;
        }
    ],
    capacity 5,
    countdown 3,
    daemon [;
        self.countdown--;
        switch (self.countdown) {
          2: 
            print "^Ysabella mumbles something. She still seems to be busy with the ritual.";
            print "^^[You can now WAIT for Ysabella to finish the ritual. Alternatively, you can try to intervene, if that is what you want. What will you do?]";
            new_line; rtrue;
          1:
            if (~~ salt in leadcoffin) {
              if (lighter has on) print "Ysabella, standing behind you, takes the ligher away from you. It would have been better to distract her with something before you try to disturb the ritual.^";
              EndingSequence(); rtrue;
            }
            print "^[You feel that something is about to happen and your next move has to be a good one.]^";
          0:
            print "^A strong wind comes up and blows the salt away.^";
            EndingSequence(); rtrue;
        }
    ],
  has static container open;

Object -> -> -> "body",
  with name 'inside' 'body' 'corpse' 'boy',
    description "From your point of view, it is impossible that the body of this boy can be so well preserved after hundreds of years. It's either a miracle or a bad omen.",
    before [;
        Touch, Push, Pull, Smell, Take, Remove: 
          "Um... no. Absolutely not. What's wrong with you?";
    ];

Object -> LordBain "Lord Bain"
  with name 'ghost' 'lord' 'bain' 'spirit' 'spectral' 'figure' 'medieval' 'scottish' 'visage' 'ghostly' 'face',
    initial "In the center of the dovecote, you behold the spectral figure of a medieval Scottish lord, his presence commanding attention even in his ethereal form. It is Lord Bain, slayer in the name of God.",
    description "Dressed in regal attire befitting his noble station, the spirit stands tall and proud. His tattered yet ornate tartan kilt drapes around his legs and a weathered sporran hangs from his waist. A faded and moth-eaten velvet doublet adorns his torso, its once vibrant colours now faded with the passing centuries. The Lord's ghostly visage exudes an aura of wisdom and authority. His face, though partially obscured by the veil of time, bears the weathered lines of a life well lived, etched with both hardship and triumph. Deep-set eyes, their piercing gaze still undimmed, survey the dovecote with a mixture of solemnity and longing.",
    life [;
        Show, Give:
          if (noun) "Lord Bain looks at the object with little interest. It does not seem to jog his memory.";
          if (LordBain.player_progress == false) {
            LordBain.player_progress = true;
          }
          "You show Lord Bain the chronology. He looks at you thoughtfully, then nods sadly.^Lord Bain: I remember. You probably think I'm pure evil, but I assure you I'm not. I have seen the true evil.^You: The boy?^^Lord Bain: The Lord of Lies speaks with a seductive tongue and appears dressed in the cloak of innocence. Whatever he or his followers want from you, trust no one. He may look like a boy but he is Lucifer's offspring. With the help of a Saint we were able to separate his body from his evil mind and trap him inside Blackwood's dungeon. If you want to ban him from the face of the earth, you need to reunite his dark consciousness with his flesh and then destroy the source of his power.^You: Why didn't you do that back then?^^Lord Bain: The saint was very clear in his instructions about what we have to do. He said there would come a time many moons later when the unholy child would be defeated once and for all. Since then I have been waiting in this limbo for that day to come.^You: I see. And what about his source of power?^^Lord Bain: The villagers used a forbidden book to conceive the child with Lucifer. It is possible that the Light Bearer even lived in this village for a while. We do not know. What we did find out was that the father of the unholy child died during conception. His mother allegedly lived in the village, but we never found her. That's confusing, isn't it?^You: Not if you are open to the theory that Lucifer could be female.^^Lord Bain: That's an interesting theory. We never took that into consideration.^You: I think I know everything I need to know now. I will search for this book and try to destroy it at the right time. Thank you, Lord Bain.^^Lord Bain: Farewell my son. Remember what your eyes can't see, your ears might hear.^You nod and then turn away from him.";
    ],
    before [;
      Touch, Attack, Pull, Push, Turn, Burn: 
        "There would be no point in doing that to someone that has no physical form.";
      Smell:
        "Why would you want to sniff a ghost? That's really disturbing.";
      Talk:
        if (self.player_progress == false) {
          "You: Lord Bain?^Lord Bain: I think that was my name once upon a time. Whatever you want from me, I probably won't be able to help you. I have to admit that my memory has faded. I wish I could remember why I'm still here.";
          }
          "Lord Bain: You know what to do. Find the source of evil and destroy it when the time is right. What the eyes can't see, your ears might hear. May the Lord be with you.";
    ],
    player_progress false,
  has proper animate;

Object -> frankincense "frankincense",
  with name 'metal' 'tray' 'frankincense' 'aromatic' 'resin' 'incense',
    description "A metal tray containing frankincense. For thousands of years, the aromatic resin is harvested from Boswellia trees in the Middle East. It looks coarse-grained and translucent brown-yellow to reddish-brown in colour.",
    before [;
        Smell:
          "Frankincense has this special, very characteristic smell which you really like.";
        Burn:
          "You needn't worry about burning the frankincense.";
        Take:
          if (self in leadcoffin) "The frankincense is part of the ritual. It needs to stay in the lead coffin.";
    ],
  has proper ;

Object -> loafbread "loaf of bread",
  with name 'loaf' 'of' 'bread',
    description "A fresh and delicious loaf of bread taken from the manor's pantry.",
    before [;
      Eat:
        "You really don't want to eat right now.";
      Take:
        if (self in leadcoffin) "The bread loaf is part of the ritual and needs to remain in the lead coffin.";
    ]
  has ;

Object -> lighter "lighter",
  with name 'lighter',
    short_name "lighter",
    describe [;
      if (self hasnt moved) {
        print "^You see a lighter ";
        if (self has on) print "(lit) ";
        "next to one of the candles.";
      }
    ],
    description [;
        print "In contrast to everything you've seen so far in Blackwood, this is a fairly modern and generic lighter, currently ";
        if (self hasnt on) print "not ";
        "lit.";
    ],
    before [;
        Burn, SwitchOn:
          if (~~ self in player) {
            print "(taking the lighter first)^";
            move lighter to player;
			      update_moved = true;
			      scope_modified = true;
          }
          if (self has on) "The lighter is already lit.";
          if (self.time_left ~= 0) "The lighter is too hot, you need to wait a bit.";
          give self on;
          self.short_name = "lighter (lit)";
          StopTimer(self);
          StartTimer(self, 2);
          print "You light up the lighter.^";
          if (location == thedark) <<Look>>;
          else rtrue;
        SwitchOff:
          if (self hasnt on) "The lighter is already unlit.";
          give self ~on;
          self.short_name = "lighter";
          StopTimer(self);
          "The lighter is now unlit again.";
    ],
    time_out [;
      if (self has on) {
        self.short_name = "lighter";
        StartTimer(self, 3);
        give self ~on;
        "^Ouch, the lighter became too hot in your hands and is now unlit.";
      }
    ],
    time_left,
    player_progress false,
has switchable ;

Object -> electrictorch "electric torch",
  with name 'electric' 'torch' 'flashlight' 'Ever Ready',
    describe [;
      if (self hasnt moved) {
        print "^You notice an electric torch ";
        if (self has on) print "(providing light) ";
        "sticking out of the pocket of an old jacket.";
      }
    ],
    article "an",
    description [;
        print "It's an Ever Ready electric torch, probably from the 1970s, ";
        if (self hasnt on) "currently off.";
        "shining brighter than the Morning Star.";
    ],
    before [;
        Drop:
          if (location == thedark) "It's not a good idea to drop your only source of light in the dark.";
        SwitchOff:
          if (self in Cellar) "It won't be a good idea to switch off your only source of light here while not carrying it. You won't be able to find it again and this area would become inaccessible.";
    ],
    after [;
        SwitchOn: 
          give self light;
          scope_modified = true;
        SwitchOff: 
          give self ~light;
          scope_modified = true;
    ],
has switchable ;

Object -> linen "piece of linen fabric",
  with name 'linen' 'fabric' 'piece' 'of',
    description "A piece of linen which doesn't seem to have been machine made, so it's probably an older fabric.",
    before [;
        PutOn:
          if (second == blood) {
            if (linen in location) print "(taking the linen fabric first)^";
            <<Rub blood linen>>;
          }
    ],
  has ;

Object -> candles "set of candles",
  with name 'candle' 'candles' 'set' 'of',
    description [;
        print "A set of pretty ordinary candles";
        if (self.player_progress == true) print ", currently lit";
        ".";
    ],
    before [;
      Take:
        if (parent(self) == gravestone) "The candles are exactly where they are supposed to be.";
      Burn:
        if (second == nothing) "Not the worst idea you've had. But how are you going to light the candles?";
        if (~~ second == lighter) "You can't light the candles with this.";
        if (~~ parent(self) == gravestone) "The candles must be securely placed before being lit.";
        if (lighter hasnt on) {
          print "You should first ";
          print "[ignite the lighter]";
          ".";
        }
        self.player_progress = true;
        move leadcoffin to OverGrownCemetery;
		    scope_modified = true;
        "You light the candles on the gravestone.^Ysabella: Veni ignis, ilumina viam impiorum!^^Ysabella now levitates over the grave, moving her arms in an incantatory gesture. The snow begins to melt. You can barely believe your eyes as you witness the earth shifting to uncover a medieval lead coffin. As if by magic, the lid opens to reveal the dead body of a boy. Horrified, you realise that he shows no signs of decomposition. In fact, he looks as if he is peacefully asleep. A cold shiver runs down your spine.^^Ysabella: now place something for the living in the coffin.";
      SwitchOn:
        print "You cannot turn on the candles but you probably want to ";
        print "[light candles with something]";
        "?";
    ],
    player_progress false,
  has pluralname ;

Object -> knife "knife",
  with name 'sharp' 'knife',
    description "A pretty sharp knife, the essence of all slasher movies.",
  has ;

Object -> -> blood "few drops of blood",
  with name 'few' 'drops' 'of' 'blood',
    initial "You notice a few drops of your blood on the ground.",
    description "The whole story has become far too bloody for my taste.",
    before [;
      Take:
        print "You can't just pick up the blood with your hands. Maybe you can ";
        print "[wipe blood with something]";
        "?";
      Rub:
        if (second == nothing) "Which object do you want to use to wipe up the blood? Try being more specific.";
        if (~~ second == linen) "This is not a good object to wipe the blood with. You can surely find something better.";
        move bloodlinen to player;
        move blood to Limbo;
        move linen to Limbo;
		    update_moved = true;
		    scope_modified = true;
        "You wipe the blood with the linen fabric.";
    ],
  has static pluralname;

Object -> -> -> bloodlinen "bloody linen fabric"
  with name 'bloody' 'linen' 'fabric',
    description "A piece of linen fabric with your blood on it. The day is getting better and better.",
    before [;
        Take:
          if (self in leadcoffin) "The bloody linen fabric is part of the ritual and needs to remain in the lead coffin.";
    ],
  has ;

Outside ForestTrail "Forest Trail"
  with name 'forest' 'trail' 'layer' 'frost' 'snowflakes' 'sprinkling' 'sharp' 'thorns' 'winter' 'berries' 'monochromatic' 'landscape' 'fruits' 'splashes' 'of' 'crimson',
        description [;
            print "Snow-covered trees tower above you, their branches creaking and swaying in the frigid wind. The trail ahead of you is barely visible, winding its way further northwest through the thick undergrowth and disappearing into the dense flora of the woods. The trail is narrow, flanked on either side by a wall of gnarled trees and prickly bushes, their leafless branches stretching out like skeletal fingers. It's very quiet except for the occasional rustle of a small animal darting through the underbrush. Further south, the path leads back to the feral garden of Blackwood Manor.^";
            if (self hasnt visited) {
              print "^[HINT: From this point on, your actions and decisions can influence how the game ends. You may want to come back later and try a different path.]";
              new_line;
            }
        ],
        s_to [;
          "There is no going back now.";
        ],
        nw_to Ruins,
        cheap_scenery
          69 'leafless' 'frosty' 'glistening' 'dense' 'gnarled' 'snow-covered' 'trees' 'branches' 'snow' 'woods' 'white' 'canopy' 'pine' 'fir' 'spruce' "The trees, ancient giants that stand out among the breathtaking flora, mostly pine, fir and spruce, are covered with a dense layer of glistening white. Their branches droop slightly under the weight of the snow, creating a mesmerizing scene as the sunlight filters through the frosty canopy."
          3 'sunlight' 'sun' 'light' "The sun is barely peeking through the trees. It is significantly colder here than it was in Blackwood's feral garden."
          2 'frigid' 'wind' "It's an icy breeze. Without warm clothing, it can quickly become dangerous out here."
          1 'trail' "You suspect that nobody has been here in a long time. The trail is worn and overgrown with moss and ferns, winding its way into the unknown."
         CS_ADD_LIST ForestTrail (cheap_scenery_pt2),
        cheap_scenery_pt2
           46 'dense' 'thick' 'prickly' 'breathtaking' 'undergrowth' 'bushes' 'flora' 'moss' 'ferns' 'underbrush' "Here in the heart of the forest, where time treads cautiously upon the snow-kissed land, the flora dons a wondrous cloak of winter enchantment. The prickly bushes stand guard like sentinels amidst the wintry expanse, clad in glistening armor of frost and veiled with a delicate sprinkling of snowflakes. Their sharp thorns, a fierce defense against any intruder, shimmer with an icy glint, daring anyone to approach too closely. Patches of winter berries add splashes of crimson to the otherwise monochromatic landscape. These resilient fruits cling tenaciously to their branches, a vibrant symbol of hope and sustenance amidst the austere season. Their sweet essence lures the weary traveler, beckoning them deeper into the forest's embrace, where each step yields a symphony of subtle crunches beneath the frost-kissed boots."
           23 'occasional' 'small' 'rustle' 'animal' 'underbrush' "You can't actually see what's darting through the underbrush."
           13 'feral' 'Blackwood' 'Manor' 'garden' "Blackwood is not visible from here but you may go south to get back to the manor."
           ,
  has light ;

Outside Ruins "Ruins"
  with name 'ruins' 'trail',
        description "You see the ruins of a small village, ancient and weathered, covered in a thick layer of snow and ice. The masonry is crumbling, the arches distorted and twisted by years of decay. The village must have been abandoned centuries ago. Strange shapes and shadows lurk in the corners of your vision, making you wonder if you're really alone in this place. The trail heads north and southeast from here.",
        n_to OverGrownCemetery,
        se_to ForestTrail,
        d_to [;
          if (cellarentrance in self) {
            print "You slowly descend to the darkest place you've ever been to.^";
            return Cellar;
          }
          "You cannot go that way.";
        ],
        before [;
          Listen:
            if (LordBain.player_progress == false) rfalse;
            if (cellarentrance in cellar) {
              move cellarentrance to Ruins;
			        scope_modified = true;
              "You hold your breath and listen to distant howling of the wind. Then you hear it. A bloodcurdling whisper, spoken in a demonic tongue, leads you to the cellar entrance. The rotten door set into the ground slams into the darkness as you pull to open it.";
            }
        ],
        cheap_scenery
          36 'small' 'ancient' 'weathered' 'stone' 'remains' 'ruins' 'village' 'houses' 'settlement' "So this is all that is left of the lives once lived here, a faded brushstroke on a long forgotten canvas, abandoned relics and witnesses of a dark age."
          14 'thick' 'snow' 'ice' 'layer' 'of' "The winter keeps this place a prisoner under its merciless, icy veil."
          2 'masonry' 'arches' "It is unbelievable that after hundreds of years there is still something left of this settlement."
          13 'elusive' 'strange' 'shapes' 'shadows' "The elusive shadows seem to defy the laws of reality, as if they exist on a plane of existence not meant for mortal eyes. Whenever you turn to face them directly, they vanish, only to reappear when your attention wavers. This is very disturbing. You don't feel safe here."
          ,
  has light ;

Room Cellar "Cellar"
  with name 'cellar' 'dim' 'light',
        description "The musty air is thick with the smell of decay as the dim light of your electric torch reveals cobwebbed corners and forgotten debris. Somebody etched strange symbols into the walls. Old wooden barrels and rusted tools are scattered about the cellar floor. You can hear whispers, but you can't make out what they're saying. It could be a language you've never heard before. Narrow, creaky stairs lead up to the ruins of the abandoned village.",
        u_to [;
          if (electrictorch in player && electrictorch has on) {
            give electrictorch ~on;
            give electrictorch ~light;
			      scope_modified = true;
            print "You turn off your electric torch to conserve battery power.";
            new_line;
          }
          return Ruins;
        ],
        before [;
            Smell:
              if (noun == nothing) "It smells like something crept in here to die and now it's rotting somewhere in the dark. The flesh decomposes while the maggots gorge themselves on it. It's hard to keep the contents of your stomach down. And you certainly don't want to find out where that smell is coming from.";
            Listen:
              if (noun == nothing) "As much as you try to focus on the whispers, you don't understand what they are saying. They seem to come from a very dark place, where absent light makes all hope die.";
        ],
        cheap_scenery
          14 'musty' 'air' 'smell' 'of' 'decay'
          [;
            Examine, Smell:
              <<Smell>>;
          ]
          22 'cobwebbed' 'forgotten' 'corners' 'debris'
          [;
            Examine, Rub:
              "Even extensive cleaning would be hopeless. Disgusting.";
          ]
          11 'strange' 'symbols' "The symbols are causing you a deep sense of unease and discomfort. What has happened here? Who etched them into the walls? And what is their purpose?"
          1 'whispers'
          [;
            Examine:
              "You hear them but you can't see where they are coming from.";
            Listen:
              <<Listen>>;
          ]
          CS_ADD_LIST Cellar (cheap_scenery_pt2),
        cheap_scenery_pt2
          32 'old' 'wooden' 'rusted' 'barrels' 'tools'
          [;
            Examine, Search:
              "Just old crap. You shouldn't waste time with it.";
          ]
          21 'narrow' 'creaky' 'stairs'
          [;
            Examine:
              "The stairs are probably the best thing about this godforsaken cellar. You can't wait to get out of here.";
            Go, Enter:
              <<Go FAKE_U_OBJ>>;
          ]
          ,
   has ~light ;

Object -> ancienttome "ancient tome",
  with name 'mysterious' 'ancient' 'tome' 'book' 'de' 'vermis' 'mysteriis',
    article "an",
    initial "In a corner is a dark alcove where a mysterious ancient tome glimmers in the light of your torch.",
    description "The title is ~De Vermis Mysteriis~. It seems to be written partly in Latin and partly in a runic language you don't know. In fact, you have heard of this book. It was written by Ludvig Prinn, a Belgian necromancer who was captured and executed for heresy during the 9th Crusade in 1271. The book was based on rites and incantations that Prinn learned from Shaitan cultists in ancient Syria. There were a few transcripts, but they are said to be all destroyed by the church. Could this be Prinn's original book?",
    before [;
        Open:
          "You leaf through the tome a little, then close it again. You can't understand everything written in it, the unknown runic language remains a mystery to you, but some of the drawings next to the runes are immensely disturbing.";
        Burn:
          if (second == nothing) "With what do you want to burn the tome?";
          if (second ~= lighter or candles) "You cannot burn the tome with this object.";
          if (second == lighter && lighter hasnt on) "The lighter is not lit.";
          if (leadcoffin.countdown == 2) "You try to ignite the tome but Ysabella, standing behind you, takes it away from you. It would probably have been better to distract her with something first.";
          if (leadcoffin.countdown ~= 1) "Let's assume for a moment that Lord Bain is not batshit crazy or bloody mental as people in this part of the world would say. If that really is the case, it's not right time to burn the tome now.";
          move ancienttome to Limbo;
		      scope_modified = true;
          StopDaemon(leadcoffin);
          print "Within a second, the ancient tome catches fire. Startled by the intensity of the flames, you drop it on the ground.^";
          Choice_Ending_Neutral = false;
          Choice_Ending_Good = true;
          EndingSequence(); rtrue;
    ],
  has ;

Object -> cellarentrance "cellar entrance",
  with name 'cellar' 'entrance' 'blackest' 'darkness',
    initial "A cellar entrance leads further down into the blackest darkness.",
    description "You can probably imagine how reluctant I am to go down there. Great, I'm already talking to myself. This won't end well.",
    before [;
        Enter:
          <<Go FAKE_D_OBJ>>;
    ],
  has static;

Outside OverGrownCemetery "Overgrown Graveyard"
  with name 'overgrown' 'graveyard',
        description [;
            print "The winter sun casts a pale light over the gravestones, some standing upright and others toppled over. Ivy vines creep over the once sturdy frame of the rusted entrance gate. The silence is so complete that you can hear your own breathing, and every footstep you take seems to echo through the stillness. From here, you can only head south and return to the ruins.^";
            if (self hasnt visited) print "^The voice whispers to you: Come to me. You can wear the amulet now.^";
        ],
        s_to [;
          if (Ritual_started == true) "You can't just walk away now while the ritual is not completed.";
          return Ruins;
        ],
        cheap_scenery
            12 'sinister' 'surface' 'markings'
            [;
              Examine:
                if (gravestone in OverGrownCemetery) "What the hell is this?";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            3 'cracks' 'erosion' 'decay'
            [;
                if (gravestone in OverGrownCemetery) "These cracks remind you that everything is destined to fade. It is unbelievable that the gravestone is still standing after hundreds of years.";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            21 'sickly' 'green' 'hue'
            [;
                if (leadcoffin in OverGrownCemetery) "It is fascinating and repulsive in equal measure.";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            55 'twisted' 'distorted' 'grotesque' 'macabre' 'ornate' 'engravings' 'patterns' 'symbols' 'scenes' 'faces'
            [;
                if (leadcoffin in OverGrownCemetery) "Why would someone engrave such disturbing scenes on a coffin?";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            CS_ADD_LIST OverGrownCemetery (cheap_scenery_pt2),
        cheap_scenery_pt2
            13 'pale' 'winter' 'sun' 'light' "The sun is not very strong here, but it's not only that. Somehow this graveyard seems to devour the light."
            75 'silver' 'lustrous' 'hollow' 'sorrowful' 'pale' 'tattered' 'flowing' 'gown' 'skin' 'eyes' 'hair' 'face' "There is something wrong with her. Yes, she is a ghost, but there is more to it than that."
            12 'of' 'signs' 'corrosion'
            [;
                if (leadcoffin in OverGrownCemetery) "The corrosion is not severe. Considering how long the lead coffin has been in the ground, it is still in a surprisingly good shape.";
                print_ret (string)MSG_PARSER_NOSUCHTHING; 
            ]
            CS_ADD_LIST OverGrownCemetery (cheap_scenery_pt3),
        cheap_scenery_pt3
            3 'entrance' 'gate' 'frame'
            [;
              Examine:
                "So many centuries have passed and yet this gate still stands as a steadfast guardian, separating the world of the living from the world of the dead.";
              Open, Close:
                "Since the gate is half-broken, it makes no sense to open or close it. One can pass it without any problems.";
              Enter, Go:
                "You are already at the graveyard. In case you want to leave, go south instead.";
            ]
            ,
  has light ;

Object -> gravestones "gravestones",
  with parse_name [w n a len;
          n = wn;
          w = NextWord();
            if (w == 'gravestones') {
              a = WordAddress(n);
              len = WordLength(n);
              if (len == 11 && a->10 == 's')
              return 1;
            }
            if (w == 'graves') return 1;
            if (w == 'inscriptions') return 1;
            if (w == 'stones') return 1;
    ],
    description "You can make out some of the names and dates carved into the stone. It seems like nobody's been buried here after 1597, which gives you an idea about how long the village is abandoned. ",
    before [;
      Examine:
        rfalse;
      Smell:
        "Walking around, sniffing graves... is that what you enjoy? You have a sick mind.";
      default:
        print_ret (string)MESS_SCENERY;
    ],
  has pluralname scenery;

Object -> salt "salt",
  with name 'crystalline' 'powder' 'salt' 'white',
    initial "Someone spilled a line of white crystalline powder in front of the cell door.",
    description "You've read about this in one of the books here in Blackwood. Salt can stop ghosts and evil entities from entering a room if one simply spills a line of it in front of all entry points, like windows or doors. It can also be poured or spilled into a circle around a victim to create an area of protection that keeps spirits from attacking. A ghost can also be encircled by salt to prevent it from escaping, much like a devil's trap.",
    before [;
      Take:
        if (salt in leadcoffin) "The salt has been poured into the lead coffin. It's impossible to take it again.";
        "You cannot loosely add this to your inventory. You need something to put it in.";
      Insert, Fill: 
        if (~~ second == leadcoffin) "That's not a good object to put the salt in.";
        if (second == leadcoffin) {
          if (leadcoffin.countdown ~= 2) "You might have a good idea but the timing is not right.";
          move salt to leadcoffin;
		      scope_modified = true;
          print "You pour the salt into the leadcoffin. The thing inside, which you are now certain is not human, twitches and writhes in pain as bloodcurdling screams alternate with guttural cries, more demonic and evil than anything you have ever heard. The eyes are fixed on you, vicious and hateful, not the eyes of a child, but those of a reptile. Like twin orbs of malevolence, they flicker with an otherworldly intensity, possessing an unsettling depth, as if peering into them could reveal the darkest secrets of the universe.^^The irises, a chilling shade of crimson, seem to pulsate with an ominous energy that sends a shiver down your spine. Within those crimson irises, you can discern a twisted reflection of your own fears and doubts, distorted and amplified. It's as if these eyes, which you understand now can only be the eyes of the Antichrist, have the power to see through your very soul, exposing your vulnerabilities and exploiting them for sinister purposes.^^As you try to look away, you notice the faintest hint of a maleficent spark, like dying embers of a wicked flame, conveying a sense of ancient knowledge, an awareness of the secrets that lie beyond mortal comprehension. In these depths, you can almost perceive the echoes of countless souls who have fallen under the Antichrist's sway.^^Ysabella yells at you: No! Why did you do that?^^Suddenly, a low howl begins to rise, growing in intensity. A strong wind whips through the ancient graveyard, rustling the tangled branches of the surrounding forest. The wind carries a haunting melody, as if a thousand voices cry in pain.";
          new_line; rtrue;
        }
    ]
  has proper ;

[EndingSequence;
  if (Choice_Ending_Neutral == true) print "^You turn around and look at Ysabella but what you see is not what you expected. She has changed and no longer appears in the translucent form of a ghost. You stand before a being that emanates an aura of profound eeriness. Her form is an unsettling blend of ethereal grace and twisted malevolence that defies comprehension and challenges your sanity. Her flesh is a sickly pallor, marked with pulsating veins that writhe beneath her ash-gray skin.^^You: Who are you?^Her lips, full and seductive, curve into an enigmatic smile that speaks of both allure and danger: Oh, like I already said, I'm just a loving mother. I have been given many names since I walk the earth. Feel free to choose one. I prefer Morning Star or Light Bearer.^^Her eyes, devoid of colour and depth, glow with an eldritch light that seems to penetrate you. They follow you with an unsettling emptiness, like two windows into a void that devours all hope and joy. Jagged obsidian horns curl menacingly from her forehead, casting sinister shadows across her twisted face. When she moves, her wings unfurl with grotesque grace, their desecrated feathers resembling shredded flesh. Each feather drips with an insidious darkness, oozing a viscous ichor. They writhe and twitch, animated by an evil energy, whispering in tongues that echo with the agonised cries of the damned.^^You try to run away but you are petrified. Lucifer is slowly making her way towards you. Then she is very close.^^Lucifer: I think I haven't introduced you properly to my son yet?^Ice-cold hands wrap around your neck from behind, choking you mercilessly. The world around you turns into a sea of deepest blackness as your screams echo through the forest.^^Cora: Thomas, wake up!^You open your eyes and recognise your wife, bent over you, looking at you anxiously.^^You: Cora? How did you find me in the forest?^Cora: Forest? I don't know what you're talking about. You had a bad dream. I heard you scream when I came through the door. You're obviously a little confused, so let me help you. It's the 23rd of December 1986 and I just came back from Inverness. Apparently you were waiting for me and put on some music here in the lounge. And then you fell asleep.^^You: Wow, I just had the worst nightmare of my life. It felt so real.^Cora: Please tell me everything about it. Let's talk tomorrow. It is already very late. I'm going to bed. Are you coming?^You: Yes, I'll be with you in a moment.^^Drowsy, you go to the bathroom and wash yourself with cold water. Your gaze wanders to the mirror and with horror you recognise the strangulation marks on your neck. It was just a dream, you say to yourself. Just a dream. Wasn't it?";
  if (Choice_Ending_Good == true) print "^The Antichrist's haunting screams ceased the very moment the ancient tome began to burn. Then you realise why. Wisps of smoke rise from the lead coffin, twisting and curling as if driven by an otherworldly force. A haunting wind howls through the graveyard, carrying with it the scent of burning flesh, a faint, unsettling odor. You approach the coffin and your heart races as you observe the macabre scene inside. The unholy boy's features are twisted in an expression of eternal torment. Flames dance mercilessly across his form, leaping in a grotesque ballet, consuming what little is left of his earthly vessel. His eyes, empty sockets now, seem to pierce through the veil between worlds, staring into the evil void from which he came.^^You turn slowly and Ysabella stands before you. Her eyes, pools of darkness, seem to hold both the weight of forgotten ages and the fire of a thousand stars. Time bends around her, and with a single heartbeat, the world transforms. The air grows even colder, and the icy breeze cuts with rage through the graveyard, carrying with it a mournful symphony of whispers and sighs. Before your very eyes, Ysabella shifts and twists into a portrait of malice, a visage that echoes the primordial terror of mankind's deepest nightmares. Gone is the semblance of elegance. The pure evil that emanates from her is palpable, a noxious miasma that curls and coils around your senses, a suffocating embrace that fills the air with impending doom.^^It is then, in that chilling moment, that realization dawns like a jagged lightning bolt. The woman before you is not merely a specter of the past, nor a mere haunt of this forsaken ground. No, she is something far more ancient, far more insidious. Lucifer, the Morning Star, the embodiment of all that is forbidden and accursed. Her lips part, and a sound emerges, a voice that seems to reverberate not only in the confines of your mind but also in the very marrow of your bones.^^Lucifer: You will regret this.^She hisses, her words both a venomous caress and a sinister threat that echoes through the forest. A brief interlude that seems to last for aeons until she vanishes before you.^^And then you see him. The ghost of an old man who apparently watched your confrontation with Lucifer and her offspring, bathed in a faint, ethereal glow, emerging from the halo above his head.^^Cuthbert: You have done well, my son. I am Cuthbert of Lindisfarne.^You: I have so many questions.^^Cuthbert: And I'm afraid I don't have many answers, nor can I linger here for long. Everything happened as it had to happen, both then and now.^He extends a hand towards you.^Cuthbert: I'll take the amulet with me. It has served its purpose.^^You: Oh, the amulet. I understand that I had to find it in order to speak to Lord Bain. Without it, I'm sure I would have made a terrible mistake. But why did Lucifer want me to wear it here too?^Cuthbert: The Morning Star is a master of deception. In fact, the amulet was also an important component of her unholy ritual. Only a thin veil separates this world from other planes of existence. And the amulet is a key to look behind it. She would have used it to open the gates of hell and plunge the world into chaos forever.^You shiver as you think about it.^^So you remove the amulet and you hold it in your hand for a moment, where it sparkles as it reflects the light of the winter sun. Just a blink of an eye and then everything is gone. The amulet, Cuthbert, the coffin, even the charred remains of the unholy tome.^^As you make your way home, the day begins to fade towards dusk and a sense of calm settles in. The late afternoon sun casts long shadows on the landscape. When you reach the manor's feral garden, the sun inches close to the horizon and bathes the grounds of Blackwood in a warm glow of gold and amber. Snow-capped hills and valleys create a serene panorama, a world untouched by the darkness that had threatened it.";
  deadflag = 2; ! you have won!
];

[ Initialise; ! init vars and start the game

  notify_mode = false; ! deactivate score notifications if set false
  lookmode = 2; ! activate verbose look modethedark

  thedark.description = TheDarkAdvanced;
  thedark.short_name = "In the dark";

  player.before = PlayerBefore; ! player.before override, see routine implementation
  player.parse_name = PlayerParseName; ! own teroutine that parses the player's name
  player.description = "Look at you. The author that came to Scotland to find inspiration. For some reason you feel like being stuck in a bad story now.";
];

[ TheDarkAdvanced;
  if (lighter has on) "The light of the lighter is unfortunately too weak to illuminate this location. And so you still stand here surrounded by darkness, likely to be devoured by a demon or some other soulless malevolence.";
  "You are surrounded by darkness. Some nastiness might drag you into the depths of hell.";
];

[ CreditsSub;
  print (string)Story, " is dedicated to Andreas Gustafsson.";
  print "^^~Night gathers, and now my watch begins. For this night and all the nights to come.~^^-- George R.R. Martin^   A Song of Ice and Fire";
  "^^Beta ghost hunters: Ant Stiller, Marco Innocenti, Soenke Wolfgramm, Fredrik Ramsberg, Johan Berntsson, Shawn Sijnstra.^^This game was created with PunyInform, a custom library by Fredrik Ramsberg and Johan Berntsson for Graham Nelson's Inform language. Parser grammar and library messages partly adapted from Dialog by Linus Akesson.";
];

[ XyzzySub;
  "You perceive a mysterious whisper. Coming from everywhere and nowhere, yet present, it says ~Nice try.~";
];

[ PlayerBefore;
    Turn:
      "Now you feel dizzy. You've had better ideas than that.";
    Attack, Cut:
      if (PROGRESS_LEVEL == 14 && second == nothing) "Looks like you want to cut yourself with something. Try being more specific.";
      if (PROGRESS_LEVEL == 14 && second == knife) {
        if (blood in knife) {
          move blood to location;
		      scope_modified = true;
          "You cut yourself with the box cutter. Just a little. Some blood drips onto the ground. The wound stops bleeding quickly.";
        }
        else "Once is enough. If you still have the urge to cut yourself, you should see a therapist.";
      }
      else "As hopeless as the situation may be, this is never a way out.";
    default:
      rfalse;
];

[ PlayerParseName n;
		n=0;
		while (NextWord()=='me' or 'myself' or 'self' or 'yourself' or 'you' or 'Thomas') {n++;}
		return n;
];

[ CallSub;
  "There must be telephone nearby to call someone.";
];

! game-specific grammar starts here
Verb 'call' * -> Call
            * noun -> Call
            * special -> Call;

Extend 'go' * 'to' noun -> Go;

Verb 'extinguish' * noun -> SwitchOff;

Verb 'ring' = 'call';
Verb 'dial' = 'call';
Verb 'play' = 'switch';
Verb 'hear' = 'listen';
Verb 'taste' = 'eat';

end;
